/*****************************************************************************
 * SysML 2 Pilot Implementation
 * Copyright (c) 2018, 2020 Model Driven Solutions, Inc.
 * Copyright (c) 2018 IncQuery Labs Ltd.
 * Copyright (c) 2019 Maplesoft (Waterloo Maple, Inc.)
 * Copyright (c) 2019 Mgnite Inc.
 *    
 * This program is free software: you can redistribute it and/or modify
 * it under the terms of the GNU Lesser General Public License as published by
 * the Free Software Foundation, either version 3 of the License, or
 * (at your option) any later version.
 *
 * This program is distributed in the hope that it will be useful,
 * but WITHOUT ANY WARRANTY; without even the implied warranty of
 * MERCHANTABILITY or FITNESS FOR A PARTICULAR PURPOSE.  See the
 * GNU Lesser General Public License for more details.
 *
 * You should have received a copy of theGNU Lesser General Public License
 * along with this program.  If not, see <https://www.gnu.org/licenses/>.
 *
 * @license LGPL-3.0-or-later <http://spdx.org/licenses/LGPL-3.0-or-later>
 * 
 * Contributors:
 *  Ed Seidewitz, MDS
 *  Zoltan Kiss, IncQuery
 *  Balazs Grill, IncQuery
 *  Hisashi Miyashita, Maplesoft/Mgnite
 * 
 *****************************************************************************/

grammar org.omg.kerml.xtext.KerML hidden (WS, ML_NOTE, SL_NOTE)

import "http://www.eclipse.org/emf/2002/Ecore" as Ecore
import "http://www.omg.org/spec/SysML/2.0" as SysML

/* ROOT NAMESPACE */

RootNamespace returns SysML::Namespace :
	{SysML::Namespace}NamespaceBodyElement*
;

/* ELEMENTS */

/* Elements */

fragment Identification returns SysML::Element :
	  'id' humanId = Name ( name = Name )?
	| name = Name
;

Element returns SysML::Element :
    'element' Identification? ElementBody
;

fragment ElementBody returns SysML::Element :
    ';' | '{' OwnedElement* '}'
;

fragment OwnedElement returns SysML::Element :
      ownedRelationship_comp += OwnedRelationship
    | documentation_comp += OwnedDocumentation
    | ownedAnnotation_comp += OwnedTextualRepresentationAnnotation
    | ownedAnnotation_comp += OwnedAnnotatingFeatureAnnotation
;

/* Relationships */

Relationship returns SysML::Relationship :
    'relationship' Identification?
    RelationshipRelatedElements
    RelationshipBody
;

OwnedRelationship returns SysML::Relationship :
    'relationship' Identification?
    'to' RelationshipTargetList
    RelationshipBody
;

fragment RelationshipRelatedElements returns SysML::Relationship :
      'from' RelationshipSourceList ( 'to' RelationshipTargetList )?
    | 'to' RelationshipTargetList
;

fragment RelationshipSourceList returns SysML::Relationship :
    RelationshipSource ( ',' RelationshipSource )*
;

fragment RelationshipSource returns SysML::Relationship :
    source += [SysML::Element | QualifiedName]
;

fragment RelationshipTargetList returns SysML::Relationship :
    RelationshipTarget ( ',' RelationshipTarget )*
;

fragment RelationshipTarget returns SysML::Relationship :
    target += [SysML::Element | QualifiedName]
;

fragment RelationshipBody returns SysML::Relationship :
    ';' | '{' RelationshipOwnedElement* '}'
;

fragment RelationshipOwnedElement returns SysML::Relationship:
      ownedRelatedElement_comp += OwnedRelatedElement
    | documentation_comp += OwnedDocumentation
    | ownedAnnotation_comp += OwnedTextualRepresentationAnnotation
;

OwnedRelatedElement returns SysML::Element :
      'element' ( humanId = Name )? ElementBody
    | OwnedRelatedRelationship
;

OwnedRelatedRelationship returns SysML::Relationship :
	'relationship' ( humanId = Name )? RelationshipBody
;

/* ANNOTATIONS */

/* Comments */

Comment returns SysML::Comment :
	( 'comment' Identification? 'about' ownedRelationship_comp += Annotation
	  ( ',' ownedRelationship_comp += Annotation )*
	| ( 'comment' Identification? )? ownedRelationship_comp += EmptyAnnotation
	)
	body = REGULAR_COMMENT
;

Annotation returns SysML::Annotation :
	annotatedElement = [SysML::Element|QualifiedName]
;

EmptyAnnotation returns SysML::Annotation :
	{SysML::Annotation}
;

/* Documentation */

OwnedDocumentation returns SysML::Documentation :
	documentingComment_comp = DocumentationComment
;

DocumentationComment returns SysML::Comment :
	'doc' ( 'id' humanId = Name )? body = REGULAR_COMMENT
;

PrefixDocumentation returns SysML::Annotation :
	ownedRelatedElement_comp += PrefixDocumentationComment
;

PrefixDocumentationComment returns SysML::Comment :
	( 'doc' ( 'id' humanId = Name )? )? body = DOCUMENTATION_COMMENT
;

/* Textual Representation */

OwnedTextualRepresentationAnnotation returns SysML::Annotation :
	ownedRelatedElement_comp += OwnedTextualRepresentation
;

OwnedTextualRepresentation returns SysML::TextualRepresentation :
	( 'rep' ( humanId = Name )? )? 
	'language' language = STRING_VALUE body = REGULAR_COMMENT
	ownedRelationship_comp += EmptyAnnotation
;

TextualRepresentation returns SysML::TextualRepresentation :
    ( 'rep' Identification? 'about' ownedRelationship_comp += Annotation
    | ( 'rep' Identification? )? ownedRelationship_comp += EmptyAnnotation
    )
    'language' language = STRING_VALUE 
    body = REGULAR_COMMENT
;

/* Annotating Features */

OwnedAnnotatingFeatureAnnotation returns SysML::Annotation :
	ownedRelatedElement_comp += OwnedAnnotatingFeature
;

OwnedAnnotatingFeature returns SysML::AnnotatingFeature :
	'metadata' ( humanId = Name ( ':' | 'typed' 'by' ) )? ownedRelationship_comp += metadataTyping 
	ownedRelationship_comp += EmptyAnnotation 
	AnnotatingFeatureBody
;

AnnotatingFeature returns SysML::AnnotatingFeature :
	( '@' | 'metadata' ) AnnotatingFeatureDeclaration 
	( 'about' ownedRelationship_comp += Annotation ( ',' ownedRelationship_comp += Annotation )* 
	| ownedRelationship_comp += EmptyAnnotation
	)	
	AnnotatingFeatureBody
;

fragment AnnotatingFeatureDeclaration returns SysML::AnnotatingFeature :
	( Identification ( ':' | 'typed' 'by' ) )? ownedRelationship_comp += metadataTyping
;

metadataTyping returns SysML::FeatureTyping :
	type = [SysML::DataType | QualifiedName]
;

fragment AnnotatingFeatureBody returns SysML::AnnotatingFeature :
	';' | '{' ( ownedFeatureMembership_comp += MetadataFeatureMember )* '}'
;

MetadataFeatureMember returns SysML::FeatureMembership :
	ownedMemberFeature_comp = MetadataFeature
;

MetadataFeature returns SysML::MetadataFeature :
	'feature'? ( ':>>' | 'redefines')? ownedRelationship_comp += OwnedRedefinition 
	'=' metadataFeatureValue_comp = MetadataFeatureValue ';'
;

MetadataFeatureValue returns SysML::MetadataFeatureValue :
	metadataValue_comp = OwnedExpression
;

/* NAMESPACES */

Namespace returns SysML::Namespace :
	NamespaceDeclaration NamespaceBody
;

fragment NamespaceDeclaration returns SysML::Namespace :
	'namespace' Identification?
;

fragment NamespaceBody returns SysML::Namespace :
	  ';' 
	| '{' ( // Note: PackageBodyElement is expanded here to avoid
			// infinite loops in the incremental parser.
		    documentation_comp += OwnedDocumentation
	      | ownedMembership_comp += NamespaceMember 
	      | ownedImport_comp += Import )* 
	  '}'
;

/* Namespace Bodies */

fragment NamespaceBodyElement returns SysML::Namespace :
	  documentation_comp += OwnedDocumentation
	| ownedMembership_comp += NamespaceMember 
	| ownedImport_comp += Import
;

NamespaceMember returns SysML::Membership : 
	( ownedRelationship_comp += PrefixDocumentation )*
    ( visibility = BasicVisibilityIndicator )?		
	( NonFeatureNamespaceMember | FeatureNamespaceMember )
;

fragment NonFeatureNamespaceMember returns SysML::Membership :
	  ownedMemberElement_comp = NonFeatureElement
	| ( 'alias' | 'import' ) memberElement = [SysML::Element|QualifiedName] ( 'as' memberName = Name )? ';'
;

fragment FeatureNamespaceMember returns SysML::Membership :
	  ownedMemberElement_comp = FeatureElement
;

Import returns SysML::Import :
	( ownedRelationship_comp += PrefixDocumentation )*
	( visibility = BasicVisibilityIndicator )?  
	'import' ( ImportedNamespace | ImportedFilterPackage ) ';'
;

fragment ImportedNamespace returns SysML::Import :
	(
	  importedNamespace = [SysML::Namespace|Name] ( '::' | '.' ) 
	| importedNamespace = [SysML::Namespace|ColonQualifiedName] '::' 
	| importedNamespace = [SysML::Namespace|DotQualifiedName] '.'
	)
	( '*' | isRecursive ?= '**' )
;

fragment ImportedFilterPackage returns SysML::Import :
	ownedRelatedElement_comp += FilterPackage
;

FilterPackage returns SysML::Package :
	ownedImport_comp += FilterPackageImport
	( ownedMembership_comp += FilterPackageMember )+
;

FilterPackageImport returns SysML::Import :
	ImportedNamespace
;

FilterPackageMember returns SysML::ElementFilterMembership :
	visibility = FilterPackageMemberVisibility condition_comp = OwnedExpression ']'
;

enum FilterPackageMemberVisibility returns SysML::VisibilityKind :
	private = '['
;

enum BasicVisibilityIndicator returns SysML::VisibilityKind:
	public = 'public' | private = 'private'
;

/* Namespace Elements */

NonFeatureElement returns SysML::Element :
	  Element
	| Relationship
	| Comment
	| TextualRepresentation
	| AnnotatingFeature
	| Namespace
	| Package 
	| Type
	| Classifier 
	| Class
	| Structure
	| DataType 
	| Association
	| AssociationStructure
	| Interaction
	| Behavior
	| Function
	| Predicate
	| Generalization
	| Conjugation
	| FeatureTyping
	| Superclassing
	| Subsetting
	| Redefinition
	| TypeFeaturing
;

FeatureElement returns SysML::Feature :
	  Feature
	| Step
	| Expression
	| BooleanExpression
	| Invariant
	| Connector
	| BindingConnector
	| Succession
	| ItemFlow
	| SuccessionItemFlow
;

/* PACKAGES */

Package returns SysML::Package :
	PackageDeclaration PackageBody 
;

fragment PackageDeclaration returns SysML::Package :
	'package' Identification?
;

fragment PackageBody returns SysML::Namespace :
	  ';' 
	| '{' ( // Note: PackageBodyElement is expanded here to avoid
			// infinite loops in the incremental parser.
		    documentation_comp += OwnedDocumentation
	      | ownedMembership_comp += NamespaceMember 
	      | ownedMembership_comp += ElementFilterMember
	      | ownedImport_comp += Import )*
	  '}'
;

ElementFilterMember returns SysML::ElementFilterMembership :
	( ownedRelationship_comp += PrefixDocumentation )*
    ( visibility = BasicVisibilityIndicator )?
	'filter' condition_comp = OwnedExpression ';'
;

/* TYPES */

/* Types */

Type returns SysML::Type :
    ( isAbstract ?= 'abstract' )? 'type' 
    TypeDeclaration TypeBody
;

fragment TypeDeclaration returns SysML::Type : 
    ( isSufficient ?= 'all' )? Identification?
    ( ownedMembership_comp += MultiplicityMember )? 
    ( SpecializationPart | ConjugationPart )+
;

fragment SpecializationPart returns SysML::Type :
      ( ':>' | 'specializes' ) ownedRelationship_comp += OwnedGeneralization
      ( ',' ownedRelationship_comp += OwnedGeneralization )*
;

fragment ConjugationPart returns SysML::Type :
	( '~' | 'conjugates' ) ownedRelationship_comp += OwnedConjugation
;

MultiplicityMember returns SysML::Membership :
	ownedMemberElement_comp = Multiplicity
;

fragment TypeBody returns SysML::Type :
	';' | 
	'{' ( documentation_comp += OwnedDocumentation
		| ownedMembership_comp += NonFeatureTypeMember 
		| ownedFeatureMembership_comp += FeatureTypeMember 
		| ownedImport_comp += Import
	)* '}'
;

NonFeatureTypeMember returns SysML::Membership : 
	TypeMemberPrefix NonFeatureNamespaceMember
;

FeatureTypeMember returns SysML::FeatureMembership : 
	FeatureMember | EndFeatureMember
;

fragment TypeMemberPrefix returns SysML::Membership :
	( ownedRelationship_comp += PrefixDocumentation )*
    ( visibility = VisibilityIndicator )?
;

enum VisibilityIndicator returns SysML::VisibilityKind :
	public = 'public' | private = 'private' | protected = 'protected' | package = 'packaged'
;

/* Generalization */

Generalization returns SysML::Generalization :
    ( 'generalization' Identification? )?
    'subtype' specific = [SysML::Type | QualifiedName]
    ( ':>' | 'specializes') general = [SysML::Type | QualifiedName] ';'
;

OwnedGeneralization returns SysML::Generalization :
    general = [SysML::Type | QualifiedName]
;

/* Conjugation */

Conjugation returns SysML::Conjugation :
	'conjugation' Identification? 
    'type' conjugatedType = [SysML::Type | QualifiedName] 
    ( '~' | 'conjugates') originalType = [SysML::Type | QualifiedName] ';'
;

OwnedConjugation returns SysML::Conjugation :
	originalType = [SysML::Type | QualifiedName]
;

/* Feature Membership */

FeatureMember returns SysML::FeatureMembership :
	TypeMemberPrefix FeatureMemberFlags 
	( ownedMemberFeature_comp = FeatureElement
	| 'feature'? ( memberName = Name )? 'is' memberFeature = [SysML::Feature|QualifiedName] ';'
	)
;

EndFeatureMember returns SysML::EndFeatureMembership :
	TypeMemberPrefix 'end' FeatureMemberFlags ownedMemberFeature_comp = FeatureElement
;

fragment FeatureMemberFlags returns SysML::FeatureMembership :
	( direction = FeatureDirection )? 
	( isComposite ?= 'composite' | isPortion ?= 'portion' )? 
	( isPort ?= 'port' )?
;

enum FeatureDirection returns SysML::FeatureDirectionKind:
	in = 'in' | out = 'out' | inout = 'inout'
;

/* CLASSIFIERS */

/* Classifiers */

Classifier returns SysML::Classifier :
	( isAbstract ?= 'abstract' )? 'classifier' 
	ClassifierDeclaration TypeBody
;

fragment ClassifierDeclaration returns SysML::Classifier :	
    (isSufficient ?= 'all' )? Identification?
	( ownedMembership_comp += MultiplicityMember )?
	( SuperclassingPart | ClassifierConjugationPart )?
;

fragment SuperclassingPart returns SysML::Classifier :
	( ':>' | 'specializes' ) ownedRelationship_comp += OwnedSuperclassing 
	( ',' ownedRelationship_comp += OwnedSuperclassing )*
;

fragment ClassifierConjugationPart returns SysML::Classifier :
	( '~' | 'conjugates' ) ownedRelationship_comp += ClassifierConjugation
;

/* Superclassing */

Superclassing returns SysML::Superclassing :
	( 'generalization' Identification? )?
    'subclass' subclass = [SysML::Classifier | QualifiedName]
    ( ':>' | 'specializes') superclass = [SysML::Classifier | QualifiedName] ';'
;

OwnedSuperclassing returns SysML::Superclassing :
	superclass = [SysML::Classifier | QualifiedName]
;

/* Classifier Conjugation */

ClassifierConjugation returns SysML::Conjugation :
	originalType = [SysML::Classifier | QualifiedName]
;

/* FEATURES */

/* Features */

Feature returns SysML::Feature :
	( isAbstract ?= 'abstract' )? 
	( 'feature'? FeatureDeclaration | 'feature' ) 
	ValuePart? TypeBody
;

fragment FeatureDeclaration returns SysML::Feature :
	( isSufficient ?= 'all' )? 
	( Identification ( FeatureSpecializationPart | FeatureConjugationPart )?
	| FeatureSpecializationPart
	| FeatureConjugationPart
	) TypeFeaturingPart?
;

fragment TypeFeaturingPart returns SysML::Feature :
	'featured' 'by' ownedRelationship_comp += OwnedTypeFeaturing 
	( ',' ownedRelationship_comp += OwnedTypeFeaturing )*
;

fragment FeatureSpecializationPart returns SysML::Feature :
	  ( -> FeatureSpecialization )+ MultiplicityPart? FeatureSpecialization*
	| MultiplicityPart FeatureSpecialization*
;

fragment MultiplicityPart returns SysML::Feature :
	ownedMembership_comp += MultiplicityMember 
	// NOTE: Can't use "&" here because of use of predication elsewhere.
	( isOrdered ?= 'ordered' isNonunique ?= 'nonunique'?
	| isNonunique ?= 'nonunique' isOrdered ?= 'ordered'?
	)?
;

fragment FeatureSpecialization returns SysML::Feature :
	Typings | Subsettings | Redefinitions
;

fragment Typings returns SysML::Feature :
	TypedBy ( ',' ownedRelationship_comp += OwnedFeatureTyping )*
;

fragment TypedBy returns SysML::Feature :
	( ':' | 'typed' 'by' ) ownedRelationship_comp += OwnedFeatureTyping
;

fragment Subsettings returns SysML::Feature :
	Subsets ( ',' ownedRelationship_comp += OwnedSubsetting )*
;

fragment Subsets returns SysML::Feature :
	( ':>' | 'subsets' ) ownedRelationship_comp += OwnedSubsetting 
;

fragment Redefinitions returns SysML::Feature :
	Redefines ( ',' ownedRelationship_comp += OwnedRedefinition )*
;

fragment Redefines returns SysML::Feature :
	( ':>>' | 'redefines' ) ownedRelationship_comp += OwnedRedefinition
;

/* Type Featuring */

TypeFeaturing returns SysML::TypeFeaturing :
	'featuring' ( Identification? 'of')?
	featureOfType = [SysML::Feature | QualifiedName]
	'by' featuringType = [SysML::Feature | QualifiedName] ';'
;

OwnedTypeFeaturing returns SysML::TypeFeaturing :
	featuringType = [SysML::Type | QualifiedName]
;

/* Feature Typing */

FeatureTyping returns SysML::FeatureTyping :
	( 'generalization' Identification? )?
    'typing' typedFeature = [SysML::Feature | QualifiedName]
    (':' | 'typed' 'by') type = [SysML::Feature | QualifiedName] ';'
;

OwnedFeatureTyping returns SysML::FeatureTyping :
	type = [SysML::Type | QualifiedName]
;

/* Subsetting */

Subsetting returns SysML::Subsetting :
	( 'generalization' Identification? )?
    'subset' subsettingFeature = [SysML::Feature | QualifiedName]
    ( ':>' | 'subsets' ) subsettedFeature = [SysML::Feature | QualifiedName] ';'
;

OwnedSubsetting returns SysML::Subsetting:
	subsettedFeature = [SysML::Feature|QualifiedName]
;

/* Redefinition */

Redefinition returns SysML::Redefinition :
	( 'generalization' Identification? )?
    'redefinition' redefiningFeature = [SysML::Feature | QualifiedName]
    ( ':>>' | 'redefines' ) redefinedFeature = [SysML::Feature | QualifiedName] ';'
;

OwnedRedefinition returns SysML::Redefinition:
	redefinedFeature = [SysML::Feature | QualifiedName] 
;

/* Feature Conjugation */

fragment FeatureConjugationPart returns SysML::Feature :
	( '~' | 'conjugates' ) ownedRelationship_comp += FeatureConjugation
;

FeatureConjugation returns SysML::Conjugation :
	originalType = [SysML::Feature | QualifiedName ]
;

/* FEATURE VALUES */

fragment ValuePart returns SysML::Feature :
	'=' ownedMembership_comp += FeatureValue
;

FeatureValue returns SysML::FeatureValue :
	value_comp = OwnedExpression
;

/* MULTIPLICITIES */

Multiplicity returns SysML::MultiplicityRange :
	// TODO: Allow general expressions for bounds. (Causes LL parsing issues.)
	'[' ( ownedMembership_comp += LiteralIntegerMember '..' )? 
	      ownedMembership_comp += LiteralUnlimitedNaturalMember ']'
;

LiteralIntegerMember returns SysML::Membership :
	ownedMemberElement_comp = LiteralInteger
;

LiteralUnlimitedNaturalMember returns SysML::Membership :
	ownedMemberElement_comp = LiteralUnlimitedNatural
;

/* CLASSIFICATION */

/* Data Types */

DataType returns SysML::DataType :
	( isAbstract ?= 'abstract' )? 'datatype' 
	ClassifierDeclaration TypeBody
;

/* Classes */

Class returns SysML::Class :
	( isAbstract ?= 'abstract' )? 'class' 
	ClassifierDeclaration TypeBody
;

/* STRUCTURES */

Structure returns SysML::Structure :
	( isAbstract ?= 'abstract' )? 'struct' 
	ClassifierDeclaration TypeBody
;


/* ASSOCIATIONS */

Association returns SysML::Association :
	( isAbstract ?= 'abstract' )? 'assoc' 
	ClassifierDeclaration TypeBody
;

AssociationStructure returns SysML::AssociationStructure :
	( isAbstract ?= 'abstract' )? 'assoc' 'struct'
	ClassifierDeclaration TypeBody
;

/* CONNECTORS */

/* Connectors */

Connector returns SysML::Connector :	 
	( isAbstract ?= 'abstract' )? 'connector' 
	ConnectorDeclaration TypeBody	  
;

fragment ConnectorDeclaration returns SysML::Connector :
	BinaryConnectorDeclaration | NaryConnectorDeclaration
;

fragment BinaryConnectorDeclaration returns SysML::Connector :
	( FeatureDeclaration? 'from' | isSufficient ?= 'all' 'from'? )?
	ownedFeatureMembership_comp += ConnectorEndMember 'to' 
	ownedFeatureMembership_comp += ConnectorEndMember
;

fragment NaryConnectorDeclaration returns SysML::Connector :
	FeatureDeclaration?
	'(' ownedFeatureMembership_comp += ConnectorEndMember ',' 
	    ownedFeatureMembership_comp += ConnectorEndMember
	    ( ',' ownedFeatureMembership_comp += ConnectorEndMember )* ')'
;

ConnectorEndMember returns SysML::EndFeatureMembership :
	( memberName = Name '=>' )? ownedMemberFeature_comp = ConnectorEnd
;

ConnectorEnd returns SysML::Feature :
	ownedRelationship_comp += OwnedSubsetting 
	( ownedMembership_comp += MultiplicityMember )?
;

/* Binding Connectors */

BindingConnector returns SysML::BindingConnector :
	( isAbstract ?= 'abstract' )? 'binding' 
	BindingConnectorDeclaration TypeBody	
;

fragment BindingConnectorDeclaration returns SysML::BindingConnector :
	( FeatureDeclaration? 'of' | isSufficient ?= 'all' 'of'? )?
	ownedFeatureMembership_comp += ConnectorEndMember '=' 
	ownedFeatureMembership_comp += ConnectorEndMember
;

/* Successions */

Succession returns SysML::Succession :
	( isAbstract ?= 'abstract' )? 'succession' 
	SuccessionDeclaration TypeBody
;

fragment SuccessionDeclaration returns SysML::Succession :
	( FeatureDeclaration? 'first' | isSufficient ?= 'all' 'first'? )?
	ownedFeatureMembership_comp += ConnectorEndMember 'then' 
	ownedFeatureMembership_comp += ConnectorEndMember
;

/* BEHAVIORS */

/* Behaviors */

Behavior returns SysML::Behavior :
	( isAbstract ?= 'abstract')? 'behavior' 
	BehaviorDeclaration TypeBody
;

fragment BehaviorDeclaration returns SysML::Behavior :
	ClassifierDeclaration ParameterList?
;

fragment ParameterList returns SysML::Type :
	'(' ( ownedFeatureMembership_comp += ParameterMember 
		( ',' ownedFeatureMembership_comp += ParameterMember )* )? ')'
;

ParameterMember returns SysML::ParameterMembership :
	( direction = FeatureDirection )? ownedMemberParameter_comp = Parameter
;

Parameter returns SysML::Feature :
      'feature'? ParameterDeclaration
    | {SysML::Step} 'step' ParameterDeclaration
    | {SysML::Expression} 'expr' ParameterDeclaration
    | {SysML::BooleanExpression} 'bool' ParameterDeclaration
;

fragment ParameterDeclaration returns SysML::Feature :
	( isSufficient ?= 'all' )? 
    ( Identification ParameterSpecializationPart? 
	| ParameterSpecializationPart
	)
;

fragment ParameterSpecializationPart returns SysML::Feature :
	  ( -> ParameterSpecialization )+ MultiplicityPart? ParameterSpecialization*
	| MultiplicityPart ParameterSpecialization*
;

fragment ParameterSpecialization returns SysML::Feature :
	TypedBy | Subsets | Redefines
;

/* Steps */

Step returns SysML::Step :
	( isAbstract ?= 'abstract' )? 'step' 
	StepDeclaration TypeBody
;

fragment StepDeclaration returns SysML::Step :
	FeatureDeclaration? ( ValuePart | StepParameterList )?
;

fragment StepParameterList returns SysML::Step :
	'(' ( ownedFeatureMembership_comp += StepParameterMember
	    ( ',' ownedFeatureMembership_comp += StepParameterMember )* )? ')'
;

StepParameterMember returns SysML::ParameterMembership :
	( direction = FeatureDirection )? ownedMemberParameter_comp = StepParameter
;

StepParameter returns SysML::Feature :
	Parameter ValuePart? 
;

/* FUNCTIONS */

/* Functions */

Function returns SysML::Function :
	( isAbstract ?= 'abstract' )? 'function' 
	FunctionDeclaration FunctionBody  
;

fragment FunctionDeclaration returns SysML::Function :
	ClassifierDeclaration ParameterPart
;

fragment ParameterPart returns SysML::Type :
	  ParameterList ( ReturnParameterPart | EmptyReturnParameterPart )
	| EmptyReturnParameterPart
;

fragment ReturnParameterPart returns SysML::Type :
	ownedFeatureMembership_comp += ReturnParameterMember
;

ReturnParameterMember returns SysML::ReturnParameterMembership :
	ownedMemberParameter_comp = Parameter
;

fragment EmptyReturnParameterPart returns SysML::Type :
	ownedFeatureMembership_comp += EmptyReturnParameterMember
;

EmptyReturnParameterMember returns SysML::ReturnParameterMembership :
	ownedMemberFeature_comp = EmptyParameter
;

EmptyParameter returns SysML::Feature :
	{SysML::Feature}
;

fragment FunctionBody returns SysML::Type :
	  ';'
	| '{' ( documentation_comp += OwnedDocumentation
		  | ownedMembership_comp += NonFeatureTypeMember 
		  | ownedFeatureMembership_comp += FeatureTypeMember
		  | ownedRelationship_comp += Import
		  )* 
		  ( ownedFeatureMembership_comp += ResultExpressionMember )? 
	  '}'
;

ResultExpressionMember returns SysML::ResultExpressionMembership :
	TypeMemberPrefix ownedResultExpression_comp = OwnedExpression
;

/* Expressions */

Expression returns SysML::BlockExpression :
	( isAbstract ?= 'abstract' )? 'expr' 
	ExpressionDeclaration FunctionBody
;

fragment ExpressionDeclaration returns SysML::Expression :
	FeatureDeclaration? 
	( ValuePart | StepParameterList ( ReturnParameterPart | EmptyReturnParameterPart ) )? 
;

/* Predicates */

Predicate returns SysML::Predicate :
	( isAbstract ?= 'abstract' )? 'predicate' 
	PredicateDeclaration FunctionBody  
;

fragment PredicateDeclaration returns SysML::Predicate :
	ClassifierDeclaration ParameterPart
;

/* Boolean Expressions */ 

BooleanExpression returns SysML::BooleanExpression :
	( isAbstract ?= 'abstract' )? 'bool' 
	ExpressionDeclaration FunctionBody
;

/* Invariants */

Invariant returns SysML::Invariant :
	( isAbstract ?= 'abstract' )? 'inv' 
	ExpressionDeclaration InvariantPart FunctionBody
;

fragment InvariantPart returns SysML::Invariant :
	ownedFeatureMembership_comp += TrueLiteralMember
;

TrueLiteralMember returns SysML::FeatureMembership :
	ownedMemberFeature_comp = TrueLiteralExpression
;

TrueLiteralExpression returns SysML::LiteralBoolean :
	{SysML::LiteralBoolean}
;

/* INTERACTIONS */

/* Interactions */

Interaction returns SysML::Interaction :
	( isAbstract ?= 'abstract' )? 'interaction' 
	BehaviorDeclaration TypeBody
;

/* Item Flows */

ItemFlow returns SysML::ItemFlow :
	( isAbstract ?= 'abstract' )? 'stream' 
	ItemFlowDeclaration TypeBody
;

SuccessionItemFlow returns SysML::SuccessionItemFlow :
	( isAbstract ?= 'abstract' )? 'flow' ItemFlowDeclaration TypeBody
;

fragment ItemFlowDeclaration returns SysML::ItemFlow :
	( FeatureDeclaration? 
      ( 'of'  ownedFeatureMembership_comp += ItemFeatureMember
      | ownedFeatureMembership_comp += EmptyItemFeatureMember
      ) 'from'
    | ( isSufficient ?= 'all' )? ownedFeatureMembership_comp += EmptyItemFeatureMember
    )
	ownedFeatureMembership_comp += ItemFlowEndMember 'to' 
	ownedFeatureMembership_comp += ItemFlowEndMember
;

ItemFeatureMember returns SysML::FeatureMembership :
	( memberName = Name ':' )? ownedMemberFeature_comp = ItemFeature
;

ItemFeature returns SysML::ItemFeature :
	  ownedRelationship_comp += OwnedFeatureTyping 
	  ( ownedMembership_comp += MultiplicityMember )?
	| ownedMembership_comp += MultiplicityMember
	  ( ownedRelationship_comp += OwnedFeatureTyping )?
;

EmptyItemFeatureMember returns SysML::FeatureMembership :
	ownedMemberFeature_comp = EmptyItemFeature
;

EmptyItemFeature returns SysML::ItemFeature :
	{SysML::ItemFeature}
;

ItemFlowEndMember returns SysML::EndFeatureMembership :
	ownedMemberFeature_comp = ItemFlowEnd
;

ItemFlowEnd returns SysML::ItemFlowEnd :
	ownedFeatureMembership_comp += ItemFlowFeatureMember
;

ItemFlowFeatureMember returns SysML::FeatureMembership :
	ownedMemberFeature_comp = ItemFlowFeature
;

ItemFlowFeature returns SysML::ItemFlowFeature :
	ownedRelationship_comp += OwnedRedefinition
;

/* EXPRESSIONS */

/* Operator Expressions */ 

OwnedExpressionMember returns SysML::FeatureMembership :
	ownedMemberFeature_comp = OwnedExpression
;

OwnedExpression returns SysML::Expression :
	 ConditionalExpression
;

// Conditional Test Expressions

ConditionalExpression returns SysML::Expression :
	NullCoalescingExpression ( {SysML::OperatorExpression.operand_comp += current}
		operator = ConditionalTestOperator operand_comp += OwnedExpression ':' operand_comp += ConditionalExpression
	)?
;

ConditionalTestOperator: 
	'?'
;

// Null Coalescing Expressions

NullCoalescingExpression returns SysML::Expression :
	ConditionalOrExpression ( {SysML::OperatorExpression.operand_comp += current}
		operator = NullCoalescingOperator operand_comp += ConditionalOrExpression )*
;

NullCoalescingOperator :
	'??'
;

// Conditional Logical Expressions

ConditionalOrExpression returns SysML::Expression :
	ConditionalAndExpression ( {SysML::OperatorExpression.operand_comp += current}
		operator = ConditionalOrOperator operand_comp += ConditionalAndExpression )*
;

ConditionalOrOperator :
	'||'
;

ConditionalAndExpression returns SysML::Expression :
	OrExpression ( {SysML::OperatorExpression.operand_comp += current}
		operator = ConditionalAndOperator operand_comp += OrExpression )*
;

ConditionalAndOperator :
	'&&'
;

// Logical Expressions

OrExpression returns SysML::Expression :
	XorExpression ( {SysML::OperatorExpression.operand_comp += current}  
		operator = OrOperator operand_comp += XorExpression )*
;

OrOperator :
	'|'
;

XorExpression returns SysML::Expression :
	AndExpression ( {SysML::OperatorExpression.operand_comp += current}  
		operator = XorOperator operand_comp += AndExpression )*
;

XorOperator :
	'^'
;

AndExpression returns SysML::Expression :
	EqualityExpression ( {SysML::OperatorExpression.operand_comp += current}  
		operator = AndOperator operand_comp += EqualityExpression )*
;

AndOperator :
	'&'
;

// Equality Expressions

EqualityExpression returns SysML::Expression :
	ClassificationExpression ( {SysML::OperatorExpression.operand_comp += current}  
		operator = EqualityOperator operand_comp += ClassificationExpression )*
	
;

EqualityOperator :
	'==' | '!=' 
;

// Classification Expressions

ClassificationExpression returns SysML::Expression :
	  RelationalExpression ( {SysML::OperatorExpression.operand_comp += current}
		operator = ClassificationOperator ownedFeatureMembership_comp += TypeReferenceMember )?
	| {SysML::OperatorExpression} operand_comp += SelfReferenceExpression 
		operator = ClassificationOperator ownedFeatureMembership_comp += TypeReferenceMember
;

ClassificationOperator :
	'instanceof' | 'hastype' | 'istype' | '@'
;

TypeReferenceMember returns SysML::FeatureMembership :
	ownedMemberFeature_comp = TypeReference
;

TypeReference returns SysML::Feature :
	ownedRelationship_comp += OwnedFeatureTyping
;

SelfReferenceExpression returns SysML::FeatureReferenceExpression :
	ownedFeatureMembership_comp += SelfReferenceMember
;

SelfReferenceMember returns SysML::ReturnParameterMembership :
	ownedMemberFeature_comp = EmptyFeature
;

EmptyFeature returns SysML::Feature :
	{SysML::Feature}
;

// Relational Expressions

RelationalExpression returns SysML::Expression :
	AdditiveExpression ( {SysML::OperatorExpression.operand_comp += current}  
		operator = RelationalOperator operand_comp += AdditiveExpression )*
;

RelationalOperator :
	'<' | '>' | '<=' | '>='
;

// Arithmetic Expressions

AdditiveExpression returns SysML::Expression :
	MultiplicativeExpression ( {SysML::OperatorExpression.operand_comp += current}  
		operator = AdditiveOperator operand_comp += MultiplicativeExpression )*	
;

AdditiveOperator :
	'+' | '-' 
;

MultiplicativeExpression returns SysML::Expression :
	ExponentiationExpression ( {SysML::OperatorExpression.operand_comp += current}  
		operator = MultiplicativeOperator operand_comp += ExponentiationExpression )*	
;

MultiplicativeOperator :
	'*' | '/' | "%"
;

ExponentiationExpression returns SysML::Expression :
	UnitsExpression ( {SysML::OperatorExpression.operand_comp += current}  
		operator = ExponentiationOperator operand_comp += UnitsExpression )*		
;

ExponentiationOperator :
	'**'
;

// Units Expressions

UnitsExpression returns SysML::Expression :
	UnaryExpression ( {SysML::OperatorExpression.operand_comp += current} 
		operator = '@[' operand_comp += OwnedExpression ']' )?		
;

// Unary Expressions

UnaryExpression returns SysML::Expression:
	  {SysML::OperatorExpression} operator = UnaryOperator operand_comp += ExtentExpression
	| ExtentExpression
;

UnaryOperator :
	'+' | '-' | '!' | '~'
;

// Extent Expressions

ExtentExpression returns SysML::Expression :
	  {SysML::OperatorExpression} operator = 'all' ownedFeatureMembership_comp += TypeReferenceMember
	| SequenceExpression
;

/* Sequence Expressions */

SequenceExpression returns SysML::Expression :
	( SequenceConstructionExpression | BaseExpression )
	( {SysML::OperatorExpression.operand_comp += current} 
	  operator = '[' operand_comp += OwnedExpression ']'
	| {SysML::OperatorExpression.operand_comp += current} '->' 
	  operator = Name ( ownedFeatureMembership_comp += BodyExpressionMember )+
	)*
;

BodyExpressionMember returns SysML::FeatureMembership :
	ownedMemberFeature_comp = BodyExpression
;

BodyExpression returns SysML::BlockExpression :
	   => ( ownedFeatureMembership_comp += BodyParameterMember 
	   	    ( ownedFeatureMembership_comp += BodyParameterMember )* 
	   	    '(' ownedFeatureMembership_comp += ResultExpressionMember ')' )
	 | ownedRelationship_comp += ExpressionTyping
;

BodyParameterMember returns SysML::ParameterMembership :
	memberName = Name ownedMemberParameter_comp = BodyParameter
;

BodyParameter returns SysML::Feature :
	{SysML::Feature} ( TypedBy MultiplicityPart? | MultiplicityPart TypedBy? )?
;

ExpressionTyping returns SysML::FeatureTyping :
	type = [SysML::Function | QualifiedName]
;

SequenceConstructionExpression returns SysML::Expression :
	  {SysML::NullExpression} '{' '}'
	| '{' OwnedExpression
	      ( {SysML::OperatorExpression.operand_comp += current} operator = ',' operand_comp += SequenceElementList
	      | {SysML::OperatorExpression.operand_comp += current} operator = '..' operand_comp += OwnedExpression
	      )?
	  '}'
;

SequenceElementList returns SysML::Expression :	
	OwnedExpression ( {SysML::OperatorExpression.operand_comp += current} operator = ',' operand_comp += SequenceElementList )?
;

/* Base Expressions */

BaseExpression returns SysML::Expression :
	  NullExpression
	| LiteralExpression 
	| FeatureReferenceExpression 
	| InvocationExpression 
	| ExtentExpression_Deprecated
	| '(' OwnedExpression ')'
;

// Feature Reference Expressions

FeatureReferenceExpression returns SysML::FeatureReferenceExpression :
	ownedFeatureMembership_comp += FeatureReferenceMember
;

FeatureReferenceMember returns SysML::ReturnParameterMembership :
	ownedMemberFeature_comp = FeatureReference
;

// Note: Use subsetting here in order to inherit typing of referent.
FeatureReference returns SysML::Feature :
	ownedRelationship_comp += OwnedSubsetting
;

// Invocation Expressions

InvocationExpression returns SysML::InvocationExpression :
	ownedRelationship_comp += OwnedFeatureTyping '(' ArgumentList? ')'
;

fragment ArgumentList returns SysML::Expression :
	PositionalArgumentList | NamedArgumentList
;

fragment PositionalArgumentList returns SysML::Expression :
	ownedFeatureMembership_comp += OwnedExpressionMember 
	( ',' ownedFeatureMembership_comp += OwnedExpressionMember )*
;

fragment NamedArgumentList returns SysML::Expression :
	ownedFeatureMembership_comp += NamedExpressionMember 
	( ',' ownedFeatureMembership_comp += NamedExpressionMember )*
;

NamedExpressionMember returns SysML::FeatureMembership :
	memberName = Name '=>' ownedMemberFeature_comp = OwnedExpression
;

// Extent Expressions (Deprecated)

ExtentExpression_Deprecated returns SysML::OperatorExpression :
	ownedFeatureMembership_comp += TypeReferenceMember '.' operator = 'allInstances' '(' ')'
;

// Null Expressions

NullExpression returns SysML::NullExpression :
	{SysML::NullExpression} 'null'
;

// Literal Expressions

LiteralExpression returns SysML::LiteralExpression :
	  LiteralBoolean 
	| LiteralString 
	| LiteralInteger
	| LiteralReal 
	| LiteralUnbounded
;

LiteralBoolean returns SysML::LiteralBoolean :
	value = BooleanValue
;

BooleanValue returns Ecore::EBoolean :
	'true' | 'false'
;

LiteralString returns SysML::LiteralString :
	value = STRING_VALUE
;

LiteralInteger returns SysML::LiteralInteger:
	value = DECIMAL_VALUE
;

LiteralReal returns SysML::LiteralReal:
	value = RealValue
;

RealValue returns Ecore::EDouble:
	DECIMAL_VALUE? '.' ( DECIMAL_VALUE | EXP_VALUE ) | EXP_VALUE
;

LiteralUnbounded returns SysML::LiteralUnbounded :
	{SysML::LiteralUnbounded} '*'
;

LiteralUnlimitedNatural returns SysML::LiteralExpression:
	LiteralInteger | LiteralUnbounded
;

/* NAMES */

Name :
	ID | UNRESTRICTED_NAME
;

QualifiedName :
	Name | ColonQualifiedName | DotQualifiedName
;

ColonQualifiedName :
	Name ('::' Name)+
;

DotQualifiedName :
	Name ('.' Name)+
;

/* TERMINALS */

//terminal NATURAL_VALUE returns Ecore::EInt :
//	('0' | '1'..'9' (('_')? '0'..'9')*) | //DECIMAL 
//	(('0b' | '0B') '0'..'1' (('_')? '0'..'1')*) | // BINARY
//	(('0x'|'0X') ('0'..'9'|'a'..'f'|'A'..'F')  (('_')? ('0'..'9'|'a'..'f'|'A'..'F'))*) | // HEX
//	('0' ('_')? '0'..'7' (('_')? '0'..'7')*) // OCT
//;

terminal DECIMAL_VALUE returns Ecore::EInt :
	'0'..'9' ('0'..'9')*
;

terminal EXP_VALUE :
	DECIMAL_VALUE ('e' | 'E') ('+' | '-')? DECIMAL_VALUE
;

terminal ID : ('a'..'z'|'A'..'Z'|'_') ('a'..'z'|'A'..'Z'|'_'|'0'..'9')* ;
terminal UNRESTRICTED_NAME returns Ecore::EString : 
	'\'' ( '\\' ('b'|'t'|'n'|'f'|'r'|'"'|"'"|'\\') | !('\\'|'\'') )* '\'' ;

terminal STRING_VALUE returns Ecore::EString : 
	'"' ( '\\' ('b'|'t'|'n'|'f'|'r'|'"'|"'"|'\\') | !('\\'|'"') )* '"' ;

terminal REGULAR_COMMENT	: '/*' !'*' -> '*/';
terminal DOCUMENTATION_COMMENT : '/**' -> '*/' ;

terminal ML_NOTE : '//*' -> '*/';
terminal SL_NOTE : '//' (!('\n'|'\r') !('\n'|'\r')*)? ('\r'? '\n')?;

terminal WS : (' '|'\t'|'\r'|'\n')+;