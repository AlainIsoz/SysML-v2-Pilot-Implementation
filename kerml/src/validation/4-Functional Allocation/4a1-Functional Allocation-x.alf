package '4a1-Functional Allocation-x' {
	import '2-Parts Interconnection-x'::*;
	import '3a-Function-based Behavior'::*;
	import '3a-Function-based Behavior'::'provide power'::*;
	
	vehicle1_c1: VehicleA {
		port fuelCmdPort: FuelCmdPort redefines VehicleA::fuelCmdPort  {
			in fuelCmd: FuelCmd;
		}
		
		perform 'provide power' (
			in fuelCmd = fuelCmdPort::fuelCmd
		);
		
		binding fuelCmdPort = engine::fuelCmdPort;
		
		part engine: Engine {
			port fuelCmdPort: FuelCmdPort redefines Engine::fuelCmdPort {
				in fuelCmd: FuelCmd;
			}
			
			perform 'generate torque' (
				in fuelCmd = fuelCmdPort::fuelCmd, 
				out engineTorque = drivePwrPort::engineTorque
			);
			
			port drivePwrPort: DrivePwrPort redefines Engine::drivePwrPort {
				out engineTorque: Torque;
			}
		}
		
		connector :EngineToTransmissionInterface is engine::drivePwrPort to transmission::clutchPort;
			
		part transmission: Transmission {
			port clutchPort: ClutchPort redefines Transmission::clutchPort {
				in engineTorque: Torque;
			}
			
			perform 'amplify torque' (
				in engineTorque = clutchPort::engineTorque, 
				out transmissionTorque = shaftPort_a::transmissionTorque
			);

			port shaftPort_a: ShaftPort_a redefines Transmission::shaftPort_a  {
				out transmissionTorque: Torque;
			}
		}
				
	}
	
}