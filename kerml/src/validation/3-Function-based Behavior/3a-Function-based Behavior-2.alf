package '3a-Function-based Behavior-2' {
	import Transfers::Transfer;
	import ControlNodes::Merge;
	import Base::Occurrence::startShot;
	import Base::Occurrence::incomingTransfers;
			
	// ITEMS
	
	datatype FuelCmd;
	datatype Torque is ISQ::TorqueValue;
	
	// SIGNALS
	
	datatype EngineStart;
	datatype EngineOff;
	
	// ACTIVITIES
	
	behavior 'Generate Torque'(in fuelCmd: FuelCmd, out engineTorque: Torque);
	behavior 'Amplify Torque'(in engineTorque: Torque, out transmissionTorque: Torque);
	behavior 'Transfer Torque'(in transmissionTorque: Torque, out driveshaftTorque: Torque);
	behavior 'Distribute Torque'(in driveShaftTorque: Torque, out wheelTorque1: Torque, out wheelTorque2: Torque);
	
	behavior 'Provide Power'(in fuelCmd: FuelCmd, out wheelTorque1: Torque, out wheelTorque2: Torque);
	
	// ACTIONS
	
	step 'provide power': 'Provide Power'(
	in fuelCmd: FuelCmd, 
		out wheelTorque1: Torque, 
		out wheelTorque2: Torque
	) {
		step 'generate torque': 'Generate Torque'(
			in fuelCmd = 'Provide Power'::fuelCmd, 
			out engineTorque
		);
		
		step 'amplify torque': 'Amplify Torque'(
			in engineTorque stream from 'generate torque'::engineTorque,  
			out transmissionTorque
		);
		
		step 'transfer torque': 'Transfer Torque'(
			in transmissionTorque stream from 'amplify torque'::transmissionTorque,  
			out driveshaftTorque
		);
		
		step 'distribute torque': 'Distribute Torque'(
			in driveshaftTorque stream from 'transfer torque'::driveshaftTorque, 
			out wheelTorque1 = 'provide power'::wheelTorque1, 
			out wheelTorque2 = 'provide power'::wheelTorque2
		); 
	}
	
	step 'provide power with control': 'Provide Power'(
		in fuelCmd: FuelCmd, 
		out wheelTorque1: Torque, 
		out wheelTorque2: Torque
	) {
		portion is startShot;
		then[0..1] step merge: Merge[*];
		then accept engineStart (EngineStart);
		then accept engineOff (EngineOff);
		then[0..1] merge;
		
		step is engineStart;
		then step redefines 'provide power' (
			in fuelCmd = 'provide power with control'::fuelCmd, 
			out wheelTorque1 = 'provide power with control'::wheelTorque1,
			out wheelTorque2 = 'provide power with control'::wheelTorque2
		);
		then engineOff;
	}
}