package '1c-Parts Tree Redefinition' {
	class Vehicle {
		feature mass;
	}
	class AxleAssembly { }
	class Axle { 
		feature mass;
	}
	class FrontAxle specializes Axle {
		feature steeringAngle;
	}
	class Wheel { }
	
	vehicle1: Vehicle {
		feature mass redefines Vehicle::mass = 1000;		
		part frontAxleAssembly: AxleAssembly {
			part frontAxle: Axle;			
			part frontWheel: Wheel[2] ordered;
		}		
		part rearAxleAssembly: AxleAssembly {
			part rearAxle: Axle;
			part rearWheel: Wheel[2] ordered;
		}		
	}

	vehicle1_c1 redefines vehicle1 {
		/*
		 * Note: These imports are currently necessary because of qualified name
		 * resolution limitations in the context of inheritance.
		 */
		import vehicle1::*;
		import vehicle1::frontAxleAssembly::*;
		import vehicle1::rearAxleAssembly::*;

		feature mass redefines vehicle1::mass = 2000;		
		part frontAxleAssembly_c1: AxleAssembly redefines frontAxleAssembly {			
			part frontAxle_c1: FrontAxle redefines frontAxle;			
			part frontWheel_1: Wheel subsets frontWheel = frontWheel[1];
			part frontWheel_2: Wheel subsets frontWheel = frontWheel[2];
		}		
		part rearAxleAssembly_c1: AxleAssembly redefines rearAxleAssembly {			
			part rearAxle_c1: Axle redefines rearAxle;		
			part rearWheel_1: Wheel subsets rearWheel = rearWheel[1];
			part rearWheel_2: Wheel subsets rearWheel = rearWheel[2];
		}		
	}	
}