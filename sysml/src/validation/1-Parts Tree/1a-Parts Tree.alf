package '1a-Parts Tree' {
	
	/* Definitions */
	
	class Vehicle {
		feature mass;
	}
	
	class AxleAssembly;
	
	class Axle { 
		feature mass;
	}
	
	class FrontAxle specializes Axle {
		feature steeringAngle;
	}
	
	class Wheel;
	
	/* Usages */
	
	/** 
	 * 'vehicle1' is a package-owned feature of type Vehicle.
	 * It is not marked as a "part", because that is only
	 * valid in the context of a nested feature.
	 */
	feature vehicle1: Vehicle {
		/**
		 * This redefines the 'mass' feature from Vehicle to give
		 * it a fixed value.
		 */		
		feature mass redefines Vehicle::mass = 1000;
		
		/**
		 * 'frontAxleAssembly' is a nested feature of feature 'vehicle1'.
		 * It is marked as a "part", which indicates that it is a
		 * composite part of the containing feature.
		 * 
		 * (And similarly for 'rearAxleAssembly'.)
		 */
		part frontAxleAssembly: AxleAssembly {
			part frontAxle: Axle;
			
			/** 
			 * 'frontWheel' is a nested feature of type 'Wheel' with
			 * multiplicity "2". This means that this axle assembly
			 * must have exactly two wheels. However, there is still
			 * only one 'frontWheel' feature. The feature is "ordered",
			 * so that the first wheel can be distinguished from the
			 * second.
			 */
			part frontWheel: Wheel[2] ordered;
		}
		
		part rearAxleAssembly: AxleAssembly {
			part rearAxle: Axle;
			part rearWheel: Wheel[2] ordered;
		}
		
	}


	/**
	 * 'vehicle1_c1' is a modified copy of 'vehicle1'. There is no
	 * connection between this copy and the original version in the
	 * model.
	 */
	feature vehicle1_c1: Vehicle {	
		
		/**
		 * The mass value has been modified.
		 */
		feature mass redefines Vehicle::mass = 2000;

		part frontAxleAssembly: AxleAssembly {
			
			/**
			 * The part 'frontAxle' has been modified to have type 'FrontAxle'.
			 */
			part frontAxle: FrontAxle;
			
			/**
			 * The parts 'frontWheel_1' and 'frontWheel_2' have been added
			 * as subsets of 'frontWheel'. These are separate features from
			 * 'frontWheel', but essebtially provide alternate names for
			 * each of the two wheels, as given by their defining expressions.
			 */
			part frontWheel: Wheel[2] ordered;
			part frontWheel_1 subsets frontWheel = frontWheel[1];
			part frontWheel_2 subsets frontWheel = frontWheel[2];
		}
		
		/**
		 * 'rearAxleAssembly' has also been modified to add subsetting features
		 * for 'rearWheel'.
		 */
		part rearAxleAssembly: AxleAssembly {			
			part rearAxle: Axle;
			
			part rearWheel: Wheel[2] ordered;
			part rearWheel_1 subsets rearWheel = rearWheel[1];
			part rearWheel_2 subsets rearWheel = rearWheel[2];
		}
		
	}
	
}