package AccessControl {
	import ScalarValues::*;
	
	class Authorizer {
		port ap[1];
		port console[1];
	}
	class SpecialAuthorizer specializes Authorizer;
	
	class Console {
		port authorizer[*];
		port supervisor[1];
	}
	
	class AccessPoint {
		feature floor: Integer; // 0..10
		feature seclev: Integer; // 1..4
		
		port user;
		port door;
		port authorizer[1];
	}
	
	class BlockingAccessPoint specializes AccessPoint;
	class LoggingAccessPoint specializes AccessPoint;
	
	class ACSystem {
		feature ap: compose AccessPoint[2..100] {
			port user redefines AccessPoint::user;
			port authorizer redefines AccessPoint::authorizer;
		}
		feature aut: compose Authorizer[2] {
			port ap redefines Authorizer::ap;
			port console redefines Authorizer::console;
		}
		feature c: compose Console[1] {
			port authorizer redefines Console::authorizer;
			port supervisor redefines Console::supervisor;
		}
		
		port user;
		port door;
		port supervisor;
		
		connector user to ap::user[*];
		connector ap_aut is ap::authorizer[*] to aut::ap[1];
		connector aut::console[*] to c::authorizer[1];
		connector c::supervisor[1] to supervisor[1];	
	}
	
	class MyACSystem specializes ACSystem {
		feature ground: compose LoggingAccessPoint[1..25] subsets ap;
		feature top: compose BlockingAccessPoint[2..99] subsets ap;
	}
	
	class MyACSnapshot specializes MyACSystem {
		feature zero: compose LoggingAccessPoint[1..25] subsets ground {
			feature floor redefines LoggingAccessPoint::floor = 0;
			feature seclev redefines LoggingAccessPoint::seclev = 4;
			
			port authorizer redefines LoggingAccessPoint::authorizer;
		}
		part first: BlockingAccessPoint[2..99] subsets top {
			feature floor redefines LoggingAccessPoint::floor = 1;
			feature seclev redefines LoggingAccessPoint::seclev = 3;
			
			port authorizer redefines LoggingAccessPoint::authorizer;
		}
		part second: BlockingAccessPoint[2..99] subsets top {
			feature floor redefines LoggingAccessPoint::floor = 2;
			feature seclev redefines LoggingAccessPoint::seclev = 3;
			
			port authorizer redefines LoggingAccessPoint::authorizer;
		}
		
		part secureAut: SpecialAuthorizer[1] subsets aut {
			port ap redefines aut::ap;
		}
		part regularAut: Authorizer[1] subsets aut {
			port ap redefines aut::ap;
		}
		
		connector zero::authorizer[1] to secureAut::ap[1] subsets ap_aut;
		connector first::authorizer[1] to regularAut::ap[1] subsets ap_aut;
		connector second::authorizer[1] to regularAut::ap[1] subsets ap_aut;
	}

}