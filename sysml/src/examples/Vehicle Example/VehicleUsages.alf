/**
 * Example usages of elements from the vehicle definitions model.
 **/
package VehicleUsages {
	import SI::N;
	import SI::m;
	import VehicleDefinitions::*;

	/* VALUES */
	 
	'N・m': VehicleDefinitions::TorqueUnit = SI::N * SI::m;
	
	T1: VehicleDefinitions::Torque = 10.0@['N・m']; // VehicleDefinitions::Torque(10.0, 'N・m')
	T2: VehicleDefinitions::Torque = 20.0@['N・m']; // VehicleDefinitions::Torque(20.0, 'N・m')
	
	/* PARTS */
	
	/** Narrow-rim wheel configuration with 4 to 5 lugbolts. **/
	narrowRimWheel: VehicleDefinitions::Wheel {
		part lugbolt: VehicleDefinitions::Lugbolt[4..5];
	}
	
	/** Wide-rim wheel configuration with 4 to 6 lugbolts. **/	
	wideRimWheel: VehicleDefinitions::Wheel {
		part lugbolt: VehicleDefinitions::Lugbolt[4..6];
	}

	/** Basic Vehicle configuration showing a part hierarchy. **/
	vehicle_C1: VehicleDefinitions::Vehicle {
		part frontAxleAssembly: VehicleDefinitions::AxleAssembly {
			part frontWheel[2] redefines narrowRimWheel {
				part lugbolt[4] redefines narrowRimWheel::lugbolt {
					feature tighteningTorque redefines VehicleDefinitions::Lugbolt::tighteningTorque = T1;
				}
			}
			part frontAxle: VehicleDefinitions::Axle;
		}
		
		part rearAxleAssembly: VehicleDefinitions.AxleAssembly {
			part rearWheel[2] redefines wideRimWheel {
				part lugbolt[6] redefines wideRimWheel::lugbolt {
					feature tighteningTorque redefines VehicleDefinitions::Lugbolt::tighteningTorque = T2;
				}
			}
			part rearAxle: VehicleDefinitions::Axle;			
		}
	}
	
	/** Specialized configuration with part-specific ports. **/
	vehicle_C2 redefines vehicle_C1 {
		part frontAxleAssembly redefines vehicle_C1::frontAxleAssembly {
			part leftFrontWheel subsets vehicle_C1::frontAxleAssembly::frontWheel = vehicle_C1::frontAxleAssembly::frontWheel[1] {
				port hub: VehicleDefinitions::WheelHubIF;
			}
			part rightFrontWheel subsets vehicle_C1::frontAxleAssembly::frontWheel = vehicle_C1::frontAxleAssembly::frontWheel[2] {
				port hub: VehicleDefinitions::WheelHubIF;
			}
			
			part frontAxle redefines vehicle_C1::frontAxleAssembly::frontAxle {
				port leftMountingPoint: VehicleDefinitions::AxleMountIF;
				port rightMountingPoint: VehicleDefinitions::AxleMountIF;
			}
		
			connector leftFrontMount: VehicleDefinitions::Mounting is 
				frontAxle::leftMountingPoint to leftFrontWheel::hub;
				
			connector rightFrontMount: VehicleDefinitions::Mounting is 
				frontAxle::rightMountingPoint to rightFrontWheel::hub;
		}
		
		part rearAxleAssembly redefines vehicle_C1::rearAxleAssembly {
			part leftRearWheel subsets vehicle_C1::rearAxleAssembly::rearWheel = vehicle_C1::rearAxleAssembly::rearWheel[1] {
				port hub: VehicleDefinitions::WheelHubIF;
			}
			part rightRearWheel subsets vehicle_C1::rearAxleAssembly::rearWheel = vehicle_C1::rearAxleAssembly::rearWheel[2] {
				port hub: VehicleDefinitions::WheelHubIF;
			}

			part rearAxle redefines vehicle_C1::rearAxleAssembly::rearAxle {
				port leftMountingPoint: VehicleDefinitions::AxleMountIF;
				port rightMountingPoint: VehicleDefinitions::AxleMountIF;
			}
			
			connector leftRearMount: VehicleDefinitions::Mounting is 
				rearAxle::leftMountingPoint to leftRearWheel::hub;
				
			connector rightRearMount: VehicleDefinitions::Mounting is 
				rearAxle::rightMountingPoint to rightRearWheel::hub;
		}		
	}
	
	/** Further specialized configuration with a connector to a deeply-nested port. **/
	vehicle_C3 redefines vehicle_C2 {
		part transmission: VehicleDefinitions::Transmission {
			port out drive: VehicleDefinitions::DriveIF;
		}
		
		part rearAxleAssembly redefines vehicle_C2::rearAxleAssembly {
			part rearAxle redefines vehicle_C2::rearAxleAssembly::rearAxle {
				port in drive: VehicleDefinitions::DriveIF;
			}
		}
		
		connector driveShaft is 
			transmission::drive to rearAxleAssembly::rearAxle::drive;			
	}
}