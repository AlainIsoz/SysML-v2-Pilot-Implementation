package VehicleUsages {

	feature 'N・m': VehicleDefinitions.TorqueUnit = SI.N * SI.m;
	
	feature T1: VehicleDefinitions.Torque = 10.0@['N・m']; // VehicleDefinitions.Torque(10.0, 'N・m')
	feature T2: VehicleDefinitions.Torque = 20.0@['N・m']; // VehicleDefinitions.Torque(20.0, 'N・m')
	
	feature narrowRimWheel: VehicleDefinitions.Wheel {
		feature lugbolt: VehicleDefinitions.Lugbolt[4..5];
	}

	feature wideRimWheel: VehicleDefinitions.Wheel {
		feature lugbolt: VehicleDefinitions.Lugbolt[4..6];
	}

	feature vehicle_C1: VehicleDefinitions.Vehicle {
		feature frontAssembly: VehicleDefinitions.AxleAssembly {
			feature frontWheel is narrowRimWheel[2] {
				feature is narrowRimWheel.lugbolt[4] {
					feature torque is VehicleDefinitions.Lugbolt.torque = T1;
				}
			}
			feature frontAxle: VehicleDefinitions.Axle {
				port out leftMountingPoint: VehicleDefinitions.WheelHub;
				port out rightMountingPoint: VehicleDefinitions.WheelHub;
			}
		}
		
		feature rearAssembly: VehicleDefinitions.AxleAssembly {
			feature rearWheel is wideRimWheel[2] {
				feature is wideRimWheel.lugbolt[6] {
					feature torque is VehicleDefinitions.Lugbolt.torque = T2;
				}
			}
			feature rearAxle: VehicleDefinitions.Axle {
				port out leftMountingPoint: VehicleDefinitions.WheelHub;
				port out rightMountingPoint: VehicleDefinitions.WheelHub;
			}			
		}
	}
	
	feature vehicle_C2 is vehicle_C1 {
		feature is vehicle_C1.frontAssembly {
			feature leftFrontWheel subsets vehicle_C1.frontAssembly.frontWheel[1] {
				port in hub: VehicleDefinitions.WheelHub;
			}
			feature rightFrontWheel subsets vehicle_C1.frontAssembly.frontWheel[1] {
				port in hub: VehicleDefinitions.WheelHub;
			}
			
			connector vehicle_C1.frontAssembly.frontAxle.leftMountingPoint => leftFrontWheel.hub;
			connector vehicle_C1.frontAssembly.frontAxle.rightMountingPoint => rightFrontWheel.hub;
		}
		
		feature is vehicle_C1.rearAssembly {
			feature leftRearWheel subsets vehicle_C1.rearAssembly.rearWheel[1] {
				port in hub: VehicleDefinitions.WheelHub;
			}
			feature rightRearWheel subsets vehicle_C1.rearAssembly.rearWheel[1] {
				port in hub: VehicleDefinitions.WheelHub;
			}

			connector vehicle_C1.rearAssembly.rearAxle.leftMountingPoint => leftRearWheel.hub;
			connector vehicle_C1.rearAssembly.rearAxle.rightMountingPoint => rightRearWheel.hub;
		}
		
	}

}