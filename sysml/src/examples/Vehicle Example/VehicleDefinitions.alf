/**
 * Example vehicle definitions model.
 **/
package VehicleDefinitions {
	// Import ScalarValues library
	import ScalarValues::*;
	
	// Import Quantities libraries
	import Quantities::*;
	import UnitsAndScales::*;
	import ISQ::*;
	
	/* LOCALLY DEFINED VALUE TYPES */
	
	/* 
	 * Torque measurement unit (and implied ratio scale) defined as a force unit times a length unit. 
	 */
	class TorqueUnit specializes DerivedUnit {
		feature unitPowerFactor1 subsets unitPowerFactor {
			unit: ForceUnit redefines unit;
			exponent redefines exponent = 1;
		}
		unitPowerFactor2 subsets unitPowerFactor {
			unit: LengthUnit redefines unit;
			exponent redefines exponent = 1;
		}
	}
	
	/*
	 * Torque quantity value and feature defined as a real  magnitude with a torque unit.
	 */
	class TorqueValue specializes QuantityValue {
		magnitude: Real redefines Quantities::QuantityValue::magnitude;
		scale: TorqueUnit redefines Quantities::QuantityValue::scale;
	}
	feature torque : TorqueValue;
	
	/* BLOCKS */
	
	class Vehicle { }
	
	class Transmission { }
	
	class AxleAssembly { }
	
	class Axle { }
	
	class Wheel { }
	
	class Lugbolt {
		tighteningTorque: torque;
	}
	
	/* INTERFACE BLOCKS */
	
	class DriveIF { 
		feature in driveTorque: torque;
	}
	
	class AxleMountIF { 
		feature out transferredTorque: torque;
	}
	
	class WheelHubIF { 
		feature in appliedTorque: torque;
	}
	
	/* ASSOCIATION BLOCKS */
	
	/** An association intended to be used as the type of a connector
	 *  mounting a Wheel to an Axle.
	 **/
	assoc Mounting {
		end axleMount: AxleMountIF;
		end hub: WheelHubIF;
	}

}