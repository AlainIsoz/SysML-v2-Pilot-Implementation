/**
 * Example vehicle definitions model.
 **/
package VehicleDefinitions {
	
	/* BLOCKS */
	
	class Vehicle;	
	class Transmission;	
	class AxleAssembly;
	class Axle;	
	class Wheel;
	class Lugbolt {
		tighteningTorque is torque;
	}
	
	/* INTERFACE BLOCKS */
	
	class DriveIF { 
		in driveTorque is torque;
	}
	
	class AxleMountIF { 
		out transferredTorque is torque;
	}
	
	class WheelHubIF { 
		in appliedTorque is torque;
	}
	
	/* ASSOCIATION BLOCKS */
	
	/** An association intended to be used as the type of a connector
	 *  mounting a Wheel to an Axle.
	 **/
	assoc Mounting {
		end axleMount: AxleMountIF;
		end hub: WheelHubIF;
	}

	/* LOCALLY DEFINED VALUE TYPES */
		
	// Import ScalarValues library
	import ScalarValues::*;
	
	// Import Quantities libraries
	import Quantities::*;
	import UnitsAndScales::*;
	import ISQ::*;

	/* 
	 * Torque measurement unit (and implied ratio scale) defined as a force unit times a length unit. 
	 */
	datatype TorqueUnit specializes DerivedUnit {
		unitPowerFactor1 subsets unitPowerFactor {
			unit: ForceUnit redefines UnitPowerFactor::unit;
			exponent redefines UnitPowerFactor::exponent = 1;
		}
		unitPowerFactor2 subsets unitPowerFactor {
			unit: LengthUnit redefines UnitPowerFactor::unit;
			exponent redefines UnitPowerFactor::exponent = 1;
		}
	}
	
	/*
	 * Torque quantity value and feature defined as a real  magnitude with a torque unit.
	 */
	datatype TorqueValue specializes QuantityValue {
		magnitude: Real redefines QuantityValue::magnitude;
		scale: TorqueUnit redefines QuantityValue::scale;
	}
	torque : TorqueValue redefines Quantity;
	
}