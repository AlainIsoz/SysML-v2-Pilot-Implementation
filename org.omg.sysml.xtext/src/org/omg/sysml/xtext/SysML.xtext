/*****************************************************************************
 * SysML 2 Pilot Implementation
 * Copyright (c) 2018, 2020 Model Driven Solutions, Inc.
 * Copyright (c) 2018 IncQuery Labs Ltd.
 * Copyright (c) 2019 Maplesoft (Waterloo Maple, Inc.)
 * Copyright (c) 2019 Mgnite Inc.
 *    
 * This program is free software: you can redistribute it and/or modify
 * it under the terms of the GNU Lesser General Public License as published by
 * the Free Software Foundation, either version 3 of the License, or
 * (at your option) any later version.
 *
 * This program is distributed in the hope that it will be useful,
 * but WITHOUT ANY WARRANTY; without even the implied warranty of
 * MERCHANTABILITY or FITNESS FOR A PARTICULAR PURPOSE.  See the
 * GNU Lesser General Public License for more details.
 *
 * You should have received a copy of theGNU Lesser General Public License
 * along with this program.  If not, see <https://www.gnu.org/licenses/>.
 *
 * @license LGPL-3.0-or-later <http://spdx.org/licenses/LGPL-3.0-or-later>
 * 
 * Contributors:
 *  Ed Seidewitz, MDS
 *  Zoltan Kiss, IncQuery
 *  Balazs Grill, IncQuery
 *  Hisashi Miyashita, Maplesoft/Mgnite
 * 
 *****************************************************************************/

grammar org.omg.sysml.xtext.SysML hidden (WS, ML_NOTE, SL_NOTE)

import "http://www.eclipse.org/emf/2002/Ecore" as Ecore
import "http://www.omg.org/spec/SysML/2.0" as SysML

/* UNITS */

Unit returns SysML::Package :
	  AnonymousPackage
	| PackageUnit 
	| AttributeDefinitionUnit
	| ItemDefinitionUnit
	| PartDefinitionUnit
	| IndividualDefinitionUnit
	| ConnectionDefinitionUnit
	| InterfaceDefinitionUnit 
	| ActionDefinitionUnit
	| CalculationDefinitionUnit
	| StateDefinitionUnit
	| ConstraintDefinitionUnit
	| RequirementDefinitionUnit
	| CaseDefinitionUnit
	| AnalysisCaseDefinitionUnit
	| VerificationCaseDefinitionUnit
	| ViewDefinitionUnit
	| ViewpointDefinitionUnit
	| RenderingDefinitionUnit
	| AttributeUnit
	| ItemUnit
	| PartUnit
	| ActionUnit
	| CalculationUnit
	| StateUnit
	| ConstraintUnit
	| RequirementUnit
	| CaseUnit
	| AnalysisCaseUnit
	| VerificationCaseUnit
	| ViewUnit
	| ViewpointUnit
	| RenderingUnit
;

AnonymousPackage returns SysML::Package :
	PackageBody
;

PackageUnit returns SysML::Package: 
    UnitPrefix PackageDeclaration PackageBody 
;

AttributeDefinitionUnit returns SysML::AttributeDefinition :
	UnitPrefix AttributeDefDeclaration DefinitionBody
;

ItemDefinitionUnit returns SysML::ItemDefinition:
	UnitPrefix ItemDefDeclaration DefinitionBody
;

PartDefinitionUnit returns SysML::PartDefinition:
	UnitPrefix PartDefDeclaration DefinitionBody
;

IndividualDefinitionUnit returns SysML::IndividualDefinition :
	UnitPrefix IndividualDefDeclaration DefinitionBody
;

ConnectionDefinitionUnit returns SysML::ConnectionDefinition :
	UnitPrefix ConnectionDefDeclaration ConnectionDefBody
;

InterfaceDefinitionUnit returns SysML::InterfaceDefinition :
	UnitPrefix InterfaceDefDeclaration InterfaceDefBody
;

ActionDefinitionUnit returns SysML::ActionDefinition :
	UnitPrefix ActionDefDeclaration ActionDefBody
;

CalculationDefinitionUnit returns SysML::CalculationDefinition :
	UnitPrefix CalculationDefDeclaration CalculationDefBody
;

StateDefinitionUnit returns SysML::StateDefinition :
	UnitPrefix StateDefDeclaration StateDefBody
;

ConstraintDefinitionUnit returns SysML::ConstraintDefinition :
	UnitPrefix ConstraintDefDeclaration ConstraintDefBody
;

RequirementDefinitionUnit returns SysML::RequirementDefinition :
	UnitPrefix RequirementDefDeclaration RequirementDefBody
;

CaseDefinitionUnit returns SysML::CaseDefinition :
	UnitPrefix CaseDefDeclaration CaseDefBody
;

AnalysisCaseDefinitionUnit returns SysML::AnalysisCaseDefinition :
	UnitPrefix AnalysisCaseDefDeclaration CaseDefBody
;

VerificationCaseDefinitionUnit returns SysML::VerificationCaseDefinition :
	UnitPrefix VerificationCaseDefDeclaration CaseDefBody
;

ViewDefinitionUnit returns SysML::ViewDefinition :
	UnitPrefix ViewDefDeclaration DefinitionBody
;

ViewpointDefinitionUnit returns SysML::ViewpointDefinition :
	UnitPrefix ViewpointDefDeclaration RequirementDefBody
;

RenderingDefinitionUnit returns SysML::RenderingDefinition :
	UnitPrefix RenderingDefDeclaration DefinitionBody
;

AttributeUnit returns SysML::AttributeUsage :
	UnitPrefix AttributeDeclaration UsageCompletion
;

ItemUnit returns SysML::ItemUsage :
	UnitPrefix ItemDeclaration UsageCompletion
;

PartUnit returns SysML::PartUsage :
	UnitPrefix PartDeclaration UsageCompletion
;

ActionUnit returns SysML::ActionUsage :
	UnitPrefix ActionDeclaration ActionDefBody
;

CalculationUnit returns SysML::CalculationUsage :
	UnitPrefix CalculationDeclaration CalculationDefBody
;

StateUnit returns SysML::StateUsage :
	UnitPrefix StateDeclaration StateDefBody
;

ConstraintUnit returns SysML::ConstraintUsage :
	UnitPrefix ConstraintDeclaration ConstraintDefBody
;

RequirementUnit returns SysML::RequirementUsage :
	UnitPrefix RequirementDeclaration RequirementDefBody
;

CaseUnit returns SysML::CaseUsage :
	UnitPrefix CaseDeclaration CaseDefBody
;

AnalysisCaseUnit returns SysML::AnalysisCaseUsage :
	UnitPrefix AnalysisCaseDeclaration CaseDefBody
;

VerificationCaseUnit returns SysML::VerificationCaseUsage :
	UnitPrefix VerificationCaseDeclaration CaseDefBody
;

ViewUnit returns SysML::ViewUsage :
	UnitPrefix ViewDeclaration ValuePart? ViewBody
;

ViewpointUnit returns SysML::ViewpointUsage :
	UnitPrefix ViewpointDeclaration RequirementDefBody
;

RenderingUnit returns SysML::RenderingUsage :
	UnitPrefix RenderingDeclaration UsageCompletion
;

fragment UnitPrefix returns SysML::Package :
	( ownedRelationship_comp += PrefixAnnotation )*
;

/* IDENTIFICATION */

fragment Identification returns SysML::Element :
	  ( 'id' humanId = Name )? name = Name
;

/* VISIBILITY */

enum PackageElementVisibilityIndicator returns SysML::VisibilityKind:
	public = 'public' | private = 'private'
;

enum VisibilityIndicator returns SysML::VisibilityKind :
	public = 'public' | private = 'private' | protected = 'protected'
;

/* ANNOTATIONS */

Annotation returns SysML::Annotation :
	{SysML::Annotation} ( 'about' annotatedElement = [SysML::Element|QualifiedName] )?
;

EmptyAnnotation returns SysML::Annotation :
	{SysML::Annotation}
;

PrefixAnnotation returns SysML::Annotation :
	ownedRelatedElement_comp += Documentation
;

/* COMMENTS */

Comment returns SysML::Comment :
	( 'comment' Identification? ownedRelationship_comp += Annotation
	| ownedRelationship_comp += EmptyAnnotation
	)
	body = ML_COMMENT
;

/* DOCUMENTATION */

Documentation returns SysML::Comment :
	( 'doc' ( 'id' humanId = Name )? )? body = DOCUMENTATION_COMMENT
;

OwnedDocumentation returns SysML::Documentation :
	documentingComment_comp = DocumentationComment
;

DocumentationComment returns SysML::Comment :
	'doc' ( 'id' humanId = Name )? body = ML_COMMENT
;

/* TEXTUAL REPRESENTATION */

TextualRepresentation returns SysML::TextualRepresentation :
    ( 'rep' Identification? ownedRelationship_comp += Annotation
    | ownedRelationship_comp += EmptyAnnotation
    )
    'language' language = STRING_VALUE 
    body = ML_COMMENT
;

/* IMPORTS */

Import returns SysML::Import :
	( ownedRelationship_comp += PrefixAnnotation )*
	( visibility = PackageElementVisibilityIndicator )?  
	'import' (
	  importedPackage = [SysML::Package|Name] ( '::' | '.' ) '*' 
	| importedPackage = [SysML::Package|ColonQualifiedName] '::' '*' 
	| importedPackage = [SysML::Package|DotQualifiedName] '.' '*'
	) ';'
;

/* PACKAGES */

Package returns SysML::Package :
	PackageDeclaration PackageBody
;

fragment PackageDeclaration returns SysML::Package : 
	'package' Identification 
;

fragment PackageBody returns SysML::Package :
	  ';' 
	| '{' ( documentation_comp += OwnedDocumentation
		  | ownedMembership_comp += PackageMember 
		  | ownedImport_comp += Import
	      )* 
	  '}'
;

PackagedDefinitionElement returns SysML::Element :
	  Package
	| AttributeDefinition
	| ItemDefinition
	| PartDefinition
	| IndividualDefinition
	| ConnectionDefinition
	| InterfaceDefinition
	| PortDefinition
	| ActionDefinition
	| CalculationDefinition
	| StateDefinition
	| ConstraintDefinition
	| RequirementDefinition
	| CaseDefinition
	| AnalysisCaseDefinition
	| VerificationCaseDefinition
	| ViewDefinition
	| ViewpointDefinition
	| RenderingDefinition
	| Comment
	| TextualRepresentation
;
	
PackagedUsageElement returns SysML::Usage :
	  AttributeUsage
	| ItemUsage
	| PartUsage
	| IndividualUsage
	| TimeSliceUsage
	| SnapshotUsage
	| PortUsage
	| ConnectionUsage
	| Connector
	| InterfaceUsage
	| ActionUsage
	| CalculationUsage
	| StateUsage
	| ConstraintUsage
	| RequirementUsage
	| CaseUsage
	| AnalysisCaseUsage
	| VerificationCaseUsage
	| ViewUsage
	| ViewpointUsage
	| RenderingUsage
;

/* PACKAGE MEMBERSHIPS */

PackageMember returns SysML::Membership : 
	PackageMemberPrefix	( PackagedDefinitionMember | PackagedUsageMember )
;

fragment PackagedDefinitionMember returns SysML::Membership :
	ownedMemberElement_comp = PackagedDefinitionElement
	| ( 'import' | 'alias' ) memberElement = [SysML::Element|QualifiedName] ( 'as' memberName = Name )? ';'
;

fragment PackagedUsageMember returns SysML::Membership:
	ownedMemberElement_comp = PackagedUsageElement
;

fragment PackageMemberPrefix returns SysML::Membership :
	( ownedRelationship_comp += PrefixAnnotation )*
    ( visibility = PackageElementVisibilityIndicator )?		
;

/* DEFINITIONS */

fragment DefinitionPrefix returns SysML::Definition :
	isAbstract ?= 'abstract' | isVariation ?= 'variation'
;

fragment DefinitionBody returns SysML::Type :
	  ';' 
	| '{' DefinitionBodyItem* '}'
;

fragment DefinitionBodyItem returns SysML::Type :
	  documentation_comp += OwnedDocumentation
	| ownedMembership_comp += NestedDefinitionMember 
	| ownedMembership_comp += VariantUsageMember
	| ownedFeatureMembership_comp += NestedUsageMember 
	| ( ownedFeatureMembership_comp += EmptySuccessionMember )?
	  ownedFeatureMembership_comp += IndividualUsageMember
	| => ownedImport_comp += Import	
;

DirectionalStructureUsageElement returns SysML::Usage :
	  ReferenceUsage
	| AttributeUsage
	| ItemRefUsage
	| PartRefUsage
	| ViewRefUsage
	| RenderingRefUsage
;

NondirectionalStructureUsageElement returns SysML::Feature :
	  PortUsage
	| ConnectionUsage
	| Connector
	| InterfaceUsage
	| BindingConnector
	| Succession
	| ItemFlow
	| SuccessionItemFlow
;

IndividualUsageElement returns SysML::Usage :
	  IndividualRefUsage
	| TimeSliceRefUsage
	| SnapshotRefUsage
;

DirectionalBehaviorUsageElement returns SysML::Usage :
	  ActionRefUsage
	| CalculationRefUsage
	| StateRefUsage
	| ConstraintRefUsage
	| RequirementRefUsage
	| CaseRefUsage
	| AnalysisCaseRefUsage
	| VerificationCaseRefUsage
	| ViewpointRefUsage
;

NondirectionalBehaviorUsageElement returns SysML::Usage :
	  PerformActionUsage
	| ExhibitStateUsage
	| AssertConstraintUsage
	| SatisfyRequirementUsage
;

VariantUsageElement returns SysML::Usage :
	  ReferenceVariantUsage
	| AttributeVariantUsage
	| ItemRefUsage
	| PartRefUsage
	| PortUsage
	| ConnectionUsage
	| Connector
	| InterfaceUsage
	| IndividualUsageElement
	| DirectionalBehaviorUsageElement
	| NondirectionalBehaviorUsageElement
;

/* DEFINITION MEMBERSHIPS */

NestedDefinitionMember returns SysML::Membership : 
	DefinitionMemberPrefix PackagedDefinitionMember
;

VariantUsageMember returns SysML::VariantMembership :
	DefinitionMemberPrefix 'variant' ownedVariantUsage_comp = VariantUsageElement
;

NestedUsageMember returns SysML::FeatureMembership :
	StructureUsageMember | BehaviorUsageMember
;

StructureUsageMember returns SysML::FeatureMembership :
	DefinitionMemberPrefix
	( ( direction = FeatureDirection )? ownedMemberFeature_comp = DirectionalStructureUsageElement
	| ownedMemberFeature_comp = NondirectionalStructureUsageElement
    )
;

IndividualUsageMember returns SysML::FeatureMembership :
	DefinitionMemberPrefix ownedMemberFeature_comp = IndividualUsageElement
;

BehaviorUsageMember returns SysML::FeatureMembership :
	DefinitionMemberPrefix
	( ( direction = FeatureDirection )? ownedMemberFeature_comp = DirectionalBehaviorUsageElement
	| ownedMemberFeature_comp = NondirectionalBehaviorUsageElement
    )
;

fragment DefinitionMemberPrefix returns SysML::Membership :
	( ownedRelationship_comp += PrefixAnnotation )*
    ( visibility = VisibilityIndicator )?
;

enum FeatureDirection returns SysML::FeatureDirectionKind :
	in = 'in' | out = 'out' | inout = 'inout'
;

/* CLASSIFIERS */

fragment ClassifierDeclarationCompletion returns SysML::Classifier :	
	Identification 
//  ( '<' ownedMember += ClassifierTemplateParameter ( ',' ownedMember += ClassifierTemplateParameter )* '>' )? 
	SuperclassingList?
;

fragment SuperclassingList returns SysML::Classifier :
	SpecializesKeyword ownedRelationship_comp += Superclassing ( ',' ownedRelationship_comp += Superclassing )*
;

SpecializesKeyword :
	':>' | 'specializes'
;

Superclassing returns SysML::Superclassing:
	superclass = [SysML::Classifier | QualifiedName]
;

/* ATTRIBUTE DEFINITIONS */

AttributeKeyword :
	'attribute'
;

AttributeDefKeyword :
	AttributeKeyword 'def' | 'value' 'type'
;

AttributeDefinition returns SysML::AttributeDefinition :
	AttributeDefDeclaration DefinitionBody
;

fragment AttributeDefDeclaration returns SysML::AttributeDefinition :
	DefinitionPrefix? AttributeDefKeyword ClassifierDeclarationCompletion
;

/* ITEM DEFINITIONS */

ItemKeyword :
	'item'
;

ItemDefKeyword :
	ItemKeyword 'def'
;

ItemDefinition returns SysML::ItemDefinition :
	ItemDefDeclaration DefinitionBody
;

fragment ItemDefDeclaration returns SysML::ItemDefinition :
	DefinitionPrefix? ItemDefKeyword ClassifierDeclarationCompletion
;

/* PART DEFINITIONS */

PartKeyword :
	'part'
;

PartDefKeyword :
	PartKeyword 'def' | 'block'
;

PartDefinition returns SysML::PartDefinition :
	PartDefDeclaration DefinitionBody
;

fragment PartDefDeclaration returns SysML::PartDefinition :
	DefinitionPrefix? PartDefKeyword ClassifierDeclarationCompletion
;

/* INDIVIDUAL DEFINITIONS */

IndividualKeyword :
	'individual'
;

IndividualDefKeyword :
	IndividualKeyword 'def'
;

IndividualDefinition returns SysML::IndividualDefinition :
	IndividualDefDeclaration DefinitionBody
;

fragment IndividualDefDeclaration returns SysML::IndividualDefinition :
	DefinitionPrefix? IndividualDefKeyword ClassifierDeclarationCompletion
	ownedMembership_comp += LifeClassMembership
;

LifeClass returns SysML::LifeClass :
	{SysML::LifeClass}
;

/* INDIVIDUAL DEFINITION MEMBERSHIPS */

LifeClassMembership returns SysML::Membership :
	ownedMemberElement_comp = LifeClass
;

/* PORT DEFINITIONS */

PortKeyword :
	'port'
;

PortDefKeyword :
	PortKeyword 'def' | 'interface' 'block'
;

PortDefinition returns SysML::PortDefinition :
	PortDefDeclaration DefinitionBody
;

fragment PortDefDeclaration returns SysML::PortDefinition :
	DefinitionPrefix? PortDefKeyword ClassifierDeclarationCompletion
	ownedMembership_comp += ConjugatedPortDefinitionMember
;

ConjugatedPortDefinition returns SysML::ConjugatedPortDefinition :
	ownedRelationship_comp += PortConjugation
;

PortConjugation returns SysML::PortConjugation :
	{SysML::PortConjugation}
;

/* PORT DEFINITION MEMBERSHIPS */

ConjugatedPortDefinitionMember returns SysML::Membership :
	ownedMemberElement_comp = ConjugatedPortDefinition
;

/* CONNECTION DEFINITIONS */

ConnectionKeyword :
	'connection'
;

ConnectionDefKeyword :
	ConnectionKeyword 'def' | 'assoc' 'block' 
;

ConnectionDefinition returns SysML::ConnectionDefinition :
	ConnectionDefDeclaration ConnectionDefBody
;

fragment ConnectionDefDeclaration returns SysML::ConnectionDefinition :
	DefinitionPrefix? ConnectionDefKeyword ClassifierDeclarationCompletion
;

fragment ConnectionDefBody returns SysML::Type :
	  ';'
	| '{' (
		  documentation_comp += OwnedDocumentation
		| ownedMembership_comp += NestedDefinitionMember 
		| ownedMembership_comp += VariantUsageMember
		| ownedFeatureMembership_comp += ConnectionUsageMember
		| ( ownedFeatureMembership_comp += EmptySuccessionMember )?
		  ownedFeatureMembership_comp += IndividualUsageMember
		| ownedImport_comp += Import
	  )* '}'
;

ConnectionEndElement returns SysML::Feature :
	  ReferenceEndUsage
	| ItemRefUsage
	| PartRefUsage
	| PortUsage
	| ActionRefUsage
	| CalculationRefUsage
	| StateRefUsage
;

/* CONNECTION DEFINITION MEMBERSHIPS */

ConnectionUsageMember returns SysML::FeatureMembership :
	NestedUsageMember | ConnectionEndMember
;

ConnectionEndMember returns SysML::EndFeatureMembership :
	DefinitionMemberPrefix 'end' ownedMemberFeature_comp = ConnectionEndElement
;

/* INTERFACE DEFINITIONS */

InterfaceKeyword :
	'interface'
;

InterfaceDefKeyword :
	InterfaceKeyword 'def'
;

InterfaceDefinition returns SysML::InterfaceDefinition :
	InterfaceDefDeclaration InterfaceDefBody
;

fragment InterfaceDefDeclaration returns SysML::InterfaceDefinition :
	DefinitionPrefix? InterfaceDefKeyword ClassifierDeclarationCompletion
;

fragment InterfaceDefBody returns SysML::Type :
	  ';'
	| '{' ( 
		  documentation_comp += OwnedDocumentation
		| ownedMembership_comp += NestedDefinitionMember 
		| ownedMembership_comp += VariantUsageMember
		| ownedFeatureMembership_comp += InterfaceUsageMember
		| ownedImport_comp += Import
	)* '}'
;

/* INTERFACE DEFINITION MEMBERSHIPS */

InterfaceUsageMember returns SysML::FeatureMembership :
	NestedUsageMember | InterfaceEndMember
;

InterfaceEndMember returns SysML::EndFeatureMembership :
	DefinitionMemberPrefix 'end' ownedMemberFeature_comp = PortEndUsage
;

/* ACTION DEFINITIONS */

ActionKeyword :
	'action'
;

ActionDefKeyword :
	ActionKeyword 'def' | 'activity'
;

ActionDefinition returns SysML::ActionDefinition :
	ActionDefDeclaration ActionDefBody
;

fragment ActionDefDeclaration returns SysML::ActionDefinition :
	DefinitionPrefix? ActionDefKeyword Identification SuperclassingList? ParameterList? 
;

fragment ParameterList returns SysML::Type :
	'(' ( ownedFeatureMembership_comp += ParameterMember ( ',' ownedFeatureMembership_comp += ParameterMember )* )? ')'
;

fragment ActionDefBody returns SysML::Type :
	    ';' 
		// Note: Using a syntactic predicate here seems to avoid a possible infinite loop
		// while incrementally parsing.
	  | '{' => ActionDefBodyItem* '}' 
;

fragment ActionDefBodyItem returns SysML::Type :
	  documentation_comp += OwnedDocumentation
	| ownedMembership_comp += NestedDefinitionMember 
	| ownedMembership_comp += VariantUsageMember
	| ownedFeatureMembership_comp += StructureUsageMember
	| ( ownedFeatureMembership_comp += EmptySuccessionMember )?
	  ownedFeatureMembership_comp += IndividualUsageMember
	| ownedFeatureMembership_comp += InitialNodeMember
	  ( ownedFeatureMembership_comp += TargetSuccessionMember )*
	| ( ownedFeatureMembership_comp += EmptySuccessionMember )? 
	  ownedFeatureMembership_comp += ( BehaviorUsageMember | ActionNodeMember )
	  ( ownedFeatureMembership_comp += TargetSuccessionMember )*
	| ownedFeatureMembership_comp += GuardedSuccessionMember
	| ownedImport_comp += Import
;

/* ACTION DEFINITION MEMBERSHIPS */

ParameterMember returns SysML::ParameterMembership :
	( direction = FeatureDirection )? ownedMemberFeature_comp = Parameter
;

InitialNodeMember returns SysML::FeatureMembership :
	DefinitionMemberPrefix 'first' memberFeature = [SysML::Feature|QualifiedName] ';'
	
;

ActionNodeMember returns SysML::FeatureMembership :
	DefinitionMemberPrefix ownedMemberFeature_comp = ActionNode
;

TargetSuccessionMember returns SysML::FeatureMembership :
	DefinitionMemberPrefix ownedMemberFeature_comp = ( TargetSuccession | GuardedTargetSuccession | DefaultTargetSuccession ) ';'
;

GuardedSuccessionMember returns SysML::FeatureMembership :
	ownedMemberFeature_comp = GuardedSuccession ';'
;

/* CALCULATION DEFINITIONS */

CalculationKeyword :
	'calc'
;

CalculationDefKeyword :
	CalculationKeyword 'def'
;

CalculationDefinition returns SysML::CalculationDefinition :
	CalculationDefDeclaration 
	( CalculationDefBody 
	| '=' ownedFeatureMembership_comp += ExpressionMember ';'
	)
;

fragment CalculationDefDeclaration returns SysML::CalculationDefinition :
	DefinitionPrefix? CalculationDefKeyword Identification 
	SuperclassingList? ParameterPart
;

fragment ParameterPart returns SysML::Type :
	  ParameterList ( ReturnParameterPart | EmptyReturnParameterPart )
	| EmptyReturnParameterPart
;

fragment ReturnParameterPart returns SysML::Type :
	ownedFeatureMembership_comp += ReturnParameterMember
;

fragment EmptyReturnParameterPart returns SysML::Type :
	ownedFeatureMembership_comp += EmptyReturnParameterMember
;

EmptyParameter returns SysML::Usage :
	{SysML::ReferenceUsage}
;


fragment CalculationDefBody returns SysML::Type :
	    ';' 
	  | '{' => CalculationDefBodyItem*
	        ( ownedFeatureMembership_comp += ExpressionMember )?
	    '}' 
;

fragment CalculationDefBodyItem returns SysML::Type :
	  documentation_comp += OwnedDocumentation
	| ownedMembership_comp += NestedDefinitionMember 
	| ownedMembership_comp += VariantUsageMember
	| ownedFeatureMembership_comp += StructureUsageMember
	| ownedFeatureMembership_comp += InitialNodeMember
	  ( ownedFeatureMembership_comp += TargetSuccessionMember )*
	| ( ownedFeatureMembership_comp += EmptySuccessionMember )? 
	  ownedFeatureMembership_comp += ( BehaviorUsageMember | ActionNodeMember )
	  ( ownedFeatureMembership_comp += TargetSuccessionMember )*
	| ownedFeatureMembership_comp += GuardedSuccessionMember
	| ownedImport_comp += Import
;

/* CALCULATION DEFINITION MEMBERSHIPS */

ReturnParameterMember returns SysML::ReturnParameterMembership :
	( 'return' )? ownedMemberParameter_comp = Parameter
;

EmptyReturnParameterMember returns SysML::ReturnParameterMembership :
	ownedMemberFeature_comp = EmptyParameter
;

/* STATE DEFINITIONS */

StateKeyword :
	'state'
;

StateDefKeyword :
	StateKeyword 'def'
;

StateDefinition returns SysML::StateDefinition :
	StateDefDeclaration StateDefBody
;

fragment StateDefDeclaration returns SysML::StateDefinition :
	( isAbstract ?= 'abstract')? StateDefKeyword Identification SuperclassingList? ParameterList? 
;

fragment StateDefBody returns SysML::Type :
	';' | '{' StateDefBodyPart '}'
;

fragment StateDefBodyPart returns SysML::Type :
	( ownedFeatureMembership_comp += EntryActionMember
	  ( ownedFeatureMembership_comp += EntryTransitionMember )*
	)?
	( ownedFeatureMembership_comp += DoActionMember )?
	( ownedFeatureMembership_comp += ExitActionMember )?
	
	// Note: Using a syntactic predicate here seems to avoid a possible infinite loop
	// while incrementally parsing.
	=> StateDefBodyItem*
;

fragment StateDefBodyItem returns SysML::Type :
	  documentation_comp += OwnedDocumentation
	| ownedMembership_comp += NestedDefinitionMember 
	| ownedMembership_comp += VariantUsageMember
	| ownedFeatureMembership_comp += StructureUsageMember
	| ( ownedFeatureMembership_comp += EmptySuccessionMember )?
	  ownedFeatureMembership_comp += IndividualUsageMember
	| ownedFeatureMembership_comp += BehaviorUsageMember
		( ownedFeatureMembership_comp += TargetTransitionSuccessionMember )*
	| ownedFeatureMembership_comp += TransitionStepMember
	| ownedImport_comp += Import
;

StateActionUsage returns SysML::ActionUsage :
	EmptyActionUsage ';' | PerformedActionUsage ActionDefBody
;

PerformedActionUsage returns SysML::ActionUsage :
	  {SysML::PerformActionUsage} PerformActionUsageDeclaration 
	| {SysML::AcceptActionUsage} AcceptNodeDeclaration 
	| {SysML::SendActionUsage} SendNodeDeclaration
;

EmptyActionUsage returns SysML::ActionUsage :
	{SysML::ActionUsage}
;

/* STATE DEFINITION MEMBERSHIPS */

EntryActionMember returns SysML::StateSubactionMembership :
	DefinitionMemberPrefix kind = EntryActionKind ownedMemberFeature_comp = StateActionUsage
;

EntryActionKind returns SysML::StateSubactionKind :
	'entry'
;

DoActionMember returns SysML::StateSubactionMembership :
	DefinitionMemberPrefix kind = DoActionKind ownedMemberFeature_comp = StateActionUsage
;

DoActionKind returns SysML::StateSubactionKind :
	'do'
;

ExitActionMember returns SysML::StateSubactionMembership :
	DefinitionMemberPrefix kind = ExitActionKind ownedMemberFeature_comp = StateActionUsage
;

ExitActionKind returns SysML::StateSubactionKind :
	'exit'
;

EntryTransitionMember returns SysML::FeatureMembership :
	DefinitionMemberPrefix 
	( ownedMemberFeature_comp = GuardedTargetSuccession 
	| 'then' ownedMemberFeature_comp = TransitionSuccession
	) ';'
;

TargetTransitionSuccessionMember returns SysML::FeatureMembership :
	DefinitionMemberPrefix ownedMemberFeature_comp = TargetTransitionStep ';'
;

TransitionStepMember returns SysML::FeatureMembership :
	DefinitionMemberPrefix ownedMemberFeature_comp = TransitionStep ';'
;

/* CONSTRAINT DEFINITIONS */

ConstraintKeyword :
	'constraint'
;

ConstraintDefKeyword :
	ConstraintKeyword 'def'
;

ConstraintDefinition returns SysML::ConstraintDefinition :
	ConstraintDefDeclaration ConstraintDefBody  
;

fragment ConstraintDefDeclaration returns SysML::ConstraintDefinition :
	DefinitionPrefix? ConstraintDefKeyword Identification 
	SuperclassingList? ParameterList? EmptyReturnParameterPart 
;

fragment ConstraintDefBody returns SysML::Type :
	  ';' | '{'  ConstraintDefMembers '}'
;

fragment ConstraintDefMembers returns SysML::Type :
	DefinitionBodyItem* 
	( ownedFeatureMembership_comp += ConstraintExpressionMember )?
;

/* CONSTRAINT DEFINITION MEMBERSHIPS */

ConstraintExpressionMember returns SysML::FeatureMembership :
	DefinitionMemberPrefix ownedMemberFeature_comp = Expression
;

/* REQUIREMENT DEFINITIONS */

RequirementKeyword :
	'requirement'
;

RequirementDefKeyword :
	RequirementKeyword 'def'
;

RequirementDefinition returns SysML::RequirementDefinition :
	RequirementDefDeclaration RequirementDefBody  
;

fragment RequirementDefDeclaration returns SysML::RequirementDefinition :
	DefinitionPrefix? RequirementDefKeyword Identification 
	SuperclassingList? RequirementDefParameterList EmptyReturnParameterPart 
;

fragment RequirementDefParameterList returns SysML::Type :
	  ownedFeatureMembership_comp += EmptyParameterMember
	| '(' ownedFeatureMembership_comp += EmptyParameterMember ')'
	| '(' ownedFeatureMembership_comp += ParameterMember ( ',' ownedFeatureMembership_comp += ParameterMember )* ')'
;

fragment RequirementDefBody returns SysML::Type :
	  ';' | '{' => RequirementDefBodyItem* '}'
;

fragment RequirementDefBodyItem returns SysML::Type :
	  DefinitionBodyItem
	| ownedFeatureMembership_comp += RequirementConstraintMember
;

RequirementConstraintUsage returns SysML::ConstraintUsage :
    ( ownedRelationship_comp += Subset UsageSpecialization*
    | 'constraint' UsageDeclaration?
    )
    ConstraintParameterPart ConstraintDefBody
;

/* REQUIREMENT DEFINITION MEMBERSHIPS */

RequirementConstraintMember returns SysML::RequirementConstraintMembership :
	DefinitionMemberPrefix kind = RequirementConstraintKind 
	ownedMemberFeature_comp = RequirementConstraintUsage
;

enum RequirementConstraintKind returns SysML::RequirementConstraintKind :
	assumption = 'assume' | requirement = 'require'
;

/* CASE DEFINITIONS */

CaseKeyword :
	'case'
;

CaseDefKeyword :
	CaseKeyword 'def'
;

CaseDefinition returns SysML::CaseDefinition :
	CaseDefDeclaration CaseDefBody
;

fragment CaseDefDeclaration returns SysML::CaseDefinition :
	DefinitionPrefix? CaseDefKeyword Identification 
	SuperclassingList? SubjectParameterList
;

fragment SubjectParameterList returns SysML::Type :
	  ownedFeatureMembership_comp += EmptySubjectParameterMember
	| ( '(' ownedFeatureMembership_comp += EmptySubjectParameterMember ')'
	  | '(' ownedFeatureMembership_comp += SubjectParameterMember ( ',' ownedFeatureMembership_comp += ParameterMember )* ')'
	  ) ReturnParameterPart?
;

fragment CaseDefBody returns SysML::Type :
	    ';' 
	  | '{' => CaseDefBodyItem*
	        ( ownedFeatureMembership_comp += ExpressionMember )?
	    '}' 
;

fragment CaseDefBodyItem returns SysML::Type :
	  CalculationDefBodyItem
//	| ownedFeatureMembership_comp += SubjectParameterMember 
	| ownedFeatureMembership_comp += ObjectiveMember
	
;

//SubjectParameter returns SysML::Usage :
//	{SysML::Parameter} UsageDeclaration? ValuePart?
//;

ObjectiveRequirementUsage returns SysML::RequirementUsage :
	RequirementUsageDeclaration RequirementDefBody
;

/* CASE DEFINITION MEMBERSHIPS */

//SubjectParameterMember returns SysML::SubjectMembership :
//	DefinitionMemberPrefix 'subject' ownedSubjectParameter_comp = SubjectParameter ';'
//;

EmptySubjectParameterMember returns SysML::SubjectMembership :
	ownedSubjectParameter_comp = EmptyParameter
;

SubjectParameterMember returns SysML::SubjectMembership :
	( direction = InDirection )? ownedSubjectParameter_comp = Parameter
;

InDirection returns SysML::FeatureDirectionKind :
	'in'
;

ObjectiveMember returns SysML::ObjectiveMembership :
	DefinitionMemberPrefix 'objective' ownedObjectiveRequirement_comp = ObjectiveRequirementUsage
;

/* ANALYSIS CASE DEFINITIONS */

AnalysisCaseKeyword :
	'analysis'
;

AnalysisCaseDefKeyword :
	AnalysisCaseKeyword 'def'
;

AnalysisCaseDefinition returns SysML::AnalysisCaseDefinition :
	AnalysisCaseDefDeclaration CaseDefBody
;

fragment AnalysisCaseDefDeclaration returns SysML::AnalysisCaseDefinition :
	DefinitionPrefix? AnalysisCaseDefKeyword Identification 
	SuperclassingList? SubjectParameterList 
;

/* VERIFICATION CASE DEFINITIONS */

VerificationCaseKeyword :
	'verification'
;

VerificationCaseDefKeyword :
	VerificationCaseKeyword 'def'
;

VerificationCaseDefinition returns SysML::VerificationCaseDefinition :
	VerificationCaseDefDeclaration CaseDefBody
;

fragment VerificationCaseDefDeclaration returns SysML::VerificationCaseDefinition :
	DefinitionPrefix? VerificationCaseDefKeyword Identification 
	SuperclassingList? SubjectParameterList 
;

/* VIEW DEFINITION */

ViewKeyword :
	'view'
;

ViewDefKeyword :
	ViewKeyword 'def'
;

ViewDefinition returns SysML::ViewDefinition :
	ViewDefDeclaration DefinitionBody
;

fragment ViewDefDeclaration returns SysML::ViewDefinition :
	DefinitionPrefix? ViewDefKeyword ClassifierDeclarationCompletion
;

/* VIEWPOINT DEFINITION */

ViewpointKeyword :
	'viewpoint'
;

ViewpointDefKeyword :
	ViewpointKeyword 'def'
;

ViewpointDefinition returns SysML::ViewpointDefinition :
	ViewpointDefDeclaration RequirementDefBody  
;

fragment ViewpointDefDeclaration returns SysML::ViewpointDefinition :
	DefinitionPrefix? ViewpointDefKeyword Identification 
	SuperclassingList? RequirementDefParameterList EmptyReturnParameterPart 
;

/* RENDERING DEFINITION */

RenderingKeyword :
	'rendering'
;

RenderingDefKeyword :
	RenderingKeyword 'def'
;

RenderingDefinition returns SysML::RenderingDefinition :
	RenderingDefDeclaration DefinitionBody
;

fragment RenderingDefDeclaration returns SysML::RenderingDefinition :
	DefinitionPrefix? RenderingDefKeyword ClassifierDeclarationCompletion
;

/* USAGES */

fragment UsagePrefix returns SysML::Usage :
	isAbstract ?= 'abstract' | isVariation ?= 'variation'
;

fragment Usage returns SysML::Usage :
	UsageDeclaration? UsageCompletion
;

fragment UsageDeclaration returns SysML::Feature :
	  Identification UsageSpecializationPart?
	| ( Subsettings | Redefinitions ) UsageSpecializationPart?
	| Typings MultiplicityPart? UsageSpecialization*
	| MultiplicityPart UsageSpecialization*
;

fragment UsageSpecializationPart returns SysML::Feature :
	  ( Subsettings | Redefinitions | Typings MultiplicityPart? ) UsageSpecialization*
	| MultiplicityPart UsageSpecialization*
;

fragment MultiplicityPart returns SysML::Feature :
	ownedFeatureMembership_comp += MultiplicityMember 
	// Not using an unordered group here to avoid possible problems
	// with semantics predicates elsewhere.
	( isOrdered ?= 'ordered' isNonunique ?= 'nonunique'? 
	| isNonunique ?= 'nonunique' isOrdered ?= 'ordered'?
	)?
;

Multiplicity returns SysML::MultiplicityRange :
	'[' ( ownedFeatureMembership_comp += NaturalLiteralMember '..' )? 
	      ownedFeatureMembership_comp += UnlimitedNaturalLiteralMember ']'
;

fragment UsageSpecialization returns SysML::Feature :
	Typings | Subsettings | Redefinitions
;

fragment Typings returns SysML::Feature :
	DefinedBy ( ',' ownedRelationship_comp += FeatureTyping )*
;

fragment DefinedBy returns SysML::Feature :
	DefinedByKeyword ownedRelationship_comp += FeatureTyping
;

DefinedByKeyword :
	':' | 'defined' 'by'
;

FeatureTyping returns SysML::FeatureTyping :
	type = [SysML::Type | QualifiedName]
;

fragment SubsettingPart returns SysML::Feature :
	  (  
	  	Subsets ( ',' ownedRelationship_comp += Subset )*
	  | Redefines ( ',' ownedRelationship_comp += Redefinition )*
	  )*
;

fragment Subsettings returns SysML::Feature :
	Subsets ( ',' ownedRelationship_comp += Subset )*
;

fragment Subsets returns SysML::Feature :
	SubsetsKeyword ownedRelationship_comp += Subset 
;

SubsetsKeyword :
	':>' | 'subsets'
;

Subset returns SysML::Subsetting:
	subsettedFeature = [SysML::Feature|QualifiedName]
;

fragment Redefinitions returns SysML::Feature :
	Redefines ( ',' ownedRelationship_comp += Redefinition )*
;

fragment Redefines returns SysML::Feature :
	RedefinesKeyword ownedRelationship_comp += Redefinition
;

RedefinesKeyword :
	':>>' | 'redefines'
;

Redefinition returns SysML::Redefinition:
	redefinedFeature = [SysML::Feature|QualifiedName] 
;

fragment UsageCompletion returns SysML::Usage :
	ValuePart? DefinitionBody
;

fragment ValuePart returns SysML::Feature :
	'=' ownedFeatureMembership_comp += FeatureValue
;

FeatureValue returns SysML::FeatureValue :
	value_comp = Expression
;

/* USAGE MEMBERSHIPS */

MultiplicityMember returns SysML::FeatureMembership :
	ownedMemberFeature_comp = Multiplicity
;

NaturalLiteralMember returns SysML::FeatureMembership :
	ownedMemberFeature_comp = NaturalLiteralExpression
;

UnlimitedNaturalLiteralMember returns SysML::FeatureMembership :
	ownedMemberFeature_comp = UnlimitedNaturalLiteralExpression
;

/* PARAMETERS */

Parameter returns SysML::Usage :
      {SysML::ReferenceUsage} ( ParameterDeclaration | ReferenceUsageKeyword ParameterDeclaration?)
    | {SysML::ItemUsage} ItemUsageKeyword ParameterDeclaration?
    | {SysML::PartUsage} PartUsageKeyword ParameterDeclaration?
    | {SysML::ActionUsage} ActionUsageKeyword ParameterDeclaration?
    | {SysML::CalculationUsage} CalculationUsageKeyword ParameterDeclaration?
    | {SysML::ConstraintUsage} ConstraintUsageKeyword ParameterDeclaration?
;

fragment ParameterDeclaration returns SysML::Usage :
	  Identification ParameterSpecializationPart? 
	| ParameterSpecializationPart
;

fragment ParameterSpecializationPart returns SysML::Feature :
	  ( Subsets | Redefines | DefinedBy MultiplicityPart? ) ParameterSpecialization*
	| MultiplicityPart ParameterSpecialization*
;

fragment ParameterSpecialization returns SysML::Feature :
	DefinedBy | Subsets | Redefines
;

/* REFERENCE USAGES */

ReferenceKeyword :
	'ref'
;

ReferenceUsageKeyword :
	ReferenceKeyword
;

ReferenceUsage returns SysML::ReferenceUsage :
	UsagePrefix? ReferenceUsageKeyword Usage
;

ReferenceEndUsage returns SysML::ReferenceUsage :
	UsagePrefix? ReferenceUsageKeyword? Usage
;

// TODO: Find a better way to handle variant references that using ReferenceUsage.
ReferenceVariantUsage returns SysML::ReferenceUsage :
	  ReferenceUsage
	| ownedRelationship_comp += Subset 
	  UsageSpecialization* DefinitionBody
;

/* ATTRIBUTE USAGES */

AttributeUsageKeyword :
	AttributeKeyword | 'value'
;

fragment AttributeDeclaration returns SysML::AttributeUsage :
	UsagePrefix? AttributeUsageKeyword UsageDeclaration
;

AttributeUsage returns SysML::AttributeUsage :
	UsagePrefix? ( AttributeUsageKeyword Usage | UsageDeclaration UsageCompletion )
;

AttributeVariantUsage returns SysML::AttributeUsage :
	UsagePrefix? AttributeUsageKeyword Usage
;

/* ITEM USAGES */

ItemUsageKeyword :
	ItemKeyword
;

fragment ItemDeclaration returns SysML::ItemUsage :
	UsagePrefix? ItemUsageKeyword UsageDeclaration?
;

ItemUsage returns SysML::ItemUsage :
	UsagePrefix? ItemUsageKeyword Usage
;

ItemRefUsage returns SysML::ItemUsage :
	UsagePrefix? ( 'ref' ItemUsageKeyword | isComposite ?= ItemUsageKeyword ) Usage
;

/* PART USAGES */

PartUsageKeyword :
	PartKeyword
;

fragment PartDeclaration returns SysML::PartUsage :
	UsagePrefix? PartUsageKeyword UsageDeclaration?
;

PartUsage returns SysML::PartUsage :
	UsagePrefix? PartUsageKeyword Usage
;

PartRefUsage returns SysML::PartUsage :
	UsagePrefix? ( 'ref' PartUsageKeyword | isComposite ?= PartUsageKeyword ) Usage
;

/* INDIVIDUAL USAGES */

IndividualUsageKeyword :
	IndividualKeyword
;

TimeSliceKeyword :
	'timeslice'
;

SnapshotKeyword :
	'snapshot'
;

IndividualUsage returns SysML::IndividualUsage :
	UsagePrefix? IndividualUsageKeyword Usage
;

IndividualRefUsage returns SysML::IndividualUsage :
	UsagePrefix? ( 'ref' IndividualUsageKeyword | isComposite ?= IndividualUsageKeyword ) Usage
;

TimeSliceUsage returns SysML::IndividualUsage :
	UsagePrefix? TimeSliceKeyword
	UsageDeclaration? ownedFeatureMembership_comp += TimeSliceFeatureMember 
	UsageCompletion
;

TimeSliceRefUsage returns SysML::IndividualUsage :
	UsagePrefix? ( 'ref' TimeSliceKeyword | isComposite ?= TimeSliceKeyword ) 
	UsageDeclaration? ownedFeatureMembership_comp += TimeSliceFeatureMember 
	UsageCompletion
;

TimeSliceFeature returns SysML::TimeSliceFeature :
	{SysML::TimeSliceFeature}
;

SnapshotUsage returns SysML::IndividualUsage :
	UsagePrefix? SnapshotKeyword
	UsageDeclaration? ownedFeatureMembership_comp += SnapshotFeatureMember 
	UsageCompletion
;

SnapshotRefUsage returns SysML::IndividualUsage :
	UsagePrefix? ( 'ref' SnapshotKeyword | isComposite ?= SnapshotKeyword ) 
	UsageDeclaration? ownedFeatureMembership_comp += TimeSliceFeatureMember 
	UsageCompletion
;

SnapshotFeature returns SysML::SnapshotFeature :
	{SysML::SnapshotFeature}
;

/* INDIVIDUAL MEMBERSHIPS */

TimeSliceFeatureMember returns SysML::FeatureMembership :
	ownedMemberFeature_comp = TimeSliceFeature
;

SnapshotFeatureMember returns SysML::FeatureMembership :
	ownedMemberFeature_comp = SnapshotFeature
;

/* PORT USAGES */

PortUsageKeyword :
	PortKeyword
;

PortUsage returns SysML::PortUsage :
	UsagePrefix? PortUsageKeyword PortUsageDeclaration UsageCompletion
;

PortEndUsage returns SysML::PortUsage :
	UsagePrefix? PortUsageKeyword? PortUsageDeclaration UsageCompletion
;

fragment PortUsageDeclaration returns SysML::Usage :
	  Identification PortTypePart? SubsettingPart
	| PortTypePart SubsettingPart
	| RedefinesKeyword ownedRelationship_comp += Redefinition PortTypePart? SubsettingPart
;

fragment PortTypePart returns SysML::Feature :
	  DefinedByKeyword ( ( ownedRelationship_comp += FeatureTyping | '~' ownedRelationship_comp += ConjugatedPortTyping ) 
	  	( ',' ownedRelationship_comp += FeatureTyping )* | 'any' ) MultiplicityPart?
	| MultiplicityPart
;

ConjugatedPortTyping returns SysML::ConjugatedPortTyping :
	portDefinition = [SysML::PortDefinition | QualifiedName]
;

/* CONNECTION USAGES */

ConnectorKeyword :
	'connect'
;

Connector returns SysML::ConnectionUsage :
	UsagePrefix? ConnectorKeyword ConnectorPart ConnectionDefBody
;

ConnectionUsageKeyword :
	ConnectionKeyword | 'link'
;

ConnectionUsage returns SysML::ConnectionUsage :
	UsagePrefix? ConnectionUsageKeyword UsageDeclaration? ( ConnectorKeyword ConnectorPart )? ConnectionDefBody
;

fragment ConnectorPart returns SysML::ConnectionUsage :
	 ownedFeatureMembership_comp += ConnectorEndMember 'to' ownedFeatureMembership_comp += ConnectorEndMember
	| '(' ownedFeatureMembership_comp += ConnectorEndMember ',' ownedFeatureMembership_comp += ConnectorEndMember
	      ( ',' ownedFeatureMembership_comp += ConnectorEndMember )*
	  ')'
;

ConnectorEnd returns SysML::Feature :
	ownedRelationship_comp += Subset ( ownedFeatureMembership_comp += MultiplicityMember )?
;

MultiplicitySourceEnd returns SysML::Feature :
	{SysML::SourceEnd} ( ownedFeatureMembership_comp += MultiplicityMember )?
;

EmptySourceEnd returns SysML::Feature :
	{SysML::SourceEnd}
;

EmptyTargetEnd returns SysML::Feature :
	{SysML::TargetEnd}
;

/* CONNECTION MEMBERSHIPS */

ConnectorEndMember returns SysML::EndFeatureMembership :
	( memberName = Name '=>' )? ownedMemberFeature_comp = ConnectorEnd
;

MultiplicitySourceEndMember returns SysML::EndFeatureMembership :
	ownedMemberFeature_comp = MultiplicitySourceEnd
;

EmptySourceEndMember returns SysML::EndFeatureMembership :
	ownedMemberFeature_comp = EmptySourceEnd
;

EmptyTargetEndMember returns SysML::EndFeatureMembership :
	ownedMemberFeature_comp = EmptyTargetEnd
;

/* INTERFACE USAGES */

InterfaceUsageKeyword :
	InterfaceKeyword
;

InterfaceUsage returns SysML::InterfaceUsage :	 
	UsagePrefix? InterfaceUsageKeyword InterfaceUsageDeclaration InterfaceDefBody
;

fragment InterfaceUsageDeclaration returns SysML::InterfaceUsage :
	UsageDeclaration? ( ConnectorKeyword ConnectorPart )? | ConnectorPart
;

/* BINDING CONNECTORS */

BindingKeyword :
	'bind'
;

BindingConnector returns SysML::BindingConnector :
	( isAbstract ?= 'abstract' )? BindingKeyword ( UsageDeclaration 'as' )?
	ownedFeatureMembership_comp += ConnectorEndMember '=' ownedFeatureMembership_comp += ConnectorEndMember
	DefinitionBody	
;

/* SUCCESSIONS */

SuccessionKeyword :
	'succession'
;

Succession returns SysML::Succession :
	( isAbstract ?= 'abstract' )? SuccessionKeyword SuccessionDeclaration ConnectionDefBody
;

fragment SuccessionDeclaration returns SysML::Succession :
	( UsageDeclaration? 'first' )?
	ownedFeatureMembership_comp += ConnectorEndMember 'then' ownedFeatureMembership_comp += ConnectorEndMember
;

TargetSuccession returns SysML::Succession :
	'then' ownedFeatureMembership_comp += MultiplicitySourceEndMember ownedFeatureMembership_comp += ConnectorEndMember
;

EmptySuccession returns SysML::Succession :
	'then' ownedFeatureMembership_comp += MultiplicitySourceEndMember ownedFeatureMembership_comp += EmptyTargetEndMember
;

GuardedSuccession returns SysML::TransitionUsage :
	SuccessionKeyword ( UsageDeclaration? 'first' )? 
	ownedFeatureMembership_comp += TransitionSourceMember 
	ownedFeatureMembership_comp += GuardExpressionMember
	'then' ownedFeatureMembership_comp += TransitionSuccessionMember
;

GuardedTargetSuccession returns SysML::TransitionUsage :
	ownedFeatureMembership_comp += GuardExpressionMember
	'then' ownedFeatureMembership_comp += TransitionSuccessionMember
;

DefaultTargetSuccession returns SysML::TransitionUsage :
	'else' ownedFeatureMembership_comp += TransitionSuccessionMember
;

/* ITEM FLOWS */

ItemFlowKeyword :
	'stream'
;

ItemFlow returns SysML::ItemFlow :
	( isAbstract ?= 'abstract' )? ItemFlowKeyword ItemFlowDeclaration DefinitionBody
;

SuccessionItemFlowKeyword :
	'flow'
;

SuccessionItemFlow returns SysML::SuccessionItemFlow :
	( isAbstract ?= 'abstract' )? SuccessionItemFlowKeyword ItemFlowDeclaration DefinitionBody
;

fragment ItemFlowDeclaration returns SysML::ItemFlow :
	( UsageDeclaration? 
      ( 'of'  ownedFeatureMembership_comp += ItemFeatureMember
      | ownedFeatureMembership_comp += EmptyItemFeatureMember
      ) 'from'
    | ownedFeatureMembership_comp += EmptyItemFeatureMember
    )
	ownedFeatureMembership_comp += ItemFlowEndMember 'to' ownedFeatureMembership_comp += ItemFlowEndMember
;

ItemFeatureTyping returns SysML::ItemFeature :
	ownedRelationship_comp += FeatureTyping ( ownedFeatureMembership_comp += MultiplicityMember )?
;

EmptyItemFeature returns SysML::ItemFeature :
	{SysML::ItemFeature}
;

ItemFlowEnd returns SysML::ItemFlowEnd :
	ownedFeatureMembership_comp += ItemFlowFeatureMember
;

ItemFlowFeature returns SysML::ItemFlowFeature :
	ownedRelationship_comp += Redefinition
;

/* ITEM FLOW MEMBERSHIPS */

ItemFeatureMember returns SysML::FeatureMembership :
	( memberName = Name DefinedByKeyword )? ownedMemberFeature_comp = ItemFeatureTyping
;

ItemFlowEndMember returns SysML::EndFeatureMembership :
	ownedMemberFeature_comp = ItemFlowEnd
;

ItemFlowFeatureMember returns SysML::FeatureMembership :
	ownedMemberFeature_comp = ItemFlowFeature
;

EmptyItemFeatureMember returns SysML::FeatureMembership :
	ownedMemberFeature_comp = EmptyItemFeature
;

/* ACTION USAGES */

ActionUsageKeyword :
	ActionKeyword
;

fragment ActionDeclaration returns SysML::ActionUsage :
    UsagePrefix? ActionUsageKeyword ActionUsageDeclaration
;

ActionUsage returns SysML::ActionUsage :
	ActionDeclaration ActionDefBody
;

ActionRefUsage returns SysML::ActionUsage :
	UsagePrefix? ( 'ref' ActionUsageKeyword | isComposite ?= ActionUsageKeyword ) ActionUsageDeclaration ActionDefBody
;

fragment ActionUsageDeclaration returns SysML::Step :
	  UsageDeclaration? ( ValuePart | ActionParameterList )?
;

fragment ActionParameterList returns SysML::Type :
	'(' ( ActionArgument ( ',' ActionArgument )* )? ')'
;

fragment ActionArgument returns SysML::Type :
	  ownedFeatureMembership_comp += ActionParameterMember
	| ownedFeatureMembership_comp += ( ParameterMember | DirectionParameterMember ) 
	  ownedFeatureMembership_comp += ActionParameterFlowMember
;

ActionParameter returns SysML::Usage :
	Parameter ValuePart? | {SysML::ReferenceUsage} ValuePart
;

ActionParameterFlow returns SysML::ItemFlow :
	( ItemFlowKeyword | {SysML::SuccessionItemFlow} SuccessionItemFlowKeyword ) 
	ownedFeatureMembership_comp += EmptyItemFeatureMember 
	'from' ownedFeatureMembership_comp += ItemFlowEndMember
;

PerformActionUsage returns SysML::PerformActionUsage :
	UsagePrefix? 'perform' PerformActionUsageDeclaration ActionDefBody
;

fragment PerformActionUsageDeclaration returns SysML::ActionUsage :
   	( ownedRelationship_comp += Subset UsageSpecialization*
   	| ActionKeyword UsageDeclaration?
   	)
    ( ValuePart | ActionParameterList )?
;

/* ACTION NODES */

ActionNode returns SysML::ActionUsage :
	  SendNode | AcceptNode | ControlNode
;

AcceptNode returns SysML::AcceptActionUsage :
	UsagePrefix? AcceptNodeDeclaration ActionDefBody
;

fragment AcceptNodeDeclaration returns SysML::ActionUsage :
	ownedFeatureMembership_comp += EmptyParameterMember
	'accept' UsageDeclaration? '(' ownedFeatureMembership_comp += ItemFeatureMember ')'
;

SendNode returns SysML::SendActionUsage :
	UsagePrefix? SendNodeDeclaration ActionDefBody
;

fragment SendNodeDeclaration returns SysML::ActionUsage :
	ownedFeatureMembership_comp += EmptyParameterMember ownedFeatureMembership_comp += EmptyItemFeatureMember
	'send' ( UsageDeclaration 'of')? ownedFeatureMembership_comp += ExpressionMember 
	'to' ownedFeatureMembership_comp += ExpressionMember
;

ControlNode returns SysML::ControlNode :
	  MergeNode
	| DecisionNode
	| JoinNode
	| ForkNode
;

MergeNode returns SysML::MergeNode :
	UsagePrefix? isComposite ?= 'merge' UsageDeclaration? ';'
;

DecisionNode returns SysML::DecisionNode :
	UsagePrefix? isComposite ?= 'decide' UsageDeclaration? ';'
;

JoinNode returns SysML::JoinNode :
	UsagePrefix? isComposite ?= 'join' UsageDeclaration? ';'
;

ForkNode returns SysML::ForkNode :
	UsagePrefix? isComposite ?= 'fork' UsageDeclaration? ';'
;

/* ACTION MEMBERSHIPS */

ActionParameterMember returns SysML::ParameterMembership :
	( direction = FeatureDirection )? ownedMemberParameter_comp = ActionParameter
;

ActionParameterFlowMember returns SysML::FeatureMembership :
	ownedMemberFeature_comp = ActionParameterFlow
;

EmptySuccessionMember returns SysML::FeatureMembership :
	ownedMemberFeature_comp = EmptySuccession
;

DirectionParameterMember returns SysML::ParameterMembership :
	( direction = FeatureDirection )? ownedMemberParameter_comp = EmptyParameter
;

EmptyParameterMember returns SysML::ParameterMembership :
	ownedMemberParameter_comp = EmptyParameter
;

/* CALCULATION USAGES */

CalculationUsageKeyword :
	CalculationKeyword
;

fragment CalculationDeclaration returns SysML::CalculationUsage :
    UsagePrefix? CalculationUsageKeyword CalculationUsageDeclaration
;

CalculationUsage returns SysML::CalculationUsage :
	CalculationDeclaration CalculationDefBody
;

CalculationRefUsage returns SysML::CalculationUsage :
	UsagePrefix? ( 'ref' CalculationUsageKeyword | isComposite ?= CalculationUsageKeyword ) CalculationUsageDeclaration CalculationDefBody
;

fragment CalculationUsageDeclaration returns SysML::Step :
	  UsageDeclaration? CalculationParameterPart
;

fragment CalculationParameterPart returns SysML::Feature :
	  ActionParameterList ( CalculationReturnParameterPart | EmptyReturnParameterPart )
	| ValuePart? EmptyReturnParameterPart
;

fragment CalculationReturnParameterPart returns SysML::Feature :
	ownedFeatureMembership_comp += CalculationReturnParameterMember
;

/* CALCULATION MEMBERSHIPS */

CalculationReturnParameterMember returns SysML::ReturnParameterMembership :
	( 'return' )? ownedMemberParameter_comp = ActionParameter
;

/* STATE USAGES */

StateUsageKeyword :
	StateKeyword
;

fragment StateDeclaration returns SysML::StateUsage :
    UsagePrefix? StateUsageKeyword UsageDeclaration? ( ValuePart | ActionParameterList )?
;

StateUsage returns SysML::StateUsage :
	UsagePrefix? StateUsageKeyword ActionUsageDeclaration StateDefBody
;

StateRefUsage returns SysML::StateUsage :
	UsagePrefix? ( 'ref' StateUsageKeyword | isComposite ?= StateUsageKeyword ) ActionUsageDeclaration StateDefBody
;

ExhibitStateUsage returns SysML::ExhibitStateUsage :
    UsagePrefix? 'exhibit' 
    ( ownedRelationship_comp += Subset UsageSpecialization*
    | StateUsageKeyword UsageDeclaration?
    )
    ( ValuePart | ActionParameterList )? StateDefBody
;

/* TRANSITION USAGES */

TransitionUsageKeyword :
	'transition'
;

TransitionStep returns SysML::TransitionUsage :
	TransitionUsageKeyword ( UsageDeclaration? 'first' )? 
	ownedFeatureMembership_comp += TransitionSourceMember 
	( ownedFeatureMembership_comp += TriggerStepMember )?
	( ownedFeatureMembership_comp += GuardExpressionMember )?
	( ownedFeatureMembership_comp += EffectBehaviorMember )?
	'then' ownedFeatureMembership_comp += TransitionSuccessionMember
;

TargetTransitionStep returns SysML::TransitionUsage :
	( ownedFeatureMembership_comp += TriggerStepMember )?
	( ownedFeatureMembership_comp += GuardExpressionMember )?
	( ownedFeatureMembership_comp += EffectBehaviorMember )?
	'then' ownedFeatureMembership_comp += TransitionSuccessionMember
;

TriggerStep returns SysML::AcceptActionUsage :
	ownedFeatureMembership_comp += EmptyParameterMember
	ownedFeatureMembership_comp += ItemFeatureMember
;

EffectBehaviorUsage returns SysML::ActionUsage :
	  EmptyActionUsage | PerformedActionUsage ( '{' ActionDefBodyItem* '}' )?
;

TransitionSuccession returns SysML::Succession :
	ownedFeatureMembership_comp += EmptySourceEndMember 
	ownedFeatureMembership_comp += ConnectorEndMember
;

/* TRANSITION MEMBERSHIPS */

TransitionSourceMember returns SysML::FeatureMembership :
	memberFeature = [SysML::Feature|QualifiedName]
;

TransitionSuccessionMember returns SysML::FeatureMembership :
	ownedMemberFeature_comp = TransitionSuccession
;

TriggerStepMember returns SysML::TransitionFeatureMembership :
	kind = TriggerFeatureKind ownedMemberFeature_comp = TriggerStep
;

enum TriggerFeatureKind returns SysML::TransitionFeatureKind :
	trigger = 'accept'
;

GuardExpressionMember returns SysML::TransitionFeatureMembership :
	kind = GuardFeatureKind ownedMemberFeature_comp = Expression
;

enum GuardFeatureKind returns SysML::TransitionFeatureKind :
	guard = 'if'
;

EffectBehaviorMember returns SysML::TransitionFeatureMembership :
	kind = EffectFeatureKind ownedMemberFeature_comp = EffectBehaviorUsage
;

enum EffectFeatureKind returns SysML::TransitionFeatureKind :
	effect = 'do'
;

/* CONSTRAINT USAGES */

ConstraintUsageKeyword :
	ConstraintKeyword
;

fragment ConstraintDeclaration returns SysML::ConstraintUsage :
    UsagePrefix? ConstraintUsageKeyword UsageDeclaration? ConstraintParameterPart
;

ConstraintUsage returns SysML::ConstraintUsage :
	UsagePrefix? ConstraintUsageKeyword ConstraintUsageDeclaration ConstraintDefBody
;

ConstraintRefUsage returns SysML::ConstraintUsage :
	UsagePrefix? ( 'ref' ConstraintUsageKeyword | isComposite ?= ConstraintUsageKeyword ) ConstraintUsageDeclaration ConstraintDefBody
;

fragment ConstraintUsageDeclaration returns SysML::ConstraintUsage :
	UsageDeclaration? ConstraintParameterPart
;

fragment ConstraintParameterPart returns SysML::ConstraintUsage :
	( ValuePart | ActionParameterList )? EmptyReturnParameterPart
;

AssertConstraintUsage returns SysML::AssertConstraintUsage :
	UsagePrefix? 'assert'
    ( ownedRelationship_comp += Subset 
    | ConstraintUsageKeyword UsageDeclaration? 
    )
    ConstraintParameterPart InvariantPart ConstraintDefBody
;

fragment InvariantPart returns SysML::Invariant :
	ownedFeatureMembership_comp += TrueLiteralMember
;

TrueLiteralExpression returns SysML::LiteralBoolean :
	{SysML::LiteralBoolean}
;

/* CONSTRAINT MEMBERSHIPS */

TrueLiteralMember returns SysML::FeatureMembership :
	ownedMemberFeature_comp = TrueLiteralExpression
;

/* REQUIREMENT USAGES */

RequirementUsageKeyword :
	RequirementKeyword
;

fragment RequirementDeclaration returns SysML::RequirementUsage :
    UsagePrefix? RequirementUsageKeyword UsageDeclaration? ConstraintParameterPart
;

RequirementUsage returns SysML::RequirementUsage :
	UsagePrefix? RequirementUsageKeyword RequirementUsageDeclaration RequirementDefBody
;

RequirementRefUsage returns SysML::RequirementUsage :
	UsagePrefix? ( 'ref' RequirementUsageKeyword | isComposite ?= RequirementUsageKeyword ) RequirementUsageDeclaration RequirementDefBody
;

fragment RequirementUsageDeclaration returns SysML::RequirementUsage :
	UsageDeclaration? ConstraintParameterPart
;

SatisfyRequirementUsage returns SysML::SatisfyRequirementUsage :
	UsagePrefix? 'satisfy'
	( ownedRelationship_comp += Subset UsageSpecialization*
    | RequirementKeyword UsageDeclaration? 
    )
    ( 'by' ownedFeatureMembership_comp += SatisfactionConnectorMember )?
    ConstraintParameterPart InvariantPart RequirementDefBody
;

SatisfactionConnector returns SysML::BindingConnector :
	ownedFeatureMembership_comp += EmptySourceEndMember ownedFeatureMembership_comp += ConnectorEndMember
;

/* REQUIREMENT MEMBERSHIPS */

SatisfactionConnectorMember returns SysML::FeatureMembership :
	ownedMemberFeature_comp = SatisfactionConnector
;

/* CASE USAGES */

CaseUsageKeyword :
	CaseKeyword
;

fragment CaseDeclaration returns SysML::CaseUsage :
    UsagePrefix? CaseUsageKeyword CalculationUsageDeclaration
;

CaseUsage returns SysML::CaseUsage :
	CaseDeclaration CaseDefBody
;

CaseRefUsage returns SysML::CaseUsage :
	UsagePrefix? ( 'ref' CaseUsageKeyword | isComposite ?= CaseUsageKeyword ) CalculationUsageDeclaration CaseDefBody
;

/* ANALYSIS CASE USAGES */

AnalysisCaseUsageKeyword :
	AnalysisCaseKeyword
;

fragment AnalysisCaseDeclaration returns SysML::AnalysisCaseUsage :
    UsagePrefix? AnalysisCaseUsageKeyword CalculationUsageDeclaration
;

AnalysisCaseUsage returns SysML::AnalysisCaseUsage :
	AnalysisCaseDeclaration CaseDefBody
;

AnalysisCaseRefUsage returns SysML::AnalysisCaseUsage :
	UsagePrefix? ( 'ref' AnalysisCaseUsageKeyword | isComposite ?= AnalysisCaseUsageKeyword ) CalculationUsageDeclaration CaseDefBody
;

/* VERIFICATION CASE USAGES */

VerificationCaseUsageKeyword :
	VerificationCaseKeyword
;

fragment VerificationCaseDeclaration returns SysML::VerificationCaseUsage :
    UsagePrefix? VerificationCaseUsageKeyword CalculationUsageDeclaration
;

VerificationCaseUsage returns SysML::VerificationCaseUsage :
	VerificationCaseDeclaration CaseDefBody
;

VerificationCaseRefUsage returns SysML::VerificationCaseUsage :
	UsagePrefix? ( 'ref' VerificationCaseUsageKeyword | isComposite ?= VerificationCaseUsageKeyword ) CalculationUsageDeclaration CaseDefBody
;

/* VIEW USAGES */

ViewUsageKeyword :
	ViewKeyword
;

fragment ViewDeclaration returns SysML::ViewUsage :
    UsagePrefix? ViewKeyword UsageDeclaration?
;

ViewUsage returns SysML::ViewUsage :
	UsagePrefix? ViewUsageKeyword UsageDeclaration? ValuePart? ViewBody
;

ViewRefUsage returns SysML::ViewUsage :
	UsagePrefix? ( 'ref' ViewUsageKeyword | isComposite ?= ViewUsageKeyword ) UsageDeclaration? ValuePart? ViewBody
;

fragment ViewBody returns SysML::ViewUsage :
	';' | '{' ViewBodyItem* '}'
;


fragment ViewBodyItem returns SysML::ViewUsage :
	  DefinitionBodyItem
	| ownedImport_comp += Expose
;

Expose returns SysML::Expose :
	( ownedRelationship_comp += PrefixAnnotation )*
	( visibility = PackageElementVisibilityIndicator )?  
	'expose' (
	  importedPackage = [SysML::Package|Name] ( '::' | '.' ) '*' 
	| importedPackage = [SysML::Package|ColonQualifiedName] '::' '*' 
	| importedPackage = [SysML::Package|DotQualifiedName] '.' '*'
	) ';'
;

/* VIEWPOINT USAGES */

ViewpointUsageKeyword :
	ViewpointKeyword
;

fragment ViewpointDeclaration returns SysML::ViewpointUsage :
    UsagePrefix? ViewpointUsageKeyword UsageDeclaration? ConstraintParameterPart
;

ViewpointUsage returns SysML::ViewpointUsage :
	UsagePrefix? ViewpointUsageKeyword RequirementUsageDeclaration RequirementDefBody
;

ViewpointRefUsage returns SysML::ViewpointUsage :
	UsagePrefix? ( 'ref' ViewpointUsageKeyword | isComposite ?= ViewpointUsageKeyword ) RequirementUsageDeclaration RequirementDefBody
;

/* RENDERING USAGES */

RenderingUsageKeyword :
	RenderingKeyword
;

fragment RenderingDeclaration returns SysML::RenderingUsage :
    UsagePrefix? RenderingKeyword UsageDeclaration?
;

RenderingUsage returns SysML::RenderingUsage :
	UsagePrefix? RenderingUsageKeyword Usage
;

RenderingRefUsage returns SysML::RenderingUsage :
	UsagePrefix? ( 'ref' RenderingUsageKeyword | isComposite ?= RenderingUsageKeyword ) Usage
;

/* EXPRESSIONS */

Expression returns SysML::Expression :
	 ConditionalExpression | QueryPathExpression
;

// Conditional Test Expressions

ConditionalExpression returns SysML::Expression :
	NullCoalescingExpression ( {SysML::OperatorExpression.operand_comp += current}
		operator = ConditionalTestOperator operand_comp += Expression ':' operand_comp += ConditionalExpression
	)?
;

ConditionalTestOperator: 
	'?'
;

// Null Coalescing Expressions

NullCoalescingExpression returns SysML::Expression :
	ConditionalOrExpression ( {SysML::OperatorExpression.operand_comp += current}
		operator = NullCoalescingOperator operand_comp += ConditionalOrExpression )*
;

NullCoalescingOperator :
	'??'
;

// Conditional Logical Expressions

ConditionalOrExpression returns SysML::Expression :
	ConditionalAndExpression ( {SysML::OperatorExpression.operand_comp += current}
		operator = ConditionalOrOperator operand_comp += ConditionalAndExpression )*
;

ConditionalOrOperator :
	'||'
;

ConditionalAndExpression returns SysML::Expression :
	OrExpression ( {SysML::OperatorExpression.operand_comp += current}
		operator = ConditionalAndOperator operand_comp += OrExpression )*
;

ConditionalAndOperator :
	'&&'
;

// Logical Expressions

OrExpression returns SysML::Expression :
	XorExpression ( {SysML::OperatorExpression.operand_comp += current}  
		operator = OrOperator operand_comp += XorExpression )*
;

OrOperator :
	'|'
;

XorExpression returns SysML::Expression :
	AndExpression ( {SysML::OperatorExpression.operand_comp += current}  
		operator = XorOperator operand_comp += AndExpression )*
;

XorOperator :
	'^'
;

AndExpression returns SysML::Expression :
	EqualityExpression ( {SysML::OperatorExpression.operand_comp += current}  
		operator = AndOperator operand_comp += EqualityExpression )*
;

AndOperator :
	'&'
;

// Equality Expressions

EqualityExpression returns SysML::Expression :
	ClassificationExpression ( {SysML::OperatorExpression.operand_comp += current}  
		operator = EqualityOperator operand_comp += ClassificationExpression )*
	
;

EqualityOperator :
	'==' | '!=' 
;

// Classification Expressions

ClassificationExpression returns SysML::Expression :
	RelationalExpression ( {SysML::OperatorExpression.operand_comp += current}
		operator = ClassificationOperator ownedFeatureMembership_comp += TypeReferenceMember )?
;

ClassificationOperator :
	'instanceof' | 'hastype' | 'istype'
;

// Relational Expressions

RelationalExpression returns SysML::Expression :
	AdditiveExpression ( {SysML::OperatorExpression.operand_comp += current}  
		operator = RelationalOperator operand_comp += AdditiveExpression )*
;

RelationalOperator :
	'<' | '>' | '<=' | '>='
;

// Arithmetic Expressions

AdditiveExpression returns SysML::Expression :
	MultiplicativeExpression ( {SysML::OperatorExpression.operand_comp += current}  
		operator = AdditiveOperator operand_comp += MultiplicativeExpression )*	
;

AdditiveOperator :
	'+' | '-' 
;

MultiplicativeExpression returns SysML::Expression :
	ExponentiationExpression ( {SysML::OperatorExpression.operand_comp += current}  
		operator = MultiplicativeOperator operand_comp += ExponentiationExpression )*	
;

MultiplicativeOperator :
	'*' | '/' 
;

ExponentiationExpression returns SysML::Expression :
	UnitsExpression ( {SysML::OperatorExpression.operand_comp += current}  
		operator = ExponentiationOperator operand_comp += UnitsExpression )*		
;

ExponentiationOperator :
	'**'
;

// Units Expressions

UnitsExpression returns SysML::Expression :
	UnaryExpression ( {SysML::OperatorExpression.operand_comp += current} 
		operator = '@' '[' operand_comp += Expression ']' )?		
;

// Unary Expressions

UnaryExpression returns SysML::Expression:
	SequenceAccessExpression | {SysML::OperatorExpression} operator = UnaryOperator operand_comp += SequenceAccessExpression
;

UnaryOperator :
	'+' | '-' | '!' | '~'
;

SequenceAccessExpression returns SysML::Expression :
	 PrimaryExpression 
	 ( {SysML::OperatorExpression.operand_comp += current} operator = '[' operand_comp += Expression ']' )?
;

// Primary Expressions

PrimaryExpression returns SysML::Expression :
	BaseExpression ( {SysML::OperatorExpression.operand_comp += current} '->' 
		operator = Name ( ownedFeatureMembership_comp += BodyMember )+
	)*
;

// Body Expressions

BodyExpression returns SysML::BlockExpression :
	   => ( ownedFeatureMembership_comp += BodyParameterMember ( ownedFeatureMembership_comp += BodyParameterMember )* 
	   '(' ownedFeatureMembership_comp += ExpressionMember ')' )
	 | ownedRelationship_comp += ExpressionTyping
;

BodyParameter returns SysML::Usage :
	{SysML::ReferenceUsage} ( DefinedBy MultiplicityPart? | MultiplicityPart DefinedBy? )?
;

ExpressionTyping returns SysML::FeatureTyping :
	type = [ SysML::Function | QualifiedName ]
;

// Base Expressions

BaseExpression returns SysML::Expression :
	  NullExpression
	| LiteralExpression 
	| FeatureReferenceExpression 
	| InvocationExpression 
	| ExtentExpression
	| SequenceConstructionExpression 
	| '(' Expression ')'
;

FeatureReferenceExpression returns SysML::FeatureReferenceExpression :
	ownedFeatureMembership_comp += FeatureReferenceMember
;

FeatureReference returns SysML::ReferenceUsage :
	ownedRelationship_comp += Subset
;

InvocationExpression returns SysML::InvocationExpression :
	ownedRelationship_comp += FeatureTyping '(' Tuple? ')'
;

fragment Tuple returns SysML::Expression :
	PositionalTuple | NamedTuple
;

fragment PositionalTuple returns SysML::Expression :
	ownedFeatureMembership_comp += ExpressionMember ( ',' ownedFeatureMembership_comp += ExpressionMember )*
;

fragment NamedTuple returns SysML::Expression :
	ownedFeatureMembership_comp += NamedExpressionMember ( ',' ownedFeatureMembership_comp += NamedExpressionMember )*
;

ExtentExpression returns SysML::OperatorExpression :
	  ownedFeatureMembership_comp += TypeReferenceMember '.' operator = 'allInstances' '(' ')'
	| operator = 'all' ownedFeatureMembership_comp += TypeReferenceMember
;

TypeReference returns SysML::Feature :
	ownedRelationship_comp += FeatureTyping
;

SequenceConstructionExpression returns SysML::Expression :
	  {SysML::NullExpression} '{' '}'
	| '{' Expression
	      ( {SysML::OperatorExpression.operand_comp += current} operator = ',' operand_comp += SequenceElementList
	      | {SysML::OperatorExpression.operand_comp += current} operator = '..' operand_comp += Expression
	      )?
	  '}'
;

SequenceElementList returns SysML::Expression :	
	Expression ( {SysML::OperatorExpression.operand_comp += current} operator = ',' operand_comp += SequenceElementList )?
;

NullExpression returns SysML::NullExpression :
	{SysML::NullExpression} 'null'
;

// Literal Expressions

LiteralExpression returns SysML::LiteralExpression :
	BooleanLiteralExpression | StringLiteralExpression | RealLiteralExpression | UnlimitedNaturalLiteralExpression
;

BooleanLiteralExpression returns SysML::LiteralBoolean :
	value = BooleanValue
;

BooleanValue returns Ecore::EBoolean :
	'true' | 'false'
;

StringLiteralExpression returns SysML::LiteralString :
	value = STRING_VALUE
;

RealLiteralExpression returns SysML::LiteralReal:
	value = RealValue
;

RealValue returns Ecore::EDouble:
	DECIMAL_VALUE? '.' ( DECIMAL_VALUE | EXP_VALUE ) | EXP_VALUE
;

NaturalLiteralExpression returns SysML::LiteralInteger:
	value = DECIMAL_VALUE
;

UnlimitedNaturalLiteralExpression returns SysML::LiteralExpression:
	NaturalLiteralExpression | {SysML::LiteralUnbounded} '*'
;

/* EXPRESSION MEMBERSHIPS */

ExpressionMember returns SysML::FeatureMembership :
	ownedMemberFeature_comp = Expression
;

FeatureReferenceMember returns SysML::ReturnParameterMembership :
	ownedMemberFeature_comp = FeatureReference
;

TypeReferenceMember returns SysML::FeatureMembership :
	ownedMemberFeature_comp = TypeReference
;

NamedExpressionMember returns SysML::FeatureMembership :
	memberName = Name '=>' ownedMemberFeature_comp = Expression
;

BodyMember returns SysML::FeatureMembership :
	ownedMemberFeature_comp = BodyExpression
;

BodyParameterMember returns SysML::ParameterMembership :
	memberName = Name ownedMemberParameter_comp = BodyParameter
;

/* QUERY PATH EXPRESSIONS */

QueryPathExpression returns SysML::Expression :
	QueryHeadExpression
	( '[' {SysML::QueryQualifierExpression.operand_comp += current}
         ( ownedFeatureMembership_comp += BodyMember ) ']' )?
	( '/' {SysML::QueryPathStepExpression.operand_comp += current}
		operand_comp += QueryNameExpression
		( '[' {SysML::QueryQualifierExpression.operand_comp += current}
	         ( ownedFeatureMembership_comp += BodyMember ) ']' )?
	)*
;

QueryNameExpression returns SysML::QueryPathExpression :
    /* isParent ?= '..'
    | isDescendants ?= '/'
    | @traversal
    */
    ownedFeatureMembership_comp += FeatureReferenceMember
;

QueryHeadExpression returns SysML::QueryPathExpression :
	'./' ownedFeatureMembership_comp += FeatureReferenceMember
;        

/* NAMES */

Name :
	ID | UNRESTRICTED_NAME
;

QualifiedName :
	Name | ColonQualifiedName | DotQualifiedName
;

ColonQualifiedName :
	Name ('::' Name)+
;

DotQualifiedName :
	Name ('.' Name)+
;

/* TERMINALS */

//terminal NATURAL_VALUE returns Ecore::EInt :
//	('0' | '1'..'9' (('_')? '0'..'9')*) | //DECIMAL 
//	(('0b' | '0B') '0'..'1' (('_')? '0'..'1')*) | // BINARY
//	(('0x'|'0X') ('0'..'9'|'a'..'f'|'A'..'F')  (('_')? ('0'..'9'|'a'..'f'|'A'..'F'))*) | // HEX
//	('0' ('_')? '0'..'7' (('_')? '0'..'7')*) // OCT
//;

terminal DECIMAL_VALUE returns Ecore::EInt :
	'0'..'9' ('0'..'9')*
;

terminal EXP_VALUE :
	DECIMAL_VALUE ('e' | 'E') ('+' | '-')? DECIMAL_VALUE
;

terminal ID : ('a'..'z'|'A'..'Z'|'_') ('a'..'z'|'A'..'Z'|'_'|'0'..'9')* ;
terminal UNRESTRICTED_NAME returns Ecore::EString : 
	'\'' ( '\\' ('b'|'t'|'n'|'f'|'r'|'"'|"'"|'\\') | !('\\'|'\'') )* '\'' ;

terminal STRING_VALUE returns Ecore::EString : 
	'"' ( '\\' ('b'|'t'|'n'|'f'|'r'|'"'|"'"|'\\') | !('\\'|'"') )* '"' ;

terminal ML_COMMENT	: '/*' !'*' -> '*/';
terminal DOCUMENTATION_COMMENT : '/**' -> '*/' ;

terminal ML_NOTE : '//*' -> '*/';
terminal SL_NOTE : '//' (!('\n'|'\r') !('\n'|'\r')*)? ('\r'? '\n')?;

terminal WS : (' '|'\t'|'\r'|'\n')+;
