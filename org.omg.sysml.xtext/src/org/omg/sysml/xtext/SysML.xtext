/*****************************************************************************
 * SysML 2 Pilot Implementation
 * Copyright (c) 2018, 2021 Model Driven Solutions, Inc.
 * Copyright (c) 2018 IncQuery Labs Ltd.
 * Copyright (c) 2019 Maplesoft (Waterloo Maple, Inc.)
 * Copyright (c) 2019 Mgnite Inc.
 *    
 * This program is free software: you can redistribute it and/or modify
 * it under the terms of the GNU Lesser General Public License as published by
 * the Free Software Foundation, either version 3 of the License, or
 * (at your option) any later version.
 *
 * This program is distributed in the hope that it will be useful,
 * but WITHOUT ANY WARRANTY; without even the implied warranty of
 * MERCHANTABILITY or FITNESS FOR A PARTICULAR PURPOSE.  See the
 * GNU Lesser General Public License for more details.
 *
 * You should have received a copy of theGNU Lesser General Public License
 * along with this program.  If not, see <https://www.gnu.org/licenses/>.
 *
 * @license LGPL-3.0-or-later <http://spdx.org/licenses/LGPL-3.0-or-later>
 * 
 * Contributors:
 *  Ed Seidewitz, MDS
 *  Zoltan Kiss, IncQuery
 *  Balazs Grill, IncQuery
 *  Hisashi Miyashita, Maplesoft/Mgnite
 * 
 *****************************************************************************/

grammar org.omg.sysml.xtext.SysML with org.omg.kerml.expressions.xtext.KerMLExpressions

import "http://www.eclipse.org/emf/2002/Ecore" as Ecore
import "http://www.omg.org/spec/SysML/2.0" as SysML

/* ROOT PACKAGE */

RootNamespace returns SysML::Namespace :
	{SysML::Namespace} PackageBodyElement*
;

/* BASIC ELEMENTS */

fragment Identification returns SysML::Element :
	  'id' humanId = Name ( name = Name )?
	| name = Name
;

/* ANNOTATIONS */

/* Comments */

Comment returns SysML::Comment :
	( 'comment' Identification? 'about' ownedRelationship_comp += Annotation
	  ( ',' ownedRelationship_comp += Annotation )*
	| ( 'comment' Identification? )? ownedRelationship_comp += EmptyAnnotation
	)
	body = REGULAR_COMMENT
;

Annotation returns SysML::Annotation :
	annotatedElement = [SysML::Element|QualifiedName]
;

EmptyAnnotation returns SysML::Annotation :
	{SysML::Annotation}
;

/* Documentation */

OwnedDocumentation returns SysML::Documentation :
	documentingComment_comp = DocumentationComment
;

DocumentationComment returns SysML::Comment :
	'doc' ( 'id' humanId = Name )? body = REGULAR_COMMENT
;

PrefixDocumentation returns SysML::Annotation :
	ownedRelatedElement_comp += PrefixDocumentationComment
;

PrefixDocumentationComment returns SysML::Comment :
	( 'doc' ( 'id' humanId = Name )? )? body = DOCUMENTATION_COMMENT
;

/* Textual Representation */

TextualRepresentation returns SysML::TextualRepresentation :
    ( 'rep' Identification? 'about' ownedRelationship_comp += Annotation
    | ( 'rep' Identification? )? ownedRelationship_comp += EmptyAnnotation
    )
    'language' language = STRING_VALUE 
    body = REGULAR_COMMENT
;

/* Annotating Features */

AnnotatingFeature returns SysML::AnnotatingFeature :
	( '@' | 'metadata' ) AnnotatingFeatureDeclaration 
	( 'about' ownedRelationship_comp += Annotation ( ',' ownedRelationship_comp += Annotation )* 
	| ownedRelationship_comp += EmptyAnnotation
	)	
	AnnotatingFeatureBody
;

fragment AnnotatingFeatureDeclaration returns SysML::AnnotatingFeature :
	( Identification DefinedByKeyword )? ownedRelationship_comp += metadataTyping
;

metadataTyping returns SysML::FeatureTyping :
	type = [SysML::DataType | QualifiedName]
;

fragment AnnotatingFeatureBody returns SysML::AnnotatingFeature :
	';' | '{' ( ownedRelationship_comp += MetadataFeatureMember )* '}'
;

MetadataFeatureMember returns SysML::FeatureMembership :
	ownedMemberFeature_comp = MetadataFeature
;

MetadataFeature returns SysML::MetadataFeature :
	'feature'? ( ':>>' | 'redefines')? ownedRelationship_comp += OwnedRedefinition 
	'=' metadataFeatureValue_comp = MetadataFeatureValue ';'
;

MetadataFeatureValue returns SysML::MetadataFeatureValue :
	metadataValue_comp = OwnedExpression
;

/* PACKAGES */

/* Packages */

Package returns SysML::Package :
	PackageDeclaration PackageBody 
;

fragment PackageDeclaration returns SysML::Package :
	'package' Identification?
;

fragment PackageBody returns SysML::Package :
	  ';' 
	| '{' ( // Note: PackageBodyElement is expanded here to avoid
			// infinite loops in the incremental parser.
		    ownedRelationship_comp += OwnedDocumentation
	      | ownedRelationship_comp += PackageMember 
	      | ownedRelationship_comp += ElementFilterMember
	      | ownedRelationship_comp += Import )* 
	  '}'
;

/* Package Bodies */

fragment PackageBodyElement returns SysML::Namespace :
	  ownedRelationship_comp += OwnedDocumentation
	| ownedRelationship_comp += ElementFilterMember
	| ownedRelationship_comp += PackageMember 
	| ownedRelationship_comp += Import
;

ElementFilterMember returns SysML::ElementFilterMembership :
	'filter' condition_comp = OwnedExpression ';'
;

PackageMember returns SysML::Membership : 
	( ownedRelationship_comp += PrefixDocumentation )*
    ( visibility = BasicVisibilityIndicator )?		
	( NonUsagePackageMember | UsagePackageMember )
;

fragment NonUsagePackageMember returns SysML::Membership :
	  ownedMemberElement_comp = DefinitionElement
	| ( 'alias' | 'import' ) memberElement = [SysML::Element|QualifiedName] ( 'as' memberName = Name )? ';'
;

fragment UsagePackageMember returns SysML::Membership :
	  ownedMemberElement_comp = UsageElement
;

Import returns SysML::Import :
	( ownedRelationship_comp += PrefixDocumentation )*
	( visibility = BasicVisibilityIndicator )?  
	'import' ( ImportedNamespace | ImportedFilterPackage ) ';'
;

fragment ImportedNamespace returns SysML::Import :
	(
	  importedNamespace = [SysML::Namespace|Name] ( '::' | '.' ) 
	| importedNamespace = [SysML::Namespace|ColonQualifiedName] '::' 
	| importedNamespace = [SysML::Namespace|DotQualifiedName] '.'
	)
	( '*' | isRecursive ?= '**' )
;

fragment ImportedFilterPackage returns SysML::Import :
	ownedRelatedElement_comp += FilterPackage
;

FilterPackage returns SysML::Package :
	ownedRelationship_comp += FilterPackageImport
	( ownedRelationship_comp += FilterPackageMember )+
;

FilterPackageImport returns SysML::Import :
	ImportedNamespace
;

FilterPackageMember returns SysML::ElementFilterMembership :
	visibility = FilterPackageMemberVisibility condition_comp = OwnedExpression ']'
;

enum FilterPackageMemberVisibility returns SysML::VisibilityKind :
	private = '['
;

enum BasicVisibilityIndicator returns SysML::VisibilityKind:
	public = 'public' | private = 'private'
;

/* Package Elements */

DefinitionElement returns SysML::Element :
	  Package
	| Comment
	| TextualRepresentation
	| AnnotatingFeature
	| Dependency	  
	| AttributeDefinition
	| EnumerationDefinition
	| ItemDefinition
	| PartDefinition
	| IndividualDefinition
	| ConnectionDefinition
	| InterfaceDefinition
	| AllocationDefinition
	| PortDefinition
	| ActionDefinition
	| CalculationDefinition
	| StateDefinition
	| ConstraintDefinition
	| RequirementDefinition
	| CaseDefinition
	| AnalysisCaseDefinition
	| VerificationCaseDefinition
	| ViewDefinition
	| ViewpointDefinition
	| RenderingDefinition
;
	
UsageElement returns SysML::Usage :
	  AttributeUsage
	| EnumerationUsage
	| ItemUsage
	| PartUsage
	| IndividualUsage
	| TimeSliceUsage
	| SnapshotUsage
	| PortUsage
	| ConnectionUsage
	| AssertConstraintUsage
	| Connector
	| InterfaceUsage
	| AllocationUsage
	| ActionUsage
	| CalculationUsage
	| StateUsage
	| ConstraintUsage
	| RequirementUsage
	| SatisfyRequirementUsage
	| CaseUsage
	| AnalysisCaseUsage
	| VerificationCaseUsage
	| ViewUsage
	| ViewpointUsage
	| RenderingUsage
;

/* TYPES */

fragment TypeBody returns SysML::Type :
	';' | 
	'{' ( ownedRelationship_comp += OwnedDocumentation
		| ownedRelationship_comp += NonFeatureTypeMember 
		| ownedRelationship_comp += FeatureTypeMember 
		| ownedRelationship_comp += Import
	)* '}'
;

NonFeatureTypeMember returns SysML::Membership : 
	TypeMemberPrefix NonUsagePackageMember
;

FeatureTypeMember returns SysML::FeatureMembership : 
	FeatureMember | EndFeatureMember
;

fragment TypeMemberPrefix returns SysML::Membership :
	( ownedRelationship_comp += PrefixDocumentation )*
    ( visibility = VisibilityIndicator )?
;

enum VisibilityIndicator returns SysML::VisibilityKind :
	public = 'public' | private = 'private' | protected = 'protected'
;

/* Feature Memberships */

FeatureMember returns SysML::FeatureMembership :
	TypeMemberPrefix direction = FeatureDirection 
	ownedMemberFeature_comp = FeatureElement
;

EndFeatureMember returns SysML::EndFeatureMembership :
	TypeMemberPrefix 'end' direction = FeatureDirection 
	ownedMemberFeature_comp = FeatureElement
;

enum FeatureDirection returns SysML::FeatureDirectionKind:
	in = 'in' | out = 'out' | inout = 'inout'
;

FeatureElement returns SysML::Feature :
      UsageElement
    | BindingConnector
    | Succession
    | ItemFlow
    | SuccessionItemFlow
;

/* CLASSIFIERS */

/* Superclassing */

fragment SuperclassingPart returns SysML::Classifier :
	SpecializesKeyword ownedRelationship_comp += OwnedSuperclassing 
	( ',' ownedRelationship_comp += OwnedSuperclassing )*
;

SpecializesKeyword :
	':>' | 'specializes'
;

OwnedSuperclassing returns SysML::Superclassing:
	superclass = [SysML::Classifier | QualifiedName]
;

/* FEATURES */

/* Features */

fragment FeatureDeclaration returns SysML::Feature :
	  Identification FeatureSpecializationPart?
	| FeatureSpecializationPart
;

fragment FeatureSpecializationPart returns SysML::Feature :
	  ( -> FeatureSpecialization )+ MultiplicityPart? FeatureSpecialization*
	| MultiplicityPart FeatureSpecialization*
;

fragment MultiplicityPart returns SysML::Feature :
	ownedRelationship_comp += MultiplicityMember 
	// NOTE: Can't use "&" here because of use of predication elsewhere.
	( isOrdered ?= 'ordered' isNonunique ?= 'nonunique'?
	| isNonunique ?= 'nonunique' isOrdered ?= 'ordered'?
	)?
;

fragment FeatureSpecialization returns SysML::Feature :
	Typings | Subsettings | Redefinitions
;

fragment Typings returns SysML::Feature :
	TypedBy ( ',' ownedRelationship_comp += OwnedFeatureTyping )*
;

fragment TypedBy returns SysML::Feature :
	DefinedByKeyword ownedRelationship_comp += OwnedFeatureTyping
;

DefinedByKeyword :
	':' | 'defined' 'by'
;

fragment Subsettings returns SysML::Feature :
	Subsets ( ',' ownedRelationship_comp += OwnedSubsetting )*
;

fragment Subsets returns SysML::Feature :
	SubsetsKeyword ownedRelationship_comp += OwnedSubsetting 
;

SubsetsKeyword :
	':>' | 'subsets'
;

fragment Redefinitions returns SysML::Feature :
	Redefines ( ',' ownedRelationship_comp += OwnedRedefinition )*
;

fragment Redefines returns SysML::Feature :
	RedefinesKeyword ownedRelationship_comp += OwnedRedefinition
;

RedefinesKeyword :
	':>>' | 'redefines'
;

/* Feature Typing, Subsetting and Redefinition */

@Override
OwnedFeatureTyping returns SysML::FeatureTyping :
	type = [SysML::Type | QualifiedName]
;

OwnedSubsetting returns SysML::Subsetting:
	subsettedFeature = [SysML::Feature|QualifiedName]
;

OwnedRedefinition returns SysML::Redefinition:
	redefinedFeature = [SysML::Feature | QualifiedName] 
;

/* Multiplicity */

MultiplicityMember returns SysML::Membership :
	ownedMemberElement_comp = Multiplicity
;

Multiplicity returns SysML::MultiplicityRange :
	// TODO: Allow general expressions for bounds. (Causes LL parsing issues.)
	'[' ( ownedRelationship_comp += LiteralIntegerMember '..' )? 
	      ownedRelationship_comp += LiteralUnlimitedNaturalMember ']'
;

LiteralIntegerMember returns SysML::Membership :
	ownedMemberElement_comp = LiteralInteger
;

LiteralUnlimitedNaturalMember returns SysML::Membership :
	ownedMemberElement_comp = LiteralUnlimitedNatural
;

/* CONNECTORS */

/* Connectors */

ConnectorEndMember returns SysML::EndFeatureMembership :
	( memberName = Name '=>' )? ownedMemberFeature_comp = ConnectorEnd
;

ConnectorEnd returns SysML::Feature :
	ownedRelationship_comp += OwnedSubsetting 
	( ownedRelationship_comp += MultiplicityMember )?
;

/* Binding Connectors */

BindingKeyword :
	'bind'
;

BindingConnector returns SysML::BindingConnector :
	( isAbstract ?= 'abstract' )? BindingKeyword 
	BindingConnectorDeclaration TypeBody	
;

fragment BindingConnectorDeclaration returns SysML::BindingConnector :
	( FeatureDeclaration 'as' )?
	ownedRelationship_comp += ConnectorEndMember '=' 
	ownedRelationship_comp += ConnectorEndMember
;

/* Successions */

SuccessionKeyword :
	'succession'
;

Succession returns SysML::Succession :
	( isAbstract ?= 'abstract' )? SuccessionKeyword SuccessionDeclaration TypeBody
;

fragment SuccessionDeclaration returns SysML::Succession :
	( FeatureDeclaration? 'first' )?
	ownedRelationship_comp += ConnectorEndMember 'then' 
	ownedRelationship_comp += ConnectorEndMember
;

/* INTERACTIONS */

/* Item Flows */

ItemFlowKeyword :
	'stream'
;

ItemFlow returns SysML::ItemFlow :
	( isAbstract ?= 'abstract' )? ItemFlowKeyword 
	ItemFlowDeclaration TypeBody
;

SuccessionItemFlowKeyword :
	'flow'
;

SuccessionItemFlow returns SysML::SuccessionItemFlow :
	( isAbstract ?= 'abstract' )? SuccessionItemFlowKeyword 
	ItemFlowDeclaration TypeBody
;

fragment ItemFlowDeclaration returns SysML::ItemFlow :
	( FeatureDeclaration? 
      ( 'of'  ownedRelationship_comp += ItemFeatureMember
      | ownedRelationship_comp += EmptyItemFeatureMember
      ) 'from'
    | ownedRelationship_comp += EmptyItemFeatureMember
    )
	ownedRelationship_comp += ItemFlowEndMember 'to' 
	ownedRelationship_comp += ItemFlowEndMember
;

ItemFeatureMember returns SysML::FeatureMembership :
	( memberName = Name DefinedByKeyword )? ownedMemberFeature_comp = ItemFeature
;

ItemFeature returns SysML::ItemFeature :
	  ownedRelationship_comp += OwnedFeatureTyping 
	  ( ownedRelationship_comp += MultiplicityMember )?
	| ownedRelationship_comp += MultiplicityMember
	  ( ownedRelationship_comp += OwnedFeatureTyping )?
;

EmptyItemFeature returns SysML::ItemFeature :
	{SysML::ItemFeature}
;

ItemFlowEndMember returns SysML::EndFeatureMembership :
	ownedMemberFeature_comp = ItemFlowEnd
;

ItemFlowEnd returns SysML::ItemFlowEnd :
	ownedRelationship_comp += ItemFlowFeatureMember
;

EmptyItemFeatureMember returns SysML::FeatureMembership :
	ownedMemberFeature_comp = EmptyItemFeature
;

EmptyItemFlowEnd returns SysML::ItemFlowEnd :
	ownedRelationship_comp += EmptyItemFlowFeatureMember
;

ItemFlowFeatureMember returns SysML::FeatureMembership :
	ownedMemberFeature_comp = ItemFlowFeature
;

ItemFlowFeature returns SysML::ItemFlowFeature :
	ownedRelationship_comp += OwnedRedefinition
;

EmptyItemFlowEndMember returns SysML::EndFeatureMembership :
	ownedMemberFeature_comp = EmptyItemFlowEnd
;

EmptyItemFlowFeatureMember returns SysML::FeatureMembership :
	ownedMemberFeature_comp = EmptyItemFlowFeature
;

EmptyItemFlowFeature returns SysML::ItemFlowFeature :
	ownedRelationship_comp += EmptyRedefinition
;

EmptyRedefinition returns SysML::Redefinition :
	{SysML::Redefinition}
;

/* DEPENDENCIES */

Dependency returns SysML::Dependency :
	'dependency' ( Identification? 'from' )?
    client += [SysML::Element|QualifiedName] ( ',' client += [SysML::Element|QualifiedName] )* 'to'
    supplier += [SysML::Element|QualifiedName] ( ',' supplier += [SysML::Element|QualifiedName] )* ';'
;

/* DEFINITION AND USAGE */

/* Definitions */

fragment DefinitionPrefix returns SysML::Definition :
	isAbstract ?= 'abstract' | isVariation ?= 'variation'
;

fragment Definition returns SysML::Definition :
	DefinitionDeclaration DefinitionBody
;

fragment DefinitionDeclaration returns SysML::Definition :	
	Identification? SuperclassingPart?
;

fragment DefinitionBody returns SysML::Type :
	  ';' 
	| '{' DefinitionBodyItem* '}'
;

fragment DefinitionBodyItem returns SysML::Type :
	  ownedRelationship_comp += OwnedDocumentation
	| ownedRelationship_comp += NestedDefinitionMember 
	| ownedRelationship_comp += VariantUsageMember
	| ownedRelationship_comp += NestedUsageMember 
	| ( ownedRelationship_comp += EmptySuccessionMember )?
	  ownedRelationship_comp += IndividualUsageMember
	| => ownedRelationship_comp += Import	
;

NestedDefinitionMember returns SysML::Membership : 
	NonFeatureTypeMember
;

VariantUsageMember returns SysML::VariantMembership :
	DefinitionMemberPrefix 'variant' 
	ownedVariantUsage_comp = VariantUsageElement
;

NestedUsageMember returns SysML::FeatureMembership :
	StructureUsageMember | BehaviorUsageMember | FlowUsageMember
;

StructureUsageMember returns SysML::FeatureMembership :
	DefinitionMemberPrefix ownedMemberFeature_comp = StructureUsageElement
;

BehaviorUsageMember returns SysML::FeatureMembership :
	DefinitionMemberPrefix ownedMemberFeature_comp = BehaviorUsageElement
;

IndividualUsageMember returns SysML::FeatureMembership :
	DefinitionMemberPrefix ownedMemberFeature_comp = IndividualUsageElement
;

FlowUsageMember returns SysML::FeatureMembership :
	DefinitionMemberPrefix direction = FeatureDirection
	ownedMemberFeature_comp = FlowUsageElement
;

fragment DefinitionMemberPrefix returns SysML::Membership :
	( ownedRelationship_comp += PrefixDocumentation )*
    ( visibility = VisibilityIndicator )?
;

/* Usages */

fragment UsagePrefix returns SysML::Usage :
	isAbstract ?= 'abstract' | isVariation ?= 'variation'
;

fragment Usage returns SysML::Usage :
	UsageDeclaration? UsageCompletion
;

fragment UsageDeclaration returns SysML::Feature :
	  FeatureDeclaration
;

fragment UsageCompletion returns SysML::Usage :
	ValueOrFlowPart? UsageBody
;

fragment UsageBody returns SysML::Usage :
	DefinitionBody
;

fragment ValuePart returns SysML::Feature :
	  '=' ownedRelationship_comp += FeatureValue
;

fragment ValueOrFlowPart returns SysML::Feature :
	ValuePart | ownedRelationship_comp += SourceItemFlowMember
;

FeatureValue returns SysML::FeatureValue :
	value_comp = OwnedExpression
;

SourceItemFlowMember returns SysML::Membership :
	ownedMemberElement_comp = SourceItemFlow
;

SourceItemFlow returns SysML::ItemFlow :
	( ItemFlowKeyword | {SysML::SuccessionItemFlow} SuccessionItemFlowKeyword ) 
	ownedRelationship_comp += EmptyItemFeatureMember 
	'from' ownedRelationship_comp += ItemFlowEndMember
	ownedRelationship_comp += EmptyItemFlowEndMember
;

/* Reference Usages */

ReferenceKeyword :
	'ref'
;

ReferenceUsageKeyword :
	ReferenceKeyword
;

ReferenceUsage returns SysML::ReferenceUsage :
	UsagePrefix? ReferenceUsageKeyword Usage
;

ReferenceEndUsage returns SysML::ReferenceUsage :
	UsagePrefix? ReferenceUsageKeyword? Usage
;

// TODO: Find a better way to handle variant references that using ReferenceUsage.
ReferenceVariantUsage returns SysML::ReferenceUsage :
	  ReferenceUsage
	| ownedRelationship_comp += OwnedSubsetting 
	  FeatureSpecialization* UsageBody
;

/* Body Elements */

StructureUsageElement returns SysML::Feature :
	  ReferenceUsage
	| AttributeUsage
	| EnumerationUsage
	| ItemRefUsage
	| PartRefUsage
	| ViewRefUsage
	| RenderingRefUsage
	| PortUsage
	| ConnectionUsage
	| Connector
	| InterfaceUsage
	| AllocationUsage
	| BindingConnector
	| Succession
	| ItemFlow
	| SuccessionItemFlow
;

IndividualUsageElement returns SysML::Usage :
	  IndividualRefUsage
	| TimeSliceRefUsage
	| SnapshotRefUsage
;

BehaviorUsageElement returns SysML::Usage :
	  ActionRefUsage
	| CalculationRefUsage
	| StateRefUsage
	| ConstraintRefUsage
	| RequirementRefUsage
	| CaseRefUsage
	| AnalysisCaseRefUsage
	| VerificationCaseRefUsage
	| ViewpointRefUsage
	| PerformActionUsage
	| ExhibitStateUsage
	| AssertConstraintUsage
	| SatisfyRequirementUsage
;

VariantUsageElement returns SysML::Usage :
	  ReferenceVariantUsage
	| AttributeVariantUsage
	| ItemRefUsage
	| PartRefUsage
	| PortUsage
	| ConnectionUsage
	| Connector
	| InterfaceUsage
	| AllocationUsage
	| IndividualUsageElement
	| BehaviorUsageElement
;

FlowUsageElement returns SysML::Usage :
	  ReferenceUsage
	| AttributeUsage
	| EnumerationUsage
	| ItemFlowUsage
	| PartFlowUsage
	| ViewFlowUsage
	| RenderingFlowUsage
	| ActionFlowUsage
	| CalculationFlowUsage
	| StateFlowUsage
	| ConstraintFlowUsage
	| RequirementFlowUsage
	| CaseFlowUsage
	| AnalysisCaseFlowUsage
	| VerificationCaseFlowUsage
	| ViewpointFlowUsage
;

/* ATTRIBUTES */

AttributeKeyword :
	'attribute'
;

AttributeDefKeyword :
	AttributeKeyword 'def' | 'value' 'type'
;

AttributeUsageKeyword :
	AttributeKeyword | 'value'
;

AttributeDefinition returns SysML::AttributeDefinition :
	DefinitionPrefix? AttributeDefKeyword Definition
;

AttributeUsage returns SysML::AttributeUsage :
	UsagePrefix? 
	( AttributeUsageKeyword Usage 
	| UsageDeclaration ValueOrFlowPart? UsageBody
	)
;

AttributeVariantUsage returns SysML::AttributeUsage :
	UsagePrefix? AttributeUsageKeyword Usage
;

/* ENUMERATIONS */

EnumerationKeyword :
	'enum'
;
EnumerationDefKeyword :
	EnumerationKeyword 'def'
;

EnumerationUsageKeyword :
	EnumerationKeyword
;

EnumerationDefinition returns SysML::EnumerationDefinition :
	EnumerationDefKeyword DefinitionDeclaration EnumerationBody
;

fragment EnumerationBody returns SysML::EnumerationDefinition :
	  ';' 
	| '{' ( => ownedRelationship_comp += EnumerationUsageMember )* '}'
;

EnumerationUsageMember returns SysML::VariantMembership :
	DefinitionMemberPrefix ownedVariantUsage_comp = EnumeratedValue
;

EnumeratedValue returns SysML::EnumerationUsage :
	EnumerationUsageKeyword? Usage
;

EnumerationUsage returns SysML::EnumerationUsage :
	EnumerationUsageKeyword Usage
;

/* ITEMS */

ItemKeyword :
	'item'
;

ItemDefKeyword :
	ItemKeyword 'def'
;

ItemUsageKeyword :
	ItemKeyword
;

ItemDefinition returns SysML::ItemDefinition :
	DefinitionPrefix? ItemDefKeyword Definition
;

ItemUsage returns SysML::ItemUsage :
	UsagePrefix? ItemUsageKeyword Usage
;

ItemFlowUsage returns SysML::ItemUsage :
	UsagePrefix? 'ref'? ItemUsageKeyword Usage
;

ItemRefUsage returns SysML::ItemUsage :
	UsagePrefix? ( 'ref' ItemUsageKeyword | isComposite ?= ItemUsageKeyword ) Usage
;

/* PARTS */

PartKeyword :
	'part'
;

PartDefKeyword :
	PartKeyword 'def' | 'block'
;

PartUsageKeyword :
	PartKeyword
;

PartDefinition returns SysML::PartDefinition :
	DefinitionPrefix? PartDefKeyword Definition
;

PartUsage returns SysML::PartUsage :
	UsagePrefix? PartUsageKeyword Usage
;

PartFlowUsage returns SysML::PartUsage :
	UsagePrefix? 'ref'? PartUsageKeyword Usage
;

PartRefUsage returns SysML::PartUsage :
	UsagePrefix? ( 'ref' PartUsageKeyword | isComposite ?= PartUsageKeyword ) Usage
;

/* PORTS */

/* Port Definitions */

PortKeyword :
	'port'
;

PortDefKeyword :
	PortKeyword 'def' | 'interface' 'block'
;

PortDefinition returns SysML::PortDefinition :
	DefinitionPrefix? PortDefKeyword Definition
	ownedRelationship_comp += ConjugatedPortDefinitionMember
;

ConjugatedPortDefinitionMember returns SysML::Membership :
	ownedMemberElement_comp = ConjugatedPortDefinition
;

ConjugatedPortDefinition returns SysML::ConjugatedPortDefinition :
	ownedRelationship_comp += PortConjugation
;

PortConjugation returns SysML::PortConjugation :
	{SysML::PortConjugation}
;

/* Port Usages */

PortUsageKeyword :
	PortKeyword
;

PortUsage returns SysML::PortUsage :
	UsagePrefix? PortUsageKeyword PortUsageDeclaration UsageCompletion
;

PortEndUsage returns SysML::PortUsage :
	UsagePrefix? PortUsageKeyword? PortUsageDeclaration UsageCompletion
;

fragment PortUsageDeclaration returns SysML::Usage :
	  Identification PortSpecializationPart?
	| PortSpecializationPart
;

fragment PortSpecializationPart returns SysML::Usage :
	  ( -> PortSpecialization )+ MultiplicityPart? PortSpecialization*
	| MultiplicityPart PortSpecialization*
;

fragment PortSpecialization returns SysML::Usage :
	PortTypings | Subsettings | Redefinitions
;

fragment PortTypings returns SysML::Feature :
	  DefinedByKeyword ownedRelationship_comp += PortTyping 
	  ( ',' ownedRelationship_comp += PortTyping )*
;

PortTyping returns SysML::FeatureTyping :
	OwnedFeatureTyping | ConjugatedPortTyping
;

ConjugatedPortTyping returns SysML::ConjugatedPortTyping :
	'~' portDefinition = [SysML::PortDefinition | QualifiedName]
;

/* CONNECTIONS */

/* Connection Definitions */

ConnectionKeyword :
	'connection'
;

ConnectionDefKeyword :
	ConnectionKeyword 'def' | 'assoc' 'block' 
;

ConnectionDefinition returns SysML::ConnectionDefinition :
	DefinitionPrefix? ConnectionDefKeyword 
	DefinitionDeclaration ConnectionBody
;

fragment ConnectionBody returns SysML::Type :
	  ';'
	| '{' (
		  ownedRelationship_comp += OwnedDocumentation
		| ownedRelationship_comp += NestedDefinitionMember 
		| ownedRelationship_comp += VariantUsageMember
		| ownedRelationship_comp += ConnectionUsageMember
		| ( ownedRelationship_comp += EmptySuccessionMember )?
		  ownedRelationship_comp += IndividualUsageMember
		| ownedRelationship_comp += Import
	  )* '}'
;

ConnectionUsageMember returns SysML::FeatureMembership :
	NestedUsageMember | ConnectionEndMember
;

ConnectionEndMember returns SysML::EndFeatureMembership :
	DefinitionMemberPrefix 'end' ownedMemberFeature_comp = ConnectionEndElement
;

ConnectionEndElement returns SysML::Feature :
	  ReferenceEndUsage
	| ItemRefUsage
	| PartRefUsage
	| PortUsage
	| ActionRefUsage
	| CalculationRefUsage
	| StateRefUsage
;

/* Connection Usages */

ConnectorKeyword :
	'connect'
;

Connector returns SysML::ConnectionUsage :
	UsagePrefix? ConnectorKeyword ConnectorPart ConnectionBody
;

ConnectionUsageKeyword :
	ConnectionKeyword | 'link'
;

ConnectionUsage returns SysML::ConnectionUsage :
	UsagePrefix? ConnectionUsageKeyword UsageDeclaration? 
	( ConnectorKeyword ConnectorPart )? ConnectionBody
;

fragment ConnectorPart returns SysML::ConnectionUsage :
	BinaryConnectorPart | NaryConnectorPart
;

fragment BinaryConnectorPart returns SysML::Connector :
	ownedRelationship_comp += ConnectorEndMember 'to' 
	ownedRelationship_comp += ConnectorEndMember
;

fragment NaryConnectorPart returns SysML::Connector :
	'(' ownedRelationship_comp += ConnectorEndMember ',' 
	    ownedRelationship_comp += ConnectorEndMember
	    ( ',' ownedRelationship_comp += ConnectorEndMember )* ')'
;

EmptySourceEndMember returns SysML::EndFeatureMembership :
	ownedMemberFeature_comp = EmptySourceEnd
;

EmptySourceEnd returns SysML::Feature :
	{SysML::SourceEnd}
;

/* INTERFACES */

/* Interface Definitions */

InterfaceKeyword :
	'interface'
;

InterfaceDefKeyword :
	InterfaceKeyword 'def'
;

InterfaceDefinition returns SysML::InterfaceDefinition :
	InterfaceDefDeclaration InterfaceDefBody
;

fragment InterfaceDefDeclaration returns SysML::InterfaceDefinition :
	DefinitionPrefix? InterfaceDefKeyword DefinitionDeclaration
;

fragment InterfaceDefBody returns SysML::Type :
	  ';'
	| '{' ( 
		  ownedRelationship_comp += OwnedDocumentation
		| ownedRelationship_comp += NestedDefinitionMember 
		| ownedRelationship_comp += VariantUsageMember
		| ownedRelationship_comp += InterfaceUsageMember
		| ownedRelationship_comp += Import
	)* '}'
;

InterfaceUsageMember returns SysML::FeatureMembership :
	NestedUsageMember | InterfaceEndMember
;

InterfaceEndMember returns SysML::EndFeatureMembership :
	DefinitionMemberPrefix 'end' ownedMemberFeature_comp = PortEndUsage
;

/* Interface Usages */

InterfaceUsageKeyword :
	InterfaceKeyword
;

InterfaceUsage returns SysML::InterfaceUsage :	 
	UsagePrefix? InterfaceUsageKeyword InterfaceUsageDeclaration InterfaceDefBody
;

fragment InterfaceUsageDeclaration returns SysML::InterfaceUsage :
	UsageDeclaration? ( ConnectorKeyword ConnectorPart )? | ConnectorPart
;

/* ALLOCATIONS */

/* Allocation Definitions */

AllocationKeyword :
	'allocation'
;

AllocationDefKeyword :
	AllocationKeyword 'def'
;

AllocationDefinition returns SysML::AllocationDefinition :
	AllocationDefDeclaration ConnectionBody
;

fragment AllocationDefDeclaration returns SysML::AllocationDefinition :
	DefinitionPrefix? AllocationDefKeyword DefinitionDeclaration
;

/* AllocationUsage Usages */

AllocationUsageKeyword :
	AllocationKeyword
;

AllocateKeyword :
	'allocate'
;

AllocationUsage returns SysML::AllocationUsage :	 
	UsagePrefix? AllocationUsageDeclaration ConnectionBody
;

fragment AllocationUsageDeclaration returns SysML::AllocationUsage :
	    AllocationUsageKeyword UsageDeclaration? ( AllocateKeyword ConnectorPart )?
	  | AllocateKeyword ConnectorPart
;

/* INDIVIDUALS */

/* Individual Definitions */

IndividualKeyword :
	'individual'
;

IndividualDefKeyword :
	IndividualKeyword 'def'
;

IndividualDefinition returns SysML::IndividualDefinition :
	DefinitionPrefix? IndividualDefKeyword Definition
	ownedRelationship_comp += LifeClassMembership
;

LifeClassMembership returns SysML::Membership :
	ownedMemberElement_comp = LifeClass
;

LifeClass returns SysML::LifeClass :
	{SysML::LifeClass}
;

/* Individual Usages */

IndividualUsageKeyword :
	IndividualKeyword
;

TimeSliceKeyword :
	'timeslice'
;

SnapshotKeyword :
	'snapshot'
;

IndividualUsage returns SysML::IndividualUsage :
	UsagePrefix? IndividualUsageKeyword Usage
;

IndividualRefUsage returns SysML::IndividualUsage :
	UsagePrefix? ( 'ref' IndividualUsageKeyword | isComposite ?= IndividualUsageKeyword ) Usage
;

TimeSliceUsage returns SysML::IndividualUsage :
	UsagePrefix? TimeSliceKeyword Usage
	ownedRelationship_comp += TimeSliceFeatureMember 
;

TimeSliceRefUsage returns SysML::IndividualUsage :
	UsagePrefix? ( 'ref' TimeSliceKeyword | isComposite ?= TimeSliceKeyword ) Usage 
	ownedRelationship_comp += TimeSliceFeatureMember 
;

TimeSliceFeatureMember returns SysML::FeatureMembership :
	ownedMemberFeature_comp = TimeSliceFeature
;

TimeSliceFeature returns SysML::TimeSliceFeature :
	{SysML::TimeSliceFeature}
;

SnapshotUsage returns SysML::IndividualUsage :
	UsagePrefix? SnapshotKeyword Usage
	ownedRelationship_comp += SnapshotFeatureMember
;

SnapshotRefUsage returns SysML::IndividualUsage :
	UsagePrefix? ( 'ref' SnapshotKeyword | isComposite ?= SnapshotKeyword ) Usage 
	ownedRelationship_comp += SnapshotFeatureMember 
;

SnapshotFeatureMember returns SysML::FeatureMembership :
	ownedMemberFeature_comp = SnapshotFeature
;

SnapshotFeature returns SysML::SnapshotFeature :
	{SysML::SnapshotFeature}
;

/* Individual Succession */

EmptySuccessionMember returns SysML::FeatureMembership :
	ownedMemberFeature_comp = EmptySuccession
;

EmptySuccession returns SysML::Succession :
	'then' ownedRelationship_comp += MultiplicitySourceEndMember 
	ownedRelationship_comp += EmptyTargetEndMember
;

MultiplicitySourceEndMember returns SysML::EndFeatureMembership :
	ownedMemberFeature_comp = MultiplicitySourceEnd
;

MultiplicitySourceEnd returns SysML::Feature :
	{SysML::SourceEnd} ( ownedRelationship_comp += MultiplicityMember )?
;

EmptyTargetEndMember returns SysML::EndFeatureMembership :
	ownedMemberFeature_comp = EmptyTargetEnd
;

EmptyTargetEnd returns SysML::Feature :
	{SysML::TargetEnd}
;

/* ACTION */

/* Action Definitions */

ActionKeyword :
	'action'
;

ActionDefKeyword :
	ActionKeyword 'def' | 'activity'
;

ActionDefinition returns SysML::ActionDefinition :
	DefinitionPrefix? ActionDefKeyword ActionDeclaration ActionBody
;

fragment ActionDeclaration returns SysML::ActionDefinition :
	DefinitionDeclaration ParameterList? 
;

fragment ParameterList returns SysML::Type :
	'(' ( ownedRelationship_comp += ParameterMember 
		( ',' ownedRelationship_comp += ParameterMember )* )? ')'
;

ParameterMember returns SysML::ParameterMembership :
	( direction = FeatureDirection )? ownedMemberParameter_comp = Parameter
;

fragment ActionBody returns SysML::Type :
	    ';' 
		// Note: Using a syntactic predicate here seems to avoid a possible infinite loop
		// while incrementally parsing.
	  | '{' => ActionBodyItem* '}' 
;

fragment ActionBodyItem returns SysML::Type :
	  ownedRelationship_comp += OwnedDocumentation
	| ownedRelationship_comp += NestedDefinitionMember 
	| ownedRelationship_comp += VariantUsageMember
	| ownedRelationship_comp += ParameterFlowUsageMember
	| ownedRelationship_comp += StructureUsageMember
	| ( ownedRelationship_comp += EmptySuccessionMember )?
	  ownedRelationship_comp += IndividualUsageMember
	| ownedRelationship_comp += InitialNodeMember
	  ( ownedRelationship_comp += TargetSuccessionMember )*
	| ( ownedRelationship_comp += EmptySuccessionMember )? 
	  ownedRelationship_comp += ( BehaviorUsageMember | ActionNodeMember )
	  ( ownedRelationship_comp += TargetSuccessionMember )*
	| ownedRelationship_comp += GuardedSuccessionMember
	| ownedRelationship_comp += Import
;

ParameterFlowUsageMember returns SysML::ParameterMembership :
	DefinitionMemberPrefix direction = FeatureDirection 
	ownedMemberParameter_comp = FlowUsageElement
;

InitialNodeMember returns SysML::FeatureMembership :
	DefinitionMemberPrefix 'first' memberFeature = [SysML::Feature|QualifiedName] ';'
	
;

ActionNodeMember returns SysML::FeatureMembership :
	DefinitionMemberPrefix ownedMemberFeature_comp = ActionNode
;

TargetSuccessionMember returns SysML::FeatureMembership :
	DefinitionMemberPrefix ownedMemberFeature_comp = ActionTargetSuccession ';'
;

GuardedSuccessionMember returns SysML::FeatureMembership :
	ownedMemberFeature_comp = GuardedSuccession ';'
;

/* Parameters */

Parameter returns SysML::Usage :
      {SysML::ReferenceUsage} ( ParameterDeclaration | ReferenceUsageKeyword ParameterDeclaration?)
    | {SysML::AttributeUsage} AttributeUsageKeyword ParameterDeclaration?
    | {SysML::ItemUsage} ItemUsageKeyword ParameterDeclaration?
    | {SysML::PartUsage} PartUsageKeyword ParameterDeclaration?
    | {SysML::ViewUsage} ViewUsageKeyword ParameterDeclaration?
    | {SysML::RenderingUsage} RenderingUsageKeyword ParameterDeclaration?
    | {SysML::ActionUsage} ActionUsageKeyword ParameterDeclaration?
    | {SysML::CalculationUsage} CalculationUsageKeyword ParameterDeclaration?
    | {SysML::StateUsage} StateUsageKeyword ParameterDeclaration?
    | {SysML::ConstraintUsage} ConstraintUsageKeyword ParameterDeclaration?
    | {SysML::RequirementUsage} RequirementUsageKeyword ParameterDeclaration?
    | {SysML::AnalysisCaseUsage} AnalysisCaseUsageKeyword ParameterDeclaration?
    | {SysML::VerificationCaseUsage} VerificationCaseUsageKeyword ParameterDeclaration?
    | {SysML::ViewpointUsage} ViewpointUsageKeyword ParameterDeclaration?
;

fragment ParameterDeclaration returns SysML::Usage :
	  Identification ParameterSpecializationPart? 
	| ParameterSpecializationPart
;

fragment ParameterSpecializationPart returns SysML::Feature :
	  ( -> ParameterSpecialization )+ MultiplicityPart? ParameterSpecialization*
	| MultiplicityPart ParameterSpecialization*
;

fragment ParameterSpecialization returns SysML::Feature :
	TypedBy | Subsets | Redefines
;

/* Action Usages */

ActionUsageKeyword :
	ActionKeyword
;

ActionUsage returns SysML::ActionUsage :
	UsagePrefix? ActionUsageKeyword ActionUsageDeclaration ActionBody
;

ActionFlowUsage returns SysML::ActionUsage :
	UsagePrefix? 'ref'? ActionUsageKeyword ActionUsageDeclaration ActionBody
;

ActionRefUsage returns SysML::ActionUsage :
	UsagePrefix? ( 'ref' ActionUsageKeyword | isComposite ?= ActionUsageKeyword ) 
	ActionUsageDeclaration ActionBody
;

PerformActionUsage returns SysML::PerformActionUsage :
	UsagePrefix? 'perform' PerformActionUsageDeclaration ActionBody
;

fragment PerformActionUsageDeclaration returns SysML::ActionUsage :
   	( ownedRelationship_comp += OwnedSubsetting FeatureSpecializationPart?
   	| ActionUsageKeyword UsageDeclaration? )
    ( ValuePart | ActionUsageParameterList )?
;

fragment ActionUsageDeclaration returns SysML::Step :
	  UsageDeclaration? ( ValuePart | ActionUsageParameterList )?
;

fragment ActionUsageParameterList returns SysML::Type :
	'(' ( ownedRelationship_comp += ActionUsageParameterMember 
		  ( ',' ownedRelationship_comp += ActionUsageParameterMember )* )? ')'
;

ActionUsageParameterMember returns SysML::ParameterMembership :
	( direction = FeatureDirection )? ownedMemberParameter_comp = ActionUsageParameter
;

ActionUsageParameter returns SysML::Usage :
	Parameter ValueOrFlowPart? | {SysML::ReferenceUsage} ValueOrFlowPart
;

/* Action Nodes */

ActionNode returns SysML::ActionUsage :
	  SendNode | AcceptNode | ControlNode
;

AcceptNode returns SysML::AcceptActionUsage :
	UsagePrefix? AcceptNodeDeclaration ActionBody
;

fragment AcceptNodeDeclaration returns SysML::ActionUsage :
	( 'action' UsageDeclaration )? 'accept' AcceptParameterPart
;

fragment AcceptParameterPart returns SysML::ActionUsage :
	ownedRelationship_comp += ItemParameterMember
	( 'for' ownedRelationship_comp += NodeParameterMember 
	| ownedRelationship_comp += EmptyParameterMember
	)
;

ItemParameterMember returns SysML::ParameterMembership :
	ownedMemberParameter_comp = ItemParameter
;

ItemParameter returns SysML::ReferenceUsage :
	  Identification? ItemParameterSpecializationPart
    | ownedRelationship_comp += OwnedFeatureTyping ( ownedRelationship_comp += MultiplicityMember )?
;

fragment ItemParameterSpecializationPart returns SysML::Feature :
	  ( -> FeatureSpecialization )+ MultiplicityPart? FeatureSpecialization*
	| MultiplicityPart FeatureSpecialization+
;

SendNode returns SysML::SendActionUsage :
	UsagePrefix? SendNodeDeclaration ActionBody
;

fragment SendNodeDeclaration returns SysML::ActionUsage :
	( 'action' UsageDeclaration )? 'send' ownedRelationship_comp += NodeParameterMember 
	'to' ownedRelationship_comp += NodeParameterMember
;

NodeParameterMember returns SysML::ParameterMembership :
	ownedMemberParameter_comp = NodeParameter
;

NodeParameter returns SysML::Usage :
	{SysML::ReferenceUsage} ownedRelationship_comp += FeatureValue
;

ControlNode returns SysML::ControlNode :
	  MergeNode
	| DecisionNode
	| JoinNode
	| ForkNode
;

MergeNode returns SysML::MergeNode :
	UsagePrefix? isComposite ?= 'merge' UsageDeclaration? ';'
;

DecisionNode returns SysML::DecisionNode :
	UsagePrefix? isComposite ?= 'decide' UsageDeclaration? ';'
;

JoinNode returns SysML::JoinNode :
	UsagePrefix? isComposite ?= 'join' UsageDeclaration? ';'
;

ForkNode returns SysML::ForkNode :
	UsagePrefix? isComposite ?= 'fork' UsageDeclaration? ';'
;

EmptyParameterMember returns SysML::ParameterMembership :
	ownedMemberParameter_comp = EmptyParameter
;

EmptyParameter returns SysML::Usage :
	{SysML::ReferenceUsage}
;

/* Action Succession */

ActionTargetSuccession returns SysML::Feature :
	TargetSuccession | GuardedTargetSuccession | DefaultTargetSuccession
;

TargetSuccession returns SysML::Succession :
	'then' ownedRelationship_comp += MultiplicitySourceEndMember 
	ownedRelationship_comp += ConnectorEndMember
;

GuardedTargetSuccession returns SysML::TransitionUsage :
	ownedRelationship_comp += GuardExpressionMember
	'then' ownedRelationship_comp += TransitionSuccessionMember
;

DefaultTargetSuccession returns SysML::TransitionUsage :
	'else' ownedRelationship_comp += TransitionSuccessionMember
;

GuardedSuccession returns SysML::TransitionUsage :
	SuccessionKeyword ( UsageDeclaration? 'first' )? 
	ownedRelationship_comp += TransitionSourceMember 
	ownedRelationship_comp += GuardExpressionMember
	'then' ownedRelationship_comp += TransitionSuccessionMember
;

/* STATES */

/* State Definitions */

StateKeyword :
	'state'
;

StateDefKeyword :
	StateKeyword 'def'
;

StateDefinition returns SysML::StateDefinition :
	( isAbstract ?= 'abstract')? StateDefKeyword ActionDeclaration StateBody
;

fragment StateBody returns SysML::Type :
	';' | '{' StateBodyPart '}'
;

fragment StateBodyPart returns SysML::Type :
	( ownedRelationship_comp += EntryActionMember
	  ( ownedRelationship_comp += EntryTransitionMember )*
	)?
	( ownedRelationship_comp += DoActionMember )?
	( ownedRelationship_comp += ExitActionMember )?
	
	// Note: Using a syntactic predicate here seems to avoid a possible infinite loop
	// while incrementally parsing.
	=> StateDefBodyItem*
;

fragment StateDefBodyItem returns SysML::Type :
	  ownedRelationship_comp += OwnedDocumentation
	| ownedRelationship_comp += NestedDefinitionMember 
	| ownedRelationship_comp += VariantUsageMember
	| ownedRelationship_comp += ParameterFlowUsageMember
	| ownedRelationship_comp += StructureUsageMember
	| ( ownedRelationship_comp += EmptySuccessionMember )?
	  ownedRelationship_comp += IndividualUsageMember
	| ownedRelationship_comp += BehaviorUsageMember
		( ownedRelationship_comp += TargetTransitionUsageMember )*
	| ownedRelationship_comp += TransitionUsageMember
	| ownedRelationship_comp += Import
;

EntryActionMember returns SysML::StateSubactionMembership :
	DefinitionMemberPrefix kind = EntryActionKind ownedMemberFeature_comp = StateActionUsage
;

EntryActionKind returns SysML::StateSubactionKind :
	'entry'
;

DoActionMember returns SysML::StateSubactionMembership :
	DefinitionMemberPrefix kind = DoActionKind ownedMemberFeature_comp = StateActionUsage
;

DoActionKind returns SysML::StateSubactionKind :
	'do'
;

ExitActionMember returns SysML::StateSubactionMembership :
	DefinitionMemberPrefix kind = ExitActionKind ownedMemberFeature_comp = StateActionUsage
;

ExitActionKind returns SysML::StateSubactionKind :
	'exit'
;

EntryTransitionMember returns SysML::FeatureMembership :
	DefinitionMemberPrefix 
	( ownedMemberFeature_comp = GuardedTargetSuccession 
	| 'then' ownedMemberFeature_comp = TransitionSuccession
	) ';'
;

StateActionUsage returns SysML::ActionUsage :
	EmptyActionUsage ';' | PerformedActionUsage ActionBody
;

EmptyActionUsage returns SysML::ActionUsage :
	{SysML::ActionUsage}
;

PerformedActionUsage returns SysML::ActionUsage :
	  {SysML::PerformActionUsage} PerformActionUsageDeclaration 
	| {SysML::AcceptActionUsage} AcceptNodeDeclaration 
	| {SysML::SendActionUsage} SendNodeDeclaration
;

TransitionUsageMember returns SysML::FeatureMembership :
	DefinitionMemberPrefix ownedMemberFeature_comp = TransitionUsage ';'
;

TargetTransitionUsageMember returns SysML::FeatureMembership :
	DefinitionMemberPrefix ownedMemberFeature_comp = TargetTransitionUsage ';'
;

/* State Usages */

StateUsageKeyword :
	StateKeyword
;

StateUsage returns SysML::StateUsage :
	UsagePrefix? StateUsageKeyword ActionUsageDeclaration StateBody
;

StateFlowUsage returns SysML::StateUsage :
	UsagePrefix? 'ref'? StateUsageKeyword ActionUsageDeclaration StateBody
;

StateRefUsage returns SysML::StateUsage :
	UsagePrefix? ( 'ref' StateUsageKeyword | isComposite ?= StateUsageKeyword ) 
	ActionUsageDeclaration StateBody
;

ExhibitStateUsage returns SysML::ExhibitStateUsage :
    UsagePrefix? 'exhibit' 
    ( ownedRelationship_comp += OwnedSubsetting FeatureSpecializationPart?
    | StateUsageKeyword UsageDeclaration?
    )
    ( ValuePart | ActionUsageParameterList )? StateBody
;

/* Transition Usages */

TransitionUsageKeyword :
	'transition'
;

TransitionUsage returns SysML::TransitionUsage :
	TransitionUsageKeyword ( UsageDeclaration? 'first' )? 
	ownedRelationship_comp += TransitionSourceMember 
	( ownedRelationship_comp += EmptyImport
	  ownedRelationship_comp += TriggerActionMember )?
	( ownedRelationship_comp += GuardExpressionMember )?
	( ownedRelationship_comp += EffectBehaviorMember )?
	'then' ownedRelationship_comp += TransitionSuccessionMember
;

TargetTransitionUsage returns SysML::TransitionUsage :
	( ownedRelationship_comp += EmptyImport
	  ownedRelationship_comp += TriggerActionMember )?
	( ownedRelationship_comp += GuardExpressionMember )?
	( ownedRelationship_comp += EffectBehaviorMember )?
	'then' ownedRelationship_comp += TransitionSuccessionMember
;

EmptyImport returns SysML::Import :
	{SysML::Import}
;

TransitionSourceMember returns SysML::Membership :
	memberElement = [SysML::Feature|QualifiedName]
;

TriggerActionMember returns SysML::TransitionFeatureMembership :
	kind = TriggerFeatureKind ownedMemberFeature_comp = TriggerAction
;

enum TriggerFeatureKind returns SysML::TransitionFeatureKind :
	trigger = 'accept'
;

TriggerAction returns SysML::AcceptActionUsage :
	AcceptParameterPart
;

GuardExpressionMember returns SysML::TransitionFeatureMembership :
	kind = GuardFeatureKind ownedMemberFeature_comp = OwnedExpression
;

enum GuardFeatureKind returns SysML::TransitionFeatureKind :
	guard = 'if'
;

EffectBehaviorMember returns SysML::TransitionFeatureMembership :
	kind = EffectFeatureKind ownedMemberFeature_comp = EffectBehaviorUsage
;

enum EffectFeatureKind returns SysML::TransitionFeatureKind :
	effect = 'do'
;

EffectBehaviorUsage returns SysML::ActionUsage :
	  EmptyActionUsage | PerformedActionUsage ( '{' ActionBodyItem* '}' )?
;

TransitionSuccessionMember returns SysML::Membership :
	ownedMemberElement_comp = TransitionSuccession
;

TransitionSuccession returns SysML::Succession :
	ownedRelationship_comp += EmptySourceEndMember 
	ownedRelationship_comp += ConnectorEndMember
;

/* CALCULATIONS */

/* Calculation Definitions */

CalculationKeyword :
	'calc'
;

CalculationDefKeyword :
	CalculationKeyword 'def'
;

CalculationDefinition returns SysML::CalculationDefinition :
	DefinitionPrefix? CalculationDefKeyword CalculationDeclaration 
	( CalculationBody 
	| '=' ownedRelationship_comp += ResultExpressionMember ';'
	)
;

fragment CalculationDeclaration returns SysML::CalculationDefinition :
	DefinitionDeclaration ( ParameterList ReturnParameterPart? )?
;

fragment ReturnParameterPart returns SysML::Type :
	ownedRelationship_comp += ReturnParameterMember
;

ReturnParameterMember returns SysML::ReturnParameterMembership :
	'return'? ownedMemberParameter_comp = Parameter
;

fragment CalculationBody returns SysML::Type :
	    ';' 
	  | '{' => CalculationBodyItem*
	        ( ownedRelationship_comp += ResultExpressionMember )?
	    '}' 
;

fragment CalculationBodyItem returns SysML::Type :
	  ActionBodyItem
	| ownedRelationship_comp += ReturnParameterFlowUsageMember
;

ReturnParameterFlowUsageMember returns SysML::ReturnParameterMembership :
	DefinitionMemberPrefix 'return' 
	ownedMemberParameter_comp = FlowUsageElement
;

@Override
ResultExpressionMember returns SysML::ResultExpressionMembership :
	DefinitionMemberPrefix ownedResultExpression_comp = OwnedExpression
;

/* Calculation Usages */

CalculationUsageKeyword :
	CalculationKeyword
;

CalculationUsage returns SysML::CalculationUsage :
	UsagePrefix? CalculationUsageKeyword CalculationUsageDeclaration CalculationBody
;

CalculationFlowUsage returns SysML::CalculationUsage :
	UsagePrefix? 'ref'? CalculationUsageKeyword CalculationUsageDeclaration CalculationBody
;

CalculationRefUsage returns SysML::CalculationUsage :
	UsagePrefix? ( 'ref' CalculationUsageKeyword | isComposite ?= CalculationUsageKeyword ) 
	CalculationUsageDeclaration CalculationBody
;

fragment CalculationUsageDeclaration returns SysML::Step :
	  UsageDeclaration? ( ValuePart | CalculationUsageParameterPart )?
;

fragment CalculationUsageParameterPart returns SysML::Feature :
	  ActionUsageParameterList CalculationReturnParameterPart?
;

fragment CalculationReturnParameterPart returns SysML::Feature :
	ownedRelationship_comp += CalculationReturnParameterMember
;

CalculationReturnParameterMember returns SysML::ReturnParameterMembership :
	'return'? ownedMemberParameter_comp = ActionUsageParameter
;

/* CONSTRAINTS */

/* Constraint Definitions */

ConstraintKeyword :
	'constraint'
;

ConstraintDefKeyword :
	ConstraintKeyword 'def'
;

ConstraintDefinition returns SysML::ConstraintDefinition :
	DefinitionPrefix? ConstraintDefKeyword ConstraintDeclaration CalculationBody  
;

fragment ConstraintDeclaration returns SysML::ConstraintDefinition :
	DefinitionDeclaration ParameterList? EmptyReturnParameterPart
;

fragment EmptyReturnParameterPart returns SysML::Type :
	ownedRelationship_comp += EmptyReturnParameterMember
;

EmptyReturnParameterMember returns SysML::ReturnParameterMembership :
	ownedMemberParameter_comp = EmptyParameter
;

/* Constraint Usages */

ConstraintUsageKeyword :
	ConstraintKeyword
;

ConstraintUsage returns SysML::ConstraintUsage :
	UsagePrefix? ConstraintUsageKeyword CalculationUsageDeclaration CalculationBody
;

ConstraintFlowUsage returns SysML::ConstraintUsage :
	UsagePrefix? 'ref'? ConstraintUsageKeyword CalculationUsageDeclaration CalculationBody
;

ConstraintRefUsage returns SysML::ConstraintUsage :
	UsagePrefix? ( 'ref' ConstraintUsageKeyword | isComposite ?= ConstraintUsageKeyword ) 
	CalculationUsageDeclaration CalculationBody
;

AssertConstraintUsage returns SysML::AssertConstraintUsage :
	UsagePrefix? 'assert'
    ( ownedRelationship_comp += OwnedSubsetting FeatureSpecializationPart?
    | ConstraintUsageKeyword UsageDeclaration? 
    )
    CalculationUsageParameterPart? InvariantPart CalculationBody
;

fragment InvariantPart returns SysML::Invariant :
	ownedRelationship_comp += TrueLiteralMember
;

TrueLiteralMember returns SysML::FeatureMembership :
	ownedMemberFeature_comp = TrueLiteralExpression
;

TrueLiteralExpression returns SysML::LiteralBoolean :
	{SysML::LiteralBoolean}
;

/* REQUIREMENTS */

/* Requirement Definitions */

RequirementKeyword :
	'requirement'
;

RequirementDefKeyword :
	RequirementKeyword 'def'
;

RequirementDefinition returns SysML::RequirementDefinition :
	DefinitionPrefix? RequirementDefKeyword ConstraintDeclaration RequirementBody  
;

fragment RequirementBody returns SysML::Type :
	  ';' | '{' => RequirementBodyItem* '}'
;

fragment RequirementBodyItem returns SysML::Type :
	  DefinitionBodyItem
	| ownedRelationship_comp += SubjectMember
	| ownedRelationship_comp += RequirementConstraintMember
	| ownedRelationship_comp += RequirementVerificationMember
;

SubjectMember returns SysML::SubjectMembership :
	DefinitionMemberPrefix ownedSubjectParameter_comp = SubjectUsage
;

SubjectUsage returns SysML::ReferenceUsage :
	'subject' Usage
;

RequirementConstraintMember returns SysML::RequirementConstraintMembership :
	DefinitionMemberPrefix kind = RequirementConstraintKind 
	ownedMemberFeature_comp = RequirementConstraintUsage
;

enum RequirementConstraintKind returns SysML::RequirementConstraintKind :
	assumption = 'assume' | requirement = 'require'
;

RequirementConstraintUsage returns SysML::ConstraintUsage :
      ownedRelationship_comp += OwnedSubsetting FeatureSpecialization* 
      CalculationUsageParameterPart? RequirementBody
    | 'constraint' CalculationUsageDeclaration CalculationBody
;

/* Requirement Usages */

RequirementUsageKeyword :
	RequirementKeyword
;

RequirementUsage returns SysML::RequirementUsage :
	UsagePrefix? RequirementUsageKeyword CalculationUsageDeclaration RequirementBody
;

RequirementFlowUsage returns SysML::RequirementUsage :
	UsagePrefix? 'ref'? RequirementUsageKeyword CalculationUsageDeclaration RequirementBody
;

RequirementRefUsage returns SysML::RequirementUsage :
	UsagePrefix? ( 'ref' RequirementUsageKeyword | isComposite ?= RequirementUsageKeyword ) 
	CalculationUsageDeclaration RequirementBody
;

SatisfyRequirementUsage returns SysML::SatisfyRequirementUsage :
	UsagePrefix? 'satisfy'
	( ownedRelationship_comp += OwnedSubsetting FeatureSpecializationPart?
    | RequirementUsageKeyword UsageDeclaration? 
    )
    ( ValuePart | ActionUsageParameterList )?
    ( 'by' ownedRelationship_comp += SatisfactionConnectorMember )?
    InvariantPart RequirementBody
;

SatisfactionConnectorMember returns SysML::Membership :
	ownedMemberElement_comp = SatisfactionConnector
;

SatisfactionConnector returns SysML::BindingConnector :
	ownedRelationship_comp += EmptySourceEndMember ownedRelationship_comp += ConnectorEndMember
;

/* CASES */

/* Case Definitions */

CaseKeyword :
	'case'
;

CaseDefKeyword :
	CaseKeyword 'def'
;

CaseDefinition returns SysML::CaseDefinition :
	DefinitionPrefix? CaseDefKeyword CalculationDeclaration CaseBody
;

fragment CaseBody returns SysML::Type :
	    ';' 
	  | '{' => CaseBodyItem*
	        ( ownedRelationship_comp += ResultExpressionMember )?
	    '}' 
;

fragment CaseBodyItem returns SysML::Type :
	  CalculationBodyItem
	| ownedRelationship_comp += SubjectMember 
	| ownedRelationship_comp += ObjectiveMember
	
;

ObjectiveMember returns SysML::ObjectiveMembership :
	DefinitionMemberPrefix 'objective' ownedObjectiveRequirement_comp = ObjectiveRequirementUsage
;

ObjectiveRequirementUsage returns SysML::RequirementUsage :
	CalculationUsageDeclaration RequirementBody
;

/* Case Usages */

CaseUsageKeyword :
	CaseKeyword
;

CaseUsage returns SysML::CaseUsage :
	UsagePrefix? CaseUsageKeyword CalculationUsageDeclaration CaseBody
;

CaseFlowUsage returns SysML::CaseUsage :
	UsagePrefix? 'ref'? CaseUsageKeyword CalculationUsageDeclaration CaseBody
;

CaseRefUsage returns SysML::CaseUsage :
	UsagePrefix? ( 'ref' CaseUsageKeyword | isComposite ?= CaseUsageKeyword ) 
	CalculationUsageDeclaration CaseBody
;

/* ANALYSIS CASES */

AnalysisCaseKeyword :
	'analysis'
;

AnalysisCaseDefKeyword :
	AnalysisCaseKeyword 'def'
;

AnalysisCaseUsageKeyword :
	AnalysisCaseKeyword
;

AnalysisCaseDefinition returns SysML::AnalysisCaseDefinition :
	DefinitionPrefix? AnalysisCaseDefKeyword CalculationDeclaration CaseBody
;

AnalysisCaseUsage returns SysML::AnalysisCaseUsage :
	UsagePrefix? AnalysisCaseUsageKeyword CalculationUsageDeclaration CaseBody
;

AnalysisCaseFlowUsage returns SysML::AnalysisCaseUsage :
	UsagePrefix? 'ref'? AnalysisCaseUsageKeyword CalculationUsageDeclaration CaseBody
;

AnalysisCaseRefUsage returns SysML::AnalysisCaseUsage :
	UsagePrefix? ( 'ref' AnalysisCaseUsageKeyword | isComposite ?= AnalysisCaseUsageKeyword ) 
	CalculationUsageDeclaration CaseBody
;

/* VERIFICATION CASES */

VerificationCaseKeyword :
	'verification'
;

VerificationCaseDefKeyword :
	VerificationCaseKeyword 'def'
;

VerificationCaseUsageKeyword :
	VerificationCaseKeyword
;

VerificationCaseDefinition returns SysML::VerificationCaseDefinition :
	DefinitionPrefix? VerificationCaseDefKeyword CalculationDeclaration CaseBody
;

VerificationCaseUsage returns SysML::VerificationCaseUsage :
	UsagePrefix? VerificationCaseUsageKeyword CalculationUsageDeclaration CaseBody
;

VerificationCaseFlowUsage returns SysML::VerificationCaseUsage :
	UsagePrefix? 'ref'? VerificationCaseUsageKeyword CalculationUsageDeclaration CaseBody
;

VerificationCaseRefUsage returns SysML::VerificationCaseUsage :
	UsagePrefix? ( 'ref' VerificationCaseUsageKeyword | isComposite ?= VerificationCaseUsageKeyword ) 
	CalculationUsageDeclaration CaseBody
;

RequirementVerificationMember returns SysML::RequirementVerificationMembership :
	DefinitionMemberPrefix kind = RequirementVerificationKind 
	ownedRequirement_comp = RequirementVerificationUsage
;

enum RequirementVerificationKind returns SysML::RequirementConstraintKind :
	requirement = 'verify'
;

RequirementVerificationUsage returns SysML::RequirementUsage :
      ownedRelationship_comp += OwnedSubsetting FeatureSpecialization* 
      CalculationUsageParameterPart? RequirementBody
    | 'requirement' CalculationUsageDeclaration RequirementBody
;

/* VIEWS */

/* View Definitions */

ViewKeyword :
	'view'
;

ViewDefKeyword :
	ViewKeyword 'def'
;

ViewDefinition returns SysML::ViewDefinition :
	DefinitionPrefix? ViewDefKeyword DefinitionDeclaration ViewDefinitionBody
;

fragment ViewDefinitionBody returns SysML::ViewDefinition :
	  ';' 
	| '{' ViewDefinitionBodyItem* '}'
;

fragment ViewDefinitionBodyItem returns SysML::ViewDefinition :
	  DefinitionBodyItem
	| ownedRelationship_comp += ElementFilterMember
;

/* View Usages */

ViewUsageKeyword :
	ViewKeyword
;

ViewUsage returns SysML::ViewUsage :
	UsagePrefix? ViewUsageKeyword UsageDeclaration? ValueOrFlowPart? ViewBody
;

ViewFlowUsage returns SysML::ViewUsage :
	UsagePrefix? 'ref'? ViewUsageKeyword UsageDeclaration? ValueOrFlowPart? ViewBody
;

ViewRefUsage returns SysML::ViewUsage :
	UsagePrefix? ( 'ref' ViewUsageKeyword | isComposite ?= ViewUsageKeyword ) 
	UsageDeclaration? ValueOrFlowPart? ViewBody
;

fragment ViewBody returns SysML::ViewUsage :
	';' | '{' ViewBodyItem* '}'
;


fragment ViewBodyItem returns SysML::ViewUsage :
	  DefinitionBodyItem
	| ownedRelationship_comp += ElementFilterMember
	| ownedRelationship_comp += Expose
;

Expose returns SysML::Expose :
	( ownedRelationship_comp += PrefixDocumentation )*
	( visibility = BasicVisibilityIndicator )?  
	'expose' ( ImportedNamespace | ImportedFilterPackage ) ';'
;

/* VIEWPOINTS */

ViewpointKeyword :
	'viewpoint'
;

ViewpointDefKeyword :
	ViewpointKeyword 'def'
;

ViewpointUsageKeyword :
	ViewpointKeyword
;

ViewpointDefinition returns SysML::ViewpointDefinition :
	DefinitionPrefix? ViewpointDefKeyword ConstraintDeclaration RequirementBody  
;

ViewpointUsage returns SysML::ViewpointUsage :
	UsagePrefix? ViewpointUsageKeyword CalculationUsageDeclaration RequirementBody
;

ViewpointFlowUsage returns SysML::ViewpointUsage :
	UsagePrefix? 'ref'? ViewpointUsageKeyword CalculationUsageDeclaration RequirementBody
;

ViewpointRefUsage returns SysML::ViewpointUsage :
	UsagePrefix? ( 'ref' ViewpointUsageKeyword | isComposite ?= ViewpointUsageKeyword ) 
	CalculationUsageDeclaration RequirementBody
;

/* RENDERINGS */

RenderingKeyword :
	'rendering'
;

RenderingDefKeyword :
	RenderingKeyword 'def'
;

RenderingDefinition returns SysML::RenderingDefinition :
	DefinitionPrefix? RenderingDefKeyword Definition
;

RenderingUsageKeyword :
	RenderingKeyword
;

RenderingUsage returns SysML::RenderingUsage :
	UsagePrefix? RenderingUsageKeyword Usage
;

RenderingFlowUsage returns SysML::RenderingUsage :
	UsagePrefix? 'ref' RenderingUsageKeyword Usage
;

RenderingRefUsage returns SysML::RenderingUsage :
	UsagePrefix? ( 'ref' RenderingUsageKeyword | isComposite ?= RenderingUsageKeyword ) Usage
;

/* EXPRESSIONS */
@Override
OwnedExpression returns SysML::Expression :
	 KerMLExpressions::OwnedExpression | QueryPathExpression
;

@Override
BodyParameter returns SysML::ReferenceUsage :
	{SysML::ReferenceUsage} ( TypedBy MultiplicityPart? | MultiplicityPart TypedBy? )?
;

/* QUERY PATH EXPRESSIONS */

QueryPathExpression returns SysML::Expression :
	QueryHeadExpression
	( '[' {SysML::QueryQualifierExpression.operand_comp += current}
         ( ownedRelationship_comp += BodyExpressionMember ) ']' )?
	( '/' {SysML::QueryPathStepExpression.operand_comp += current}
		operand_comp += QueryNameExpression
		( '[' {SysML::QueryQualifierExpression.operand_comp += current}
	         ( ownedRelationship_comp += BodyExpressionMember ) ']' )?
	)*
;

QueryNameExpression returns SysML::QueryPathExpression :
    /* isParent ?= '..'
    | isDescendants ?= '/'
    | @traversal
    */
    ownedRelationship_comp += FeatureReferenceMember
;

QueryHeadExpression returns SysML::QueryPathExpression :
	'./' ownedRelationship_comp += FeatureReferenceMember
;