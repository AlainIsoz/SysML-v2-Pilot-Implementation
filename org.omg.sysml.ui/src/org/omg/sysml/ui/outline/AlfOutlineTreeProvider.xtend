/*
 * generated by Xtext 2.12.0
 */
package org.omg.sysml.ui.outline

import org.eclipse.xtext.ui.editor.outline.impl.DefaultOutlineTreeProvider
import org.eclipse.xtext.ui.editor.outline.IOutlineNode
import org.omg.sysml.lang.sysml.Element
import org.omg.sysml.lang.sysml.Feature
import org.omg.sysml.lang.sysml.Generalization
import org.omg.sysml.lang.sysml.SysMLPackage
import org.omg.sysml.lang.sysml.Membership
import org.omg.sysml.lang.sysml.Redefinition
import org.omg.sysml.lang.sysml.Subset
import org.omg.sysml.lang.sysml.Import
import org.omg.sysml.lang.sysml.ConnectorEnd

/**
 * Customization of the default outline structure.
 *
 * See https://www.eclipse.org/Xtext/documentation/310_eclipse_support.html#outline
 */
class AlfOutlineTreeProvider extends DefaultOutlineTreeProvider {

	def String _text(Element element) {
		var text = element.eClass.name;
		if (element.name !== null) {
			text += ' ' + element.name;
		} else if (element instanceof Membership) {
			var member = element as Membership;
			if (member.visibility !== null) {
				text += ' ' + member.visibility;
			}
			if (member.memberName !== null) {
				text += ' ' + member.memberName
			} else if (member.ownedMemberElement !== null) {
				text += ' owns'
				if (member.ownedMemberElement.name !== null) {
					text += ' ' + member.ownedMemberElement.name;
				}
			} else if (member.memberElement !== null) {
				if (member.memberElement.name !== null) {
					text += ' ' + member.memberElement.name;
				}
			}
		} else if (element instanceof Import) {
			var import = element as Import
			if (import.visibility !== null) {
				text += ' ' + import.visibility
			}
			if (import.importedPackage !== null && 
				import.importedPackage.name !== null) {
				text += ' ' + import.importedPackage.name
			}
		}
		text 
	}
	
	def boolean _isLeaf(Membership membership) {
		false
	}
	
	def void _createChildren(IOutlineNode parentNode, Membership membership) {
		super._createChildren(parentNode, membership)
		var memberElement = membership.memberElement;
		if (membership.ownedMemberElement === null && 
				memberElement !== null) {
			createEObjectNode(parentNode, memberElement, 
				memberElement._image, memberElement._text, 
				membership.membershipOwningPackage == memberElement || memberElement._isLeaf
			)
		}
	}
	
	def boolean _isLeaf(Import _import) {
		_import.importedPackage === null && _import.ownedElement.isEmpty
	}
	
	def void _createChildren(IOutlineNode parentNode, Import _import) {
		super._createChildren(parentNode, _import)
		var importedPackage = _import.importedPackage;
		if (importedPackage !== null) {
			createEObjectNode(parentNode, importedPackage, 
				importedPackage._image, importedPackage._text, 
				_import.importOwningPackage == importedPackage || importedPackage._isLeaf
			)
		}
	}
	
	def boolean _isLeaf(Feature feature) {
		super._isLeaf(feature) && feature.referencedType.isEmpty
	}
	
	def void _createChildren(IOutlineNode parentNode, Feature feature) {
		var referencedTypes = feature.referencedType
		if (!referencedTypes.isEmpty) {
			createEStructuralFeatureNode(parentNode, feature, 
				SysMLPackage.Literals.FEATURE__REFERENCED_TYPE, 
				referencedTypes._image, "Type", false
			)
		}
		super._createChildren(parentNode, feature)
	}

	def boolean _isLeaf(Generalization generalization) {
		generalization.general === null
	}
	
	def void _createChildren(IOutlineNode parentNode, Generalization generalization) {
		if (generalization.general !== null) {
			createEObjectNode(parentNode, generalization.general, 
				generalization.general._image, generalization.general._text, 
				generalization.owner == generalization.general || generalization.general._isLeaf
			)
		}
	}
	
	def boolean _isLeaf(Redefinition redefinition) {
		redefinition.redefinedFeature === null
	}

	def void _createChildren(IOutlineNode parentNode, Redefinition redefinition) {
		if (redefinition.redefinedFeature !== null) {
			createEObjectNode(parentNode, redefinition.redefinedFeature, 
				redefinition.redefinedFeature._image, redefinition.redefinedFeature._text, 
				redefinition.owner == redefinition.redefinedFeature || redefinition.redefinedFeature._isLeaf
			)
		}
	}

	def boolean _isLeaf(Subset subset) {
		subset.subsettedFeature === null
	}

	def void _createChildren(IOutlineNode parentNode, Subset subset) {
		if (subset.subsettedFeature !== null) {
			createEObjectNode(parentNode, subset.subsettedFeature, 
				_image(subset.subsettedFeature), subset.subsettedFeature._text, 
				subset.owner == subset.subsettedFeature || subset.subsettedFeature._isLeaf
			)
		}
	}
	
	def boolean _isLeaf(ConnectorEnd connectorEnd) {
		connectorEnd.end === null && connectorEnd.feature === null;
	}
	
	def void _createChildren(IOutlineNode parentNode, ConnectorEnd connectorEnd) {
		if (connectorEnd.end !== null) {
			createEObjectNode(parentNode, connectorEnd.end, 
				_image(connectorEnd.end), "end " + connectorEnd.end._text, 
				connectorEnd.end.owner == connectorEnd.end || connectorEnd.end._isLeaf
			)
		}
		if (connectorEnd.feature !== null) {
			createEObjectNode(parentNode, connectorEnd.feature, 
				_image(connectorEnd.feature), "feature " + connectorEnd.feature._text, 
				connectorEnd.feature.owner == connectorEnd.feature || connectorEnd.feature._isLeaf
			)
		}
	}

}
